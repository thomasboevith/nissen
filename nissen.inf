!% -~S
!% -s
!% +language_name=Danish
!% +include_path=./inform6lib-6.11,./danish-inform
!% -v5
!% $MAX_VERBS=255
Include "DanAlpha";
Release 4;
Serial "171207";
Array UUID_ARRAY string "UUID://74857675-F93C-45F6-8838-7DD5D8E60EF0//";
#Ifdef UUID_ARRAY; #Endif;
Array TUID_ARRAY string "TUID://zvtmbwxgon32lgjh//";
#Ifdef TUID_ARRAY; #Endif;
Constant Story "På loftet sidder nissen";
Constant Headline "^En interaktiv nissefiktion af Thomas Bøvith^";
!Constant MANUAL_PRONOUNS;
Constant MAX_SCORE  24;
Constant OK_SCORE = 22;
Constant MAX_CARRIED = 3;
Constant NOISY_DIR_TOS;
Replace DrawStatusLine; ! For customized status line
Replace Refers; ! For GoNeighbor
Include "Parser";
! ============================================================================ !
!Include "librarymessages_extensions.inf";
Object  LibraryMessages
 with   before [;
         Jump:
            print "Du gør et hop med din krop";
            if (location == room_loftet && rotter.friendly == false &&
                risengroed.announced) ", men rotterne er ligeglade og omringer
                dig kortvarigt med deres runddans. ~Jeg henter katten, hvis I
                ikke holder op!~, truer du.";
            ".";
         Look:
            if (lm_n == 7 && location == room_kaelderen && staalrist has open)
                "Du kigger ned i det snævre afløb. Du kan ikke se, hvad der
                venter dig dernede.";
            if (lm_n == 7 && location == room_loftet)
                "Du kigger ned gennem lemmen for at sikre dig, at der er fri bane
                ned i huset. Det er fri bane.";
         Take:
            if (lm_n == 12 && action_to_be == ##Take)
                "Du er en stærk nisse, men kan trods alt kun bære tre ting ad
                gangen af gangen.";
         PutOn:
            if (lm_n == 3 && second == overvaagningskamera && noun ~= pind) "Kameraet er uden for din rækkevidde.";
            if (lm_n == 3 && second == roegalarm && noun ~= pind) "Røgalarmen er uden for din rækkevidde.";
         ThrowAt:
            if (lm_n == 1 && second == overvaagningskamera && noun ~= pind) "Det ville ikke virke.";
            if (lm_n == 1 && second == roegalarm && noun ~= pind) "Det ville ikke virke.";
         Miscellany:
!            if (lm_n == 38 && teksteventyr.play_state > 0)
!                "Du bliver grebet af teksteventyret, hvori du ender
!		op i en underjordisk hule, hvor du skal samle en
!		masse skatte. Men du er alt for sulten til at du kan
!		forsætte med at spille ret meget længere, så du
!		gemmer spillet og afslutter det.";
            if (lm_n == 17)
                "Det er bælgmørkt her, og du kan ikke se en hånd for
 		dig.";
	    if (lm_n == 38)
		"Det ved jeg ikke helt, hvordan man gør.";
	 Sing: "Du nynner en lille julemelodi.";
	 Buy:  "Julen varer længe, koster mange penge.";
	 Swim: "Du er for sulten til at gå i vandet.";
	 Strong, Mild: "Sådan taler rigtige nisser ikke.";
	 Sleep: "Du er for sulten til at sove nu.";
	 Kiss: "Nå, ja, julen er hjerternes fest -- med eller uden
 risengrød.";
	 Score: if (deadflag) print "I dette spil fik du "; else print "Du har ";
	    print TotalScore(); print " ud af "; print MAX_SCORE;
 	    print " point og "; print TotalBonus(), " bonuspoint i "; print turns; print " træk";
	    return 1;
	];
! ============================================================================ !
Include "VerbLib";
! ============================================================================ !
!Include "GoNeighbour";
! --------------------------------------------------------------------------
! GONEIGHBOUR
!
! Version 0.20, written by Alan Trewartha      alan@alant.demon.co.uk
! Last updated 3rd Feb 2001
!
! SUMMARY
! This extension makes extra use of room 'name' properties, which are
! usually only used to produce a "you do not need to refer to that" sort
! of message. It allows the objects in the Compass (n_obj, e_obj, etc)
! to take on the 'name's of the location that the player would get to by
! GOing in that direction. i.e. if the n_to of the current location leads
! to a location with the word 'farm' in its name property, then "GO FARM"
! will be the same as "GO NORTH" and "FARM" is the same as "NORTH".
!
! More precisely the compass object will take on the neighbouring
! locations 'name' property if that location has either the 'visited' or
! 'transparent' attribute but not if it is 'concealed'. The compass object
! also takes on words in a property called "external_name", regardless.
!
! --------------------------------------------------------------------------
! HOW TO USE GONEIGHBOUR
!
! Before include "Parser" add the line "Replace Refers;"
! Include "GoNeighbour" any point after "Parser"
!
! If you have "noisy" *_to properties then add "Constant NOISY_DIR_TOS".
! This means that LeadsTo() won't call routines to see what they return.
!
! When writing location or "Room" objects, keep in mind
!    * transparent attribute  (to allow 'name' words even if not visited)
!    * concealed attribute (to disallow 'name' words even if visited)
!    * name property
!    * external_name property (YOU MUST USE SINGLE QUOTES!!)
!
! Optionally it's quite nice to add a little extra bit of grammar so that
! it accepts "GO TO FARM" "GO TOWARDS FARM":
!
!    Extend 'go' last
!    * 'to'/'toward'/'towards' noun -> Go;
!
! --------------------------------------------------------------------------
! EXAMPLE CODE
!
! Room_2 can be got to with "GO GREEN" before you've been there. You can see
! the green-ness from Room_1, so it's in the external_name property. You can
! only use "GO TRIANGLE" once you've been there.
!
! Object Room_1 "Red Circle Room"
!   with description
!           "The walls here are red. There is a circle on the ceiling.
!            To the north is a green room",
!        name "red" "circle",
!        external_name 'red',
!        n_to Room_2,
!   has  light;
!
! Object Room_2 "Green Triangle Room"
!   with description
!           "The walls here are green. There is a triangle on the ceiling.
!            To the south is a red room.",
!        name "green" "triangle",
!        external_name 'green',
!        s_to Room_1,
!   has  light;
!
! --------------------------------------------------------------------------


! A new common property
Property external_name;

! The refers patch
[ Refers obj wnum   wd i;
  if (obj==0) rfalse;

    i = wn; wn = wnum; wd = NextWordStopped(); wn = i;

    ! First the default
    if (WordInProperty(wd,obj,name)) rtrue;


    ! Then if obj has 'external_name' try that too
    if (obj provides external_name)
        if (WordInProperty(wd,obj,external_name)) rtrue;

  ! Now the trick.
  ! If obj is in compass, then try the object it leads to
    if (parent(obj)==Compass) {

      i=location; if (i==thedark) i=real_location;
      i=LeadsTo(obj,i); if (i==0) rfalse;

      ! First the external_name
      if (WordInProperty(wd,i,external_name)) rtrue;

      ! If the room is concealed forget it...
      if (i has concealed) rfalse;

      ! If the room is unvisited and "opaque" forget it
      if (i hasnt visited && i hasnt transparent) rfalse;

      ! Otherwise check name
      if (WordInProperty(wd,i,name)) rtrue;
  }
  rfalse;
];



! LeadsTo returns either false (0), or the location that the player would
! come to by GOing in the direction given. This is a cut down version of
! the same routione provided in 'MoveClass' so the "Ifndef" makes sure that
! this is only compiled if it isn't already defined.

Ifndef LeadsTo;
Ifndef NOISY_DIR_TOS;
Message "** LeadsTo assuming quiet *_to (NOISY_DIR_TOS not defined) **";
Endif;
[ LeadsTo direction thisroom k tmp tmp2;
   if (~~(direction provides door_dir)) rfalse;
   if (~~(thisroom provides direction.door_dir)) rfalse;
   k=thisroom.(direction.door_dir);

   #ifdef NOISY_DIR_TOS;
     if (ZRegion(k)==2) rfalse;
   #endif;

   #ifndef NOISY_DIR_TOS;
     if (ZRegion(k)==2)
         k=k();
   #endif;

   if (ZRegion(k)~=1) rfalse;
   if (k has door)
   { tmp=parent(k);
     move k to thisroom;
     tmp2=k.door_to();
     if (tmp==0)
       remove k;
     else
       move k to tmp;
     k=tmp2;
   }
   return k;
];
Endif;

! ============================================================================ !
Include "DanRout";
! ============================================================================ !
!Include "classes.inf";
Class   Room
 with   before [;
         Examine:
            if (location hasnt light) "Det er for mørkt til at se noget som helst.";
	 Smell:
	    if (noun == nothing) {
		if (TotalScore() < OK_SCORE && player notin room_loftet) {
	    	    if (roegetsild in player) "Du kan mest af alt lugte den røgede sild, som du bærer på.";
	    	} else { print "En";
		    if (player notin room_loftet) print " fjern"; else print " dejlig";
		    print " duft af risengrød med kanelsukker";
		    if (roegetsild in player && player notin room_loftet) " blandet med den kraftige
		    lugt af den røgede sild, som du bærer på.";
		    else ".";
	    	}
	    }
	 Xyzzy:
	    "Du siger det magiske ord, men det virker nok ikke her i huset.";
        ],
 has    light transparent;

Class   LabyrintRoom
 with   labyrintroomnum 0,
        before [;
         Listen:
            if (self.labyrintroomnum == 11 && stenskred.appeared == false) "Æbleskiverne udsender en sært knitrende lyd.";
            if (self.labyrintroomnum == 8) "Lyden af rindende væske.";
            if (self.labyrintroomnum == 11 && stenskred.appeared) "Lavastenene udsender en knitrende lyd, som de gløder der.";
            if (self.labyrintroomnum == 15 && drage in room_labyrint_15) "Den spyende ild fra dragen udsender en støj, som er øredøvende høj.";
            if (self.labyrintroomnum == 15 && enhjoerning in room_labyrint_15) "Enhjørningen vrinsker i et lyst toneleje fra tid til anden.";
            if (self.labyrintroomnum == 14) "Regnbuen udsender en lyd, som hvis et uendelig stort symfoniorkester spillede alle universets klange samtidig.";
            if (self.labyrintroomnum == 12) "Lugten af success.";
            if (self.labyrintroomnum == 14) "Afgrunden suger al lyd til sig, så du kan intet høre.";
            switch(self.labyrintroomnum%8) {
             0: print "Du hører fjerne og sære lyde.";
             1: print "Lyden af nogen, der lytter til John Cage's 4'33'' for fuld udblæsning.";
             2: print "Lyden af din mave, der rumler.";
             3: print "Lyden af stilhed.";
             4: print "Lyden af et teksteventyr.";
             5: print "Lyden af din indre stemme.";
             6: print "Lyden af rottelatter.";
             7: print "Lyden af et fjernt tordenvejr.";
            }
            print "^";
            rtrue;
         Smell:
            if (self.labyrintroomnum == 6 && stearinlys in room_labyrint_6) "Den sarte duft af et ægte stearinlys, der brænder.";
            if (self.labyrintroomnum == 1 && pebernoedder in room_labyrint_1) "Duften af gode pebernødder giver dig lyst til at spise dem allesammen.";
            if (self.labyrintroomnum == 11 && stenskred.appeared == false) "Duften af de gyldne æbleskiver virker virkelig dragende på dig.";
            if (self.labyrintroomnum == 8 && gloegg.examined) "Duften af gløgg gør dig skør.";
            if (self.labyrintroomnum == 8 && gloegg.examined == false) "Duften fra kildens væske gør dig skør.";
            if (self.labyrintroomnum == 9 && baal.burned == false) "Duften af kviste og kvas.";
            if (self.labyrintroomnum == 9 && baal.burned == true) "Duften af lejrbål giver dig lyst til at synge.";
            if (self.labyrintroomnum == 11 && stenskred.appeared) "Svovllugten fra lavastenene får dig til at tænke på nytårsaften.";
            if (self.labyrintroomnum == 15 && drage in room_labyrint_15) "Ud over ild, kommer der også en skarp stank af dårlig ånde fra dragens gab.";
            if (self.labyrintroomnum == 15 && enhjoerning in room_labyrint_15) "Enhjørningen udsender en nyvasket duft, langt fra hvordan en hest f.eks. ville lugte.";
            if (self.labyrintroomnum == 14) "Regnbuen udsender en drømmeagtig duft af blomster og frugt.";
            if (self.labyrintroomnum == 12) "Lugten af success.";
            "En stank af skimmelsvamp og fugt, overlejret af en symfoni af uspecificerede og mere behagelige dufte.";
	 Xyzzy:
	    if (player in room_labyrint_0) "Du siger det magiske
	    ord ~XYZZY~, og du mærker en svag vibration i gulvet og hører en
	    fjern dyb lyd.";
	    if (player in room_labyrint_11 && stenskred in room_labyrint_11) {
       	        "Lavastenene er helt upåvirkede af din magi. En mere
                lavpraktisk løsning, som f.eks. en skovl ville dog være
                behjælpelig.";
            }
	    if (player in room_labyrint_15 && drage in room_labyrint_15) {
		if (player.magic >= 3) {
		    remove drage;
		    move enhjoerning to room_labyrint_15;
		    "Du siger det magiske ord ~XYZZY~, og hele
		    labyrinten begynder at ryste som under et enormt
		    jordskælv. ~BANG!~ Et øredøvende brag lyder og
		    dragen forvinder i en stor røgsky. Da røgen har lagt
		    sig kan du se et lille væsen stå foran dig. Det er
 		    en enhjørning!";
		}
		"Du siger det magiske ord ~XYZZY~, og jorden ryster
		blidt. Det er som om du har ikke magiske kræfter nok
		til din rådighed.";
	    }
	    if (player in room_labyrint_15 && enhjoerning in room_labyrint_15) {
		"Du siger det magiske ord ~XYZZY~. Enhjørningen
		vrinsker og pruster og nikker med hovedet, som om
		den forsøger at pege mod vest.";
	    }
	    if (player in room_labyrint_14 && enhjoerning in
	    room_labyrint_14) {
		if (player.magic >= 3) {
		    remove enhjoerning;
		    print "Du siger det magiske ord ~XYZZY~ igen, og pludselig
                        sidder du på enhjørningens ryg. Enhjørningen tager
                        tilløb og begynder at galopere, og før du ved af det
                        flyver I over regnbuen. Du kigger ned og ser den
                        uendelige afgrund og gyser, idet afgrunden stirrer
                        tilbage. Efter at have landet på den anden side kaster
                        enhjørningen dig af og forsvinder tilbage over
                        afgrunden. Du hører fjerne ekkoer af
                        enhjørninge-vrinsk fra den anden side af afgrunden.^";
                    PlayerTo(room_labyrint_13);
		    rtrue;
		}
		"Du siger det magiske ord ~XYZZY~, men du har ikke
		nok magiske kræfter til, at det rigtig virker.";
	    }
	    "Du siger det magiske ord, men der sker ikke så meget lige nu.";
        ],
 has    light transparent;
! ============================================================================ !
!Include "parserextensions.inf";
[ PrefaceByArticle o acode pluralise capitalise  i artform findout;
! ¤#¤ Routine entirely rewritten. Capitalise and findout are now meaningless.
   i=(acode+short_name_case*LanguageCases);
   capitalise = findout;
   if (o.&articles~=0) {
     if(o.#articles>WORDSIZE) {
       if(metaclass(o.&articles-->i)==String) {
         print (string) o.&articles-->i, " ";
         if (pluralise) return;
       }
       jump JustSN;
     }
     else if(metaclass(o.articles)==String) {
       print (string) o.articles, " ";
       if (pluralise) return;
       jump JustSN;
     }
     else if(metaclass(o.articles)==Routine) {
       if(PrintOrRun(o, articles)) {
         print " ";
         if (pluralise) return;
       }
       jump JustSN;
     }
   }
   if(indef_mode==false)
     jump JustSN;
   i = GetGNAOfObject(o);


! ¤#¤ Changes
!   if (pluralise)
!   {   if (i<3 || (i>=6 && i<9)) i = i + 3;
!   }
   if (pluralise)
   {   if (i<4 || (i>=8 && i<12)) i = i + 4;
   }
! ¤#¤ End of change

   i = LanguageGNAsToArticles-->i;

   artform = LanguageArticles
             + 3*WORDSIZE*LanguageContractionForms*(short_name_case + i*LanguageCases);

! ¤#¤ Whole section removed, since it only applies if LanguageContractionForms > 1

   print (string) artform-->acode;
   if (pluralise) return;
.JustSN;
   print (PSN__) o;
];

! Nyttige generelle udvidelser til parseren
![ IsObjInContainer obj container;
!    for (:parent(obj)~=container:obj=parent(obj))
!        if(parent(obj)==nothing) rfalse; rtrue;
!];
!
![ IsCarried obj;
!    ! Check if player is carrying obj
!    return IsObjInContainer(obj, player);
!];

[ IsHeld obj;
    ! Check if player is holding obj in his hands
    if (obj in player) rtrue; rfalse;
];

[ NotHeld obj;
    ! Check if player is not holding obj in his hands
    if (obj in player) rfalse; rtrue;
];

[ PrintTextArrayOffset text_array offset   i c;
    ! Prints a text array at an offset
    for (i=0:i<text_array->0:i++) {
        c = text_array->(i+offset);
        print (char) c;
    }
];

! Customizing the status line

[ DrawStatusLine width posa pad;
    @split_window 1;
    @set_window 1;
    @set_cursor 1 1;
    width = 0->33;

    style reverse;
    spaces (width);
    @set_cursor 1 2;
    PrintShortName(location);
    pad = StatusLineScore(width, 45, 1);
    posa = width-pad;
    @set_cursor 1 posa;
    StatusLineScore(width, 45, 0);
    @set_cursor 1 1;
    style roman;
    @set_window 0;
];

[ Bold text_string;
    style bold; print (string) text_string; style roman;
];

! Two common uses of PrintTextArrayOffset:
[ PrintTextArray text_array; PrintTextArrayOffset(text_array, 1); ];
![ PrintReadTextArray readtext_array; PrintTextArrayOffset(readtext_array, 2); ];

[ PrintTextArrayOffsetLength text_array offset length  i c;
    ! Prints a text array at an offset
    for (i=0:i<length:i++) {
        c = text_array->(i+offset);
        print (char) c;
    }
];

[ CompareTextArrays text_array1 text_array2 offset1 offset2   i c1 c2;
    ! Compare two text buffers at offsets. Return 0 if similar.
    ! Only checks the characters of the text_array1
    if (text_array1->0 ~= text_array2->1) return 1;
    for (i=0:i<text_array1->0:i++) {
        c1 = text_array1->(i+offset1);
        c2 = text_array2->(i+offset2);
        if (c1 ~= c2) return 1;
    }
    return 0;
];

[ CompareReadWithTextArray text_array readtext_array;
    ! Compare a text buffer with a read in text buffer. Return 0 if similar.
    ! Only checks the characters of the text_array
    return CompareTextArrays(text_array, readtext_array, 1, 2);
];

[ CopyReadTextArrayToTextArray read_text_array text_array   x;
    ! Copy the contents of a read text array to another text array
    text_array->0 = read_text_array->1;
    for (x=0:x<read_text_array->1:x++) {
        text_array->(1+x) = read_text_array->(2+x);
    }
    return text_array;
];

![ SetTextArray text_array string_array   x;
!    for (x=0:x<string_array->0:x++)
!    {
!        text_array->(1+x) = string_array->(1+x);
!    }
!    return text_array;
!];

! Specifikke udvidelser
[ Filnavne;
    print (string) random("Dansk", "Engelsk", "Tysk", "Fransk", "Arabisk"), "Stil.doc, ";
    print (string) "0", random(9), "_",
        (string) random("Goodiepal", "Grateful_Dead", "MC_Einar", "Justin_Bieber", "Kim_Larsen",
                          "Gnags", "Nissebanden", "JS_Bach", "Nik_og_Jay",
                        "Britney_Spears", "Jodle_Birge"),
                          (string) random(".mp3, ", ".flac, ", ".m4a, ");
    print (string) random("Budget", "Regnskab", "Årskarakterer", "Fodboldklub"),
        ".xls og ";
    print (string) random("MineCraft.exe", "SimCity.exe", "nissen.z5",
                          "Doom.exe", "FIFA.exe", "wow.exe",
                          "system32.dll");

    return 0;
];

[ Plader i j  k;
    k=0;
    if (j == 1) print "en ";
    if (j == 2) print "en plade med ~";
    if (j == 3) print "pladen med ~";
    if (j == 4) print "~";
    switch(i%165) {
     0: print "The Beatles";
     1: print "The Beatles";
     2: print "Aphex Twin";
     3: print "Hr. Skæg";
     4: print "Alle tiders julesange vol. 4"; k=1;
     5: print "Judas Priest";
     6: print "Ludwig van Beethoven";
     7: print "David Bowie";
     8: print "Savage Rose";
     9: print "Alle tiders julesange vol. 7"; k=1;
     10: print "Black Sabbath";
     11: print "Kraftwerk";
     12: print "Various Artists";
     13: print "MC Einar"; k=1;
     14: print "Alle tiders julesange vol. 1"; k=1;
     15: print "Metallica";
     16: print "Pelle Gudmundsen-Holmgreen";
     17: print "Natasja";
     18: print "Dan Turèll og Sølvstjernerne";
     19: print "Alle tiders julesange vol. 2"; k=1;
     20: print "Monty Python Flying Circus";
     21: print "A Tribe Called Quest";
     22: print "Else Marie Pade";
     23: print "Grateful Dead";
     24: print "Katinka";
     25: print "Alle tiders julesange vol. 9"; k=1;
     26: print "Public Enemy";
     27: print "Kylie Minogue";
     28: print "Goodiepal";
     29: print "Gil Evans And His Orchestra";
     30: print "John Coltrane";
     31: print "Malk De Koijn";
     32: print "Pink Floyd";
     33: print "Peaches";
     34: print "Alle tiders julesange vol. 5"; k=1;
     35: print "Justin Bieber";
     36: print "B.B. King";
     37: print "Tortoise";
     38: print "James Brown";
     39: print "Bob Dylan";
     40: print "Neil Young";
     41: print "Coil";
     42: print "Alle tiders julesange vol. 3"; k=1;
     43: print "Kid Koala";
     44: print "Islaja";
     45: print "The Police";
     46: print "Johann Sebastian Bach";
     47: print "Portishead";
     48: print "Stan Getz";
     49: print "Alle tiders julesange vol. 6"; k=1;
     50: print "Beastie Boys";
     51: print "Prince";
     52: print "Sly and the Family Stone";
     53: print "Jungle Brothers";
     54: print "Plaid";
     55: print "Kool Moe Dee";
     56: print "Alle tiders julesange vol. 8"; k=1;
     57: print "Harry Pussy";
     58: print "Laurie Anderson";
     59: print "Maja Ratkje og Jaap Blonk";
     60: print "King Crimson";
     61: print "Run-DMC";
     62: print "Alle tiders julesange vol. 9"; k=1;
     63: print "PJ Harvey";
     64: print "The Orb";
     65: print "Pete Seeger";
     66: print "The Residents";
     67: print "Alle tiders julesange vol. 10"; k=1;
     68: print "Rick James";
     69: print "Caroline Henderson";
     70: print "Velvet Underground";
     71: print "Kirsten Ketsjer";
     72: print "Alle tiders julesange vol. 42"; k=1;
     73: print "Sabrina";
     74: print "Liss";
     75: print "Cat Stevens";
     76: print "John Oswald";
     77: print "Patti Smith";
     78: print "Frodegruppen 40";
     79: print "Jefferson Airplane";
     80: print "Pizzicato Five";
     81: print "Jandek";
     82: print "Nina Hagen";
     83: print "Men At Work";
     84: print "De La Soul";
     85: print "Björk";
     86: print "Talking Heads";
     87: print "Miles Davis";
     88: print "Jack Rose";
     89: print "Jimi Hendrix";
     90: print "Lars H.U.G.";
     91: print "John Lennon & Yoko Ono";
     92: print "Eric B. and Rakim";
     93: print "Deee-Lite";
     94: print "John Fahey";
     95: print "Tom Waits";
     96: print "Joanna Newsom";
     97: print "Parliament";
     98: print "Britney Spears";
     99: print "Sugarhill Gang";
     100: print "Frank Zappa";
     101: print "Manu Dibango";
     102: print "Funkadelic";
     103: print "Sade";
     104: print "Digital Underground";
     105: print "3rd Bass";
     106: print "John Cage";
     107: print "Earth, Wind and Fire";
     108: print "James Ferraro";
     109: print "Bob Marley";
     110: print "Voks";
     111: print "Biz Markie";
     112: print "Sting";
     113: print "John Lee Hooker";
     114: print "Frank Sinatra";
     115: print "Bill Evans";
     116: print "Rickie Lee Jones";
     117: print "Daft Punk";
     118: print "Skullflower";
     119: print "Lou Reed";
     120: print "Igor Stravinsky";
     121: print "Gang Starr";
     122: print "P-Funk All Stars";
     123: print "LL Cool J";
     124: print "Per Vers";
     125: print "Tom Tom Club";
     126: print "Per Nørgård";
     127: print "Frantz Schubert";
     128: print "Charalambides";
     129: print "Ultramagnetic MC's";
     130: print "Jim O'Rourke";
     131: print "Anne Linnet";
     132: print "Beach Boys";
     133: print "Duke Ellington";
     134: print "Blues Brothers";
     135: print "Jodle Birge";
     136: print "Rick Astley";
     137: print "Wilco";
     138: print "Prince Charles And The City Beat Band";
     139: print "Steve Reich";
     140: print "Brian Eno";
     141: print "Sonic Youth";
     142: print "Maceo Parker";
     143: print "Gilberto Gil";
     144: print "Steely Dan";
     145: print "Crosby, Stills and Nash";
     146: print "Bikstok Røgsystem";
     147: print "Simon & Garfunkel";
     148: print "Karlheinz Stockhausen";
     149: print "Led Zeppelin";
     150: print "Stevie Wonder";
     151: print "Madonna";
     152: print "Allman Brothers";
     153: print "Gnags";
     154: print "Count Basie";
     155: print "Tori Amos";
     156: print "Lisa Ekdahl";
     157: print "Joni Mitchell";
     158: print "George Clinton";
     159: print "Tricky";
     160: print "Tone";
     161: print "Peter Gabriel";
     162: print "Kandis";
     163: print "Ronald Holmberg";
     164: print "Erik Satie";
    }
    if (j == 1) print "-plade";
    if (j == 2) print "~";
    if (j == 3) print "~";
    if (j == 4) print "~-pladen";
    return k;
];

[ TV i;
    switch (i%16) {
     0: print "det nyeste afsnit af Troldspejlet, en temaudsendelse om teksteventyr.";
     1: print "Vejret: Der varsles snestorm i juledagene.";
     2: print "Nyhederne: Generalstrejke blandt selvkørende biler i julen.";
     3: print "Brødrene Price: Sådan laver du den bedste risengrød."; return 1;
     4: print "Året der gik: Endnu et år, samme cirkus.";
     5: print "Nyhederne: Terrorpakke 17 stemmes enstemmigt igennem af de 179 kunstige politiker-intelligenser i ~Folke~-tinget.";
     6: print "Disney Juleshow: Anders And i snekamp med Rip, Rap og Rup."; return 1;
     7: print "Nyhederne: Cyberminister Trine Bramsen stiller lovforslag om forbud mod offentligt at bære hvide tennissokker og mod at benytte sure smileys på nettet ved omtale af regeringen.";
     8: print "Nyhederne: Rumsignal indeholdende en 128kbps mp3-fil af David Bowies ~Space Oddity~ opfanges fra en fjern civilisation.";
     9: print "Casper og Mandrilaftalens julekalender."; return 1;
     10: print "Nyhederne: Forskere finder på en fjern planet et kamera, der har videofilmet en 10 millioner år lang fødsel af en stjerne.";
     11: print "Nyhederne: Tilfredsheden med regeringen er steget til 100%, siger en internetafstemning blandt 1000 chatbots.";
     12: print "Nissebanden på Mars."; return 1;
     13: print "Nyhederne: Præsident sætter sig på sin ~smartphone~ og starter tredje verdenskrig ved et uheld.";
     14: print "Nyhederne: Donald Duck er blevet præsident for anden gang i træk.";
     15: print "Nyhederne: Omfavnelsen af Nysprog i den danske folkeskole går for langsomt, raser Oplysningsministeren.";
    }
    return 0;
];

Array roomscores --> 0 0 0 0 0 0 0 0 0 0 0 0;
Array roommaxscores --> 3 3 3 3 3 3 3 3 3 3 3 3;
Array roombonus --> 0 0 0 0 0 0 0 0 0 0 0 0;
Global bonuspoints = 0;
Global motivationmessage = 0;

[ TotalScore i s;
    s = 0;
    for (i=0:i<12:i++) s = s + (roomscores-->i);
    return s;
];

[ TotalBonus i s;
    s = 0;
    for (i=0:i<12:i++) s = s + (roombonus-->i);
    return s + bonuspoints;
];

[ StatusLineScore width thres getstrlen   p;
    p = TotalScore();
    if (width > thres) {
        if (getstrlen == 1) return StatusLineScoreLength() + 20;
        print " Score: ";
        print p;
        print "/";
        print MAX_SCORE;
        print " Bonus: ";
        print TotalBonus();
    } else {
        if (getstrlen == 1) return StatusLineScoreLength() + 10;
        print " S:";
        print p;
        print "/";
        print MAX_SCORE;
        print " B:";
        print TotalBonus();
    }
];

[ StatusLineScoreLength len;
    len = 0;
    if (TotalScore() > 9) len = len + 1;
    switch(bonuspoints) {
     0 to 9: len = len + 1;
     10 to 99: len = len + 2;
     100 to 999: len = len + 3;
     1000 to 9999: len = len + 4;
    }
    return len;
];

[ GetPoint roomnum taskscore  roompointsleft totalpointsleft p bp;
    if (TotalScore() >= OK_SCORE) {
        p = 0;
        bp = taskscore;
    } else {
        totalpointsleft = OK_SCORE - TotalScore();
        roompointsleft = (roommaxscores-->roomnum) - (roomscores-->roomnum);
        if (totalpointsleft < roompointsleft) roompointsleft = totalpointsleft;
        if (taskscore > roompointsleft) {
            p = roompointsleft;
            bp = taskscore - p;
        } else {
            p = taskscore;
            bp = 0;
        }
    }
    (roomscores-->roomnum) = (roomscores-->roomnum) + p;
    (roombonus-->roomnum) = (roombonus-->roomnum) + bp;
    EndTurnPrintScore(p, 1, bp, 1);
    return 1;
];

[ GetFinalPoint roomnum taskscore  roompointsleft totalpointsleft p bp;
    if (TotalScore() >= MAX_SCORE) {
        p = 0;
        bp = taskscore;
    } else {
        totalpointsleft = MAX_SCORE - TotalScore();
        roompointsleft = (roommaxscores-->roomnum) - (roomscores-->roomnum);
        if (totalpointsleft < roompointsleft) roompointsleft = totalpointsleft;
        if (taskscore > roompointsleft) {
            p = roompointsleft;
            bp = taskscore - p;
        } else {
            p = taskscore;
            bp = 0;
        }
    }
    (roomscores-->roomnum) = (roomscores-->roomnum) + p;
    (roombonus-->roomnum) = (roombonus-->roomnum) + bp;
    !EndTurnPrintScore(p, 1, bp, 1);
    print "^[Du får de sidste "; print p; print " point og har nu ";
    print TotalScore(); print ".]^^";
    return 1;
];

[ EndTurnPrintScore p psign bp bpsign;
    if (p > 0 && bp > 0) {
	if (psign > 0) print "^[Du får "; else print "^[Du mister ";
	print p;
        print " point og";
	if ((psign <= 0 && bpsign <= 0) || (psign > 0 && bpsign > 0)) {
	    print " ";
	} else {
	    if (bpsign > 0) print " får "; else print " mister ";
	}
	print bp;
	print " bonuspoint. ";
        print "Du har nu "; print TotalScore(); print " point og "; print TotalBonus(); print " bonuspoint.]^";
    }
    if (p > 0 && bp == 0) {
	if (psign > 0) print "^[Du får "; else print "^[Du mister ";
	print p;
	print " point. ";
        print "Du har nu "; print TotalScore(); print " point og "; print TotalBonus(); print " bonuspoint.]^";
    }
    if (p == 0 && bp > 0) {
	if (bpsign > 0) print "^[Du får "; else print "^[Du mister ";
	print bp;
	print " bonuspoint. ";
        print "Du har nu "; print TotalScore(); print " point og "; print TotalBonus(); print " bonuspoint.]^";
    }
    if (psign > 0 && TotalScore() >= 20 && motivationmessage < 20) {
        motivationmessage = 20;
        print "^~På loftet sidder nissen med sin julegrød~, nynner du og håber, at det snart bliver sandt.^";
    }
    if (psign > 0 && TotalScore() >= 16 && motivationmessage < 16) {
        motivationmessage = 16;
        print "^Nu må menneskene da snart have forstået beskeden: Nisser kræver risengrød til jul.^";
    }
    if (psign > 0 && TotalScore() >= 12 && motivationmessage < 12) {
        motivationmessage = 12;
        print "^Hvis du fortsætter sådan, får du måske risengrød inden julen er omme, tænker du.^";
    }
    if (psign > 0 && TotalScore() >= 8 && motivationmessage < 8) {
        motivationmessage = 8;
        print "^Det føles rart at drille, men din mave knurrer nu ekstra højt.^";
    }
    if (psign > 0 && TotalScore() == 2 && motivationmessage < 2) {
        motivationmessage = 2;
        print "^Du har savnet at drille menneskene, kan du mærke.^";
    }
    return 1;
];

[ GetBonus bp;
    bonuspoints = bonuspoints + bp;
    EndTurnPrintScore(0, -1, bp, 1);
];

[ TakePoint roomnum taskscore  p bp;
    if (roombonus-->roomnum > 0) {
        if (taskscore <= roombonus-->roomnum) {
            p = 0;
            bp = taskscore;
        } else {
            bp = (roombonus-->roomnum);
            p = taskscore - bp;
            if (p >= roomscores-->roomnum) p = (roomscores-->roomnum);
        }
    } else {
        if (taskscore <= roomscores-->roomnum) {
            p = taskscore;
            bp = 0;
        } else {
            p = (roomscores-->roomnum);
            bp = 0;
        }
    }
    (roombonus-->roomnum) = (roombonus-->roomnum) - bp;
    (roomscores-->roomnum) = (roomscores-->roomnum) - p;
    EndTurnPrintScore(p, -1, bp, -1);
    return 1;
];

[ NumCarried x obj;
    x = 0;
    objectloop (obj in player) x++;
    return x;
];

[ NumObjects container  x obj;
    x = 0;
    objectloop (obj in container) x++;
    return x;
];

[ MakeNonConcealed obj;
    if (obj has concealed) give obj ~concealed;
    return true;
];
[ CanCarryMore;
    if (NumCarried() > MAX_CARRIED) {
        return false;
    } else {
        return true;
    }
];

[ GlueAnnounce;
    if (tastatur.glued && tastatur.glueannounced == false) {
        tastatur.glueannounced = true;
        print "Det er svært at bruge et tastatur med lim under tasterne, men
            du får løsnet nogle af tasterne, og med besvær lykkes det: ";
    }
];
! ============================================================================ !
!Include "room_badevaerelset.inf";
Room  room_badevaerelset "Badeværelset" ! Room 4
    with   description "Badeværelset er flot og rent i anledning af julen.
    Både bad og toilet skinner flot, men mest imponerende er håndvasken med
    dens guldarmaturer og det store blankpolerede spejl.",
    name 'badeværelse' 'badeværelset' 'badet',
    external_name 'badeværelse' 'badeværelset' 'badet',
    e_to room_foerstesalen,
    out_to room_foerstesalen,
    cant_go "Du kan kun gå ud på førstesalen her fra.",
    puzzlesolved false,
    saebesmidt false,
    before [;
     Listen:
        if (self.puzzlesolved == false && self.saebesmidt) <<Wait>>; ! Deflect to default action by using an unused verb
        if (noun == nothing) "Du hører kun en søvnig dryppen fra håndvasken.";
     Smell:
        if (self.puzzlesolved == false && self.saebesmidt) <<Wait>>; ! Deflect to default action by using an unused verb
        if (noun == nothing && saebe in haandvask) {
            "Sæben, der ligger på vasken, får hele rummet til at dufte af
            blomster og honning.";
        }
        if (noun == saebe) {
            if (IsHeld(saebe)) "Du løfter sæben helt op under din næse og snuser ind.
	    	Det helt nye og fine stykke sæbe dufter rigtig godt af
	    	blomster og honning."; else  "Sæben, der ligger på vasken, får hele rummet til at dufte af
            	blomster og honning.";
        }
        if (noun == laebestift) {
            "Den har en subtil og behagelig duft.";
        }
        if (noun == toilet) { "Dét ville være lidt mere information om dette
            toilet end du har brug for. Men som sagt, alt er pænt og nyligt
            rengjort her.";
        }
        "Her dufter rent, men heldigvis ikke alt for meget af
        rengøringsprodukters syntetiske blomsterdufte.";
     default:
        if (self.puzzlesolved == false && self.saebesmidt) {
            self.puzzlesolved = true;
            remove saebe;
            print "I det samme går døren op og en af familiens forældre kommer
                ind og træder som det første på sæben og står på halen med et
                højt skrig. Efter at have bandet lidt og sundet sig forsvinder
                mennesket fortumlet ud af badeværelset";
            if (foraeldre in room_sovevaerelset) print " og i seng igen.^";
	    else print " igen.^";
	    GetPoint(4, 2);
	    rtrue;
        }
    ],
    has    light;

Object -> gulv "gulv"
  with name 'gulv' 'gulvet' 'badeværelsegulv' 'badeværelsegulvet',
        short_name_def "badeværelsegulvet",
        description "Et flisegulv i en ubestemmelig gul/brun nuance.",
 has    neuter supporter concealed static;

Object -> bad "bad"
  with name 'bad' 'badet' 'brusebad' 'brusebadet' 'karbad' 'karbadet',
        short_name_def "badet",
        before [;
         Examine: "En brusekabine, som nogen har brugt timer på at afkalke i
            dagens anledning.";
         default: "Du er ren nok af en drillenisse at være.";
        ],
 has    neuter supporter concealed static;

Object -> toilet "toilet"
  with name 'toilet' 'toilettet' 'wc' 'wc^et',
        short_name_def "toilettet",
        before [;
         Examine: "Toilettet er lige så velpoleret som vasken.";
         Tis: "Du får lige lidt privatliv, imens du bruger toilettet.";
         Brug: if (noun == toilet) "Du får lige lidt privatliv, imens du bruger toilettet.";
         default:
            "Du får lige lidt privatliv, imens du bruger toilettet.";
        ],
 has    neuter supporter concealed static;

Object -> vasketoejskurv "vasketøjskurv"
  with name 'vasketøjskurv' 'vasketøjskurven' 'kurv' 'kurven',
        short_name_def "vasktøjskurven",
	before [;
	 default: "Tøjvask er ikke din største interesse.";
	],
 has    uter scenery;

Object -> spejl "spejl"
  with name 'spejl' 'spejlet',
        short_name_def "spejlet",
        drawn_upon false,
        before [;
         Examine, Search:
            if (self.drawn_upon) print "Bag din julehilsen kan du skimte dig
                selv. Noget sulten ser du ud, "; else print "Du kigger
		ind i spejlet og ser dig selv. Noget sulten ser du
		ud, ";
            if (laebestift.put_on == false) "men du
            smiler til dig selv ved tanken om al den sjov, du skal have i dag.";
            if (laebestift.put_on == true) "men dine nu flotte røde læber har
            frisket dit udseende godt op.";
         TegnPaa:
            if (NotHeld(laebestift)) "Så må du finde noget at tegne med først.";
            if (self.drawn_upon == false) {
                self.drawn_upon = true;
                print "Ved brug af læbestiften skriver du med flot
		håndskrift på spejlet: ~Glædelig jul! Ingen grød,
		ingen julefred! Kærlig hilsen, Nissen.~^";
		GetPoint(4, 1);
		rtrue;
            } else {
                "Din julehilsen er allerede helt perfekt.";
            }
        ],
 has    neuter scenery;

Object  -> haandvask "håndvask"
  with name 'vask' 'vasken' 'håndvask' 'håndvasken' 'armatur'
  'armaturer' 'guldarmatur' 'guldarmaturer',
        short_name_def "håndvasken",
        description  [;
            print "En stor porcelænsvask, som er flot og nypoleret.";
            if (saebe in haandvask || laebestift in haandvask) {
                print " På håndvasken ligger";
                if (saebe in haandvask) {
                    print " et stykke sæbe";
                    if (laebestift in haandvask) print " og";
                }
                if (laebestift in haandvask) print " en læbestift";
                print ".^";
            } else {
                print "^";
            }
            rtrue;
       ],
  has  uter supporter scenery;

Object saebe "et stykke sæbe" haandvask
  with name 'håndsæbe' 'håndsæben' 'sæbe' 'sæben',
        description "Sæbestykket er helt nyt i dagens anledning. Der står
            ~Imperial Soap~ på et lille fint mærke på sæben.",
        invent "et stykke sæbe",
        short_name_def "sæben",
        after [;
         Drop, PutOn:
            if (second == haandvask) {
                give self concealed;
                "Du lægger sæben tilbage på håndvasken.";
            }
            if (self in room_badevaerelset) {
                room_badevaerelset.saebesmidt = true;
                "Du placerer sæben midt på gulvet, og i det samme hører du trin
                uden for døren. Du gemmer dig hurtigt bag ved vasketøjskurven.";
            }
         Take: give self ~concealed;
        ],
 has    uter concealed;

Object laebestift "læbestift" haandvask
  with name 'læbestift' 'læbestiften' 'stift' 'stiften' 'makeup' 'makeuppen',
        description "Det er en læbestift i samme røde nuance som din
	    hue.",
        short_name_def "læbestiften",
        put_on false,
        after [;
         Drop, PutOn:
            if (player in room_badevaerelset) {
                move self to haandvask;
                give self concealed;
                "Du lægger læbestiften tilbage på håndvasken.";
            }
         Take: give self ~concealed;
        ],
        before [;
         TegnPaa:
            if (second == spejl) <<TegnPaa spejl>>;
            if (second == player) <<Wear laebestift>>;
            print "Hvad vil du tegne på med læbestiften?^";
            rfalse;
         Wear:
            self.put_on = true;
            "Dine læber er nu flotte røde og matcher perfekt din hue.";
         Brug: if (noun == laebestift) "Dine læber er nu flotte røde og matcher
            perfekt din hue.";
        ],
 has    uter concealed;
! ============================================================================ !
!Include "room_dagligstuen.inf";
Room  room_dagligstuen "Dagligstuen" ! Room 10
 with   description [;
	    print "Dette er familiens dagligstue, hvor familien
		bruger mange timer sammen på at lytte til musik og
		se fjernsyn. Et stereoanlæg og en stor samling
		vinylplader fylder det meste af reolen.";
	    if (kat in room_dagligstuen && kat.sleeping == true)
		print " En kat ligger i sofaen og sover.";
	    if (juletrae.painted) " Ovre ved vinduet står det stolte
	    regnbuefarvede juletræ."; " Ovre ved vinduet står et juletræ.";
	],
        name 'dagligstue' 'dagligstuen' 'stue' 'stuen',
        external_name 'dagligstue' 'dagligstuen' 'stue' 'stuen',
        w_to room_entreen,
        out_to room_entreen,
        cant_go "Du kan kun gå ud i entreen her fra.",
    before [;
     Listen:
        if (stereoanlaeg has on || fjernsyn has on) {
            if (stereoanlaeg has on && fjernsyn has on) "Du lytter til en skøn
            kakofoni af musik fra stereoanlægget og lyd fra fjernsynet.";
            if (stereoanlaeg has on) <<Examine stereoanlaeg>>;
            if (fjernsyn has on) <<Examine fjernsyn>>;
        }
     Smell: if (juletrae.painted) "Her lugter mest af alt af nymalet juletræ."; else
        "Her i dagligstuen lugter dejligt af grantræ.";
     Examine:
        if (noun == out_obj || noun == vindue || second == vindue) "Den kolde vintervind hvirvler
        sneen rundt i luften udenfor, og der er nok faldet mere end en halv
        meter sne siden i går. Julen bliver helt sikkert hvid -- med eller
        uden risengrød i din mave.";
        rfalse;
     Go:
	if (noun == out_to || noun == w_to) {
	    if (IsHeld(pladesamling)) {
		move pladesamling to room_dagligstuen;
		print "Hov, du er en ordensnisse, så du sætter
		    omhyggeligt ";
		Plader(pladesamling.record_index, 3);
		print " tilbage i pladesamlingen, inden du forlader dagligstuen.^";
	    }
	}
    ],
    after [;
	if (noun == e_to) {
            if (TotalScore() < OK_SCORE && roegetsild in player && kat.sleeping) {
                <Look>;
                 "^Idet du træder ind i dagligstuen, åbner katten et øje på
                klem ved lugten af fisken, som du bærer. Katten falder dog i
                søvn igen, fordi den er både mæt og træt.";
            }
            if (kat.sleeping && player in room_dagligstuen && roegetsild in player) {
                kat.sleeping = false;
                <Look>;
                give kat ~concealed;
                "^Da katten lugter den røgede sild, som du bærer på, vågner
                den og springer op fra sofaen.";
            }
        }
    ],
 has    light;

Object 	-> reol "reol"
 with   name 'reol' 'reolen' 'reoler' 'reolerne' 'pladereol'
 'pladereolen' 'pladereolerne' 'pladereoler',
        short_name_def "reolen",
        description [;
            PrintOrRun(self, description); rtrue;
        ],
        before [;
         Examine, Take, Consult:
            "Reolen er indeholder et stereoanlæg og en stor pladesamling.";
	],
 has    uter scenery container transparent open;

Object  stereoanlaeg "stereoanlæg" reol
 with   name 'stereoanlæg' 'stereoanlægget' 'anlæg' 'anlægget' 'grammofon' 'grammofonen',
        short_name_def "stereoanlægget",
        loaded false,
        turned_up false,
	xmas_playing_loud false,
        playing false,
	got_xmas_record 0,
        before [;
	 Examine:
	    if (self hasnt on) print "Et ældre men velholdt B&O-stereoanlæg med grammofon. Anlægget er slukket. ";
	    if (self has on && self.playing == false) {
		print "Anlægget er tændt og udsender en ";
		if (self.turned_up) print "kraftig"; else print "svag";
		print " brummelyd. ";
	    }
	    if (self.playing == false) {
		if (self.loaded) {
		    print "Der ligger "; Plader(pladesamling.record_index, 2); print " på grammofonen.";
		}
	    } else {
 		if (pladesamling.got_xmas_record) {
		    if (self.turned_up) {
		    	print "Du -- og hele huset -- lytter til den dejligste julemusik.";
		    } else  {
		    	print "Du lytter til den dejligste julemusik.";
		    }
		} else {
		    if (self.turned_up) {
		    	print "Du -- og hele huset -- lytter til "; Plader(pladesamling.record_index, 3); print ".";
		    } else  {
		    	print "Du lytter til "; Plader(pladesamling.record_index, 3); print ".";
		    }
		}
	    }
	    print "^";
	    rtrue;
	 SwitchOff:
	    if (self has on) {
	    	self.playing = false;
	    	give self ~on;
	    	print "Du slukker for anlægget";
 	    	if (self.loaded) print " og sætter omhyggeligt pladen tilbage i pladereolen,
	    	    som den ordensnisse du er.^"; else print ".^";
	    	self.loaded = false;
		if (self.xmas_playing_loud == true) {
		    TakePoint(10, 1);
		    self.xmas_playing_loud = false;
		}
	    	rtrue;
	    } else {
		"Stereoanlægget er allerede slukket.";
	    }
         SwitchOn:
	    if (self has on) "Anlægget er allerede tændt. ";
	    if (self.loaded == false) {
		print "Du tænder for stereoanlægget men hører kun en
		    dyb, varm brummelyd fra højttalerne, så du
		    slukker igen.^";
		rtrue;
	    }
	    if (self.loaded == true) {
		give self on;
	    	self.playing = true;
	    	print "Du tænder for stereoanlægget, og
		    grammofonpladen begynder at rotere. Forsigtigt
		    sætter du grammofonens nål ned på pladen og ";
		if (pladesamling.got_xmas_record == 1) {
		    print "julepladen begynder at spille. Lyden
			er dog så svag, at selv en nisse knapt nok kan
			høre den.^";
		} else {
		    Plader(pladesamling.record_index, 4);
		    print " begynder at spille. Lyden er dog så
			svag, at selv en nisse knapt nok kan høre
			den.^";
		}
	    rtrue;
	    }
	 SkruOp:
	    if (self hasnt on) "Anlægget er slukket.";
	    if (self.turned_up == false) {
		self.turned_up = true;
                print "Du skruer anlægget op på maksimal lydstyrke. ";
		if (pladesamling.got_xmas_record == 1) {
		    print "Julepladens søde julemelodier spiller nu så
			højt, at menneskene i hele huset kan høre
			dem. Det vil sikkert bringe deres tanker i
			din retning.^";
		} else {
		    print "Nu kan du høre pladen med ~"; Plader(pladesamling.record_index, 0);
		    print "~ i alle dens detaljer. Du nyder musikken,
			men det er bare desværre ikke musik,
			som bringer menneskenes tanker i din
			retning.^";
		}
		if (self.xmas_playing_loud == false) {
		    if (pladesamling.got_xmas_record == 1) {
		    	GetPoint(10, 1);
			self.xmas_playing_loud = true;
		    }
		}
		rtrue;
	    } else {
                "Anlægget kan ikke skrues højere op.";
            }
         SkruNed:
            if (self.turned_up == true) {
                self.turned_up = false;
		print "Du skruer anlægget ned. Du kan knapt nok høre musikken.^";
		if (self.xmas_playing_loud == true) {
		    TakePoint(10, 1);
		    self.xmas_playing_loud = false;
		}
		rtrue;
            } else {
                "Anlægget kan ikke skrues længere ned.";
            }
        ],
 has    neuter switchable ~on static supporter concealed;

Object  pladesamling "plade" reol
 with   name 'plade' 'plader' 'pladen' 'pladerne' 'lp' 'vinyl' 'vinylplade'
 'vinylplader' 'vinylpladerne' 'vinylpladen' 'pladesamling'
 'pladesamlingen' 'musik' 'musikken' 'lyd' 'lyden',
        short_name_def "pladen",
        description [;
	    if (IsHeld(self)) {
		print "Du holder ";
		Plader(self.record_index, 2);
		print ".^";
		rtrue;
	    } else {
		print "En pladesamling med musik i mange forskellige genrer.^";
	    }
	],
	invent [;
	    if (IsHeld(self)) {
		Plader(self.record_index, 2);
		rtrue;
	    } else {
		print "en pladesamling";
	    }
	],
	examined false,
        got_xmas_record 0,
        record_index 0,
	was_loud false,
        before [;
	 SwitchOn, Burn:
	    <<SwitchOn stereoanlaeg>>;
         Take, Skift:
	    if (stereoanlaeg.loaded) {
		print "Du skifter pladen på grammofonen ud med en plade med ~";
	    }
	    if (IsHeld(self)) {
		if (IsHeld(self)) print "Du sætter pladen tilbage og tager en anden. En med ~";
	    }
	    if (IsHeld(self) == false && stereoanlaeg.loaded == false) {
                if (CanCarryMore()) {
                    if (IsHeld(self) == false) {
                        move self to player;
                        print "Du tager en plade ud af pladereolen. Det er en plade med ~";
                    }
                } else {
                    "Du er en stærk nisse, men kan trods alt kun bære tre ting ad
                    gangen af gangen.";
                }
	    }
	    self.record_index++;
	    self.got_xmas_record = Plader(self.record_index, 0);
	    print "~.^";
	    if (stereoanlaeg.loaded) {
		if (stereoanlaeg.xmas_playing_loud == true && self.got_xmas_record == false) {
		    TakePoint(10, 1);
		    stereoanlaeg.xmas_playing_loud = false;
		}
		if (stereoanlaeg.xmas_playing_loud == false && self.got_xmas_record == true && stereoanlaeg.turned_up == true) {
		    GetPoint(10, 1);
		    stereoanlaeg.xmas_playing_loud = true;
		}
	    }
	    rtrue;
         PutOn, Insert:
	    if (self notin player) {
                <<Take pladesamling>>;
            }
            if (second == stereoanlaeg) {
                stereoanlaeg.loaded = true;
		move self to stereoanlaeg;
		"Du tager vinylpladen ud af omslaget og lægger den på grammofonen.";
            }
	    if (second == reol && IsHeld(pladesamling)) {
		move self to reol;
		"Du sætter omhyggeligt pladen tilbage i pladereolen.";
	    }
	    rtrue;
	 Drop:
            if (IsHeld(self)) {
                if (player in room_dagligstuen) {
                    give self concealed;
		    move self to reol;
		    "Du sætter omhyggeligt pladen tilbage i pladereolen.";
	        } else {
                    give self ~concealed;
                    move self to location;
                    "Du lægger forsigtigt pladen.";
                }
            } else {
                "Du må tage en plade først.";
            }
	 SkruOp:
	    <<SkruOp stereoanlaeg>>;
	 SkruNed:
	    <<SkruNed stereoanlaeg>>;
        ],
	after [;
	 Examine:
	    self.examined = true;
	],
 has    uter container open concealed;


Object  fjernsyn "fjernsyn" room_dagligstuen
 with   name 'fjernsyn' 'fjernsynet' 'tv' 'tvet' 'tv^et' 'kanal' 'kanalen' 'kanaler' 'program' 'programmet' 'programmer',
        short_name_def "fjernsynet",
	show_index -1,
        show_xmas 0,
        prev_show_xmas 0,
        before [;
         Examine:
	    print "Et gammeldags fjernsyn med billedrør og ulden
		billedkvalitet. Fjernsynet er ";
            if (self has on) print "tændt og viser "; else "slukket.";
	    TV(self.show_index);
	    print "^";
	    rtrue;
	 Skift:
	    if (self hasnt on) "Fjernsynet er slukket.";
	    if (self has on && noun == fjernsyn) {
                if (self.show_xmas == 1) self.prev_show_xmas = 1; else self.prev_show_xmas = 0;
		self.show_index++;
	    	print "Du skifter kanal til ";
	    	self.show_xmas = TV(self.show_index);
		print "^";
                if (self.prev_show_xmas == 1) {
		    TakePoint(10, 1);
	    	}
		if (self.show_xmas == 1) {
		    GetPoint(10, 1);
		    self.show_xmas = 1;
	    	}
		rtrue;
	    }
        ],
        after [;
         SwitchOn:
	    self.show_index++;
	    if (self.show_index == 0) {
		print "Du trykker på tændknappen, og efter at apparatet er
                    varmet op, toner Troldspejlets venlige vært frem på
                    skærmen. Det er en temaudsendelse om teksteventyr.^";
	    } else {
	    	print "Du tænder for fjernsynet igen og skifter kanal: ";
	    	self.show_xmas = TV(self.show_index);
		print "^";
	    }
	    rtrue;
         SwitchOff:
	    if (self.show_xmas == 1) {
		TakePoint(10, 1);
		self.show_xmas = 0;
	    }
            "Du trykker på slukknappen, og billedet forsvinder i
	    form af en hvid linje, der bliver til en uendelig lille
	    hvid prik på en sort baggrund.";
        ]
 has    neuter static switchable ~on concealed;

Object  sofa "sofa" room_dagligstuen
 with   name 'sofa' 'sofaen',
        short_name_def "sofaen",
        description [;
               if (kat.sleeping) {
                   "En slidt sofa i lyst, ternet betræk. En kat ligger og
                   sover på den.";
	       } else {
		   "En sofa i lyst, ternet betræk.";
	       }
	],
        before [;
	 Enter, Climb, Jump:
	    if (kat.sleeping) "Den ser eller indbydende ud, den sofa, men den er desværre optaget
	    af husets søde kat."; "Du tænker dig om, og har hverken tid eller
                lyst til at side og hvile dig nu.";
         Push, Touch:
            "Sofaen er så stor og tung, at den ikke kan flyttes af en nisse.";
        ],
 has    uter scenery enterable;

Object  kat "kat" room_dagligstuen
 with   name 'kat' 'katten' 'mis' 'missen',
        short_name_def "katten",
        sleeping true,
	invent [;
	    if (self.sleeping) print "en kat (som sover på sofaen)";
	    else print "en kat (som smyger sig om dine ben)";
	    rtrue;
	],
	before [;
         Dril: "Katten er vel nok din bedste ven i dette hus, så du driller
            den kun yderst sjældent,og slet ikke, når det er jul..";
         default:
            if (self.sleeping) "Katten ligger og sover på sofaen, så
	    du må hellere lade være med at forstyrre den lige nu.";
            if (self.sleeping == false && rotter.friendly == false) "Katten er frisk og sulten
	    efter fisk eller i mangel af fisk: mus eller rotter.";
            if (rotter.friendly == true && player in room_loftet && kat in
                room_loftet) "Katten løber rundt og kigger skiftevis på dig,
                rotterne, fisken og grøden.";
            "Katten løber rundt, som katte nu gør.";
	],
        daemon [;
            if (TotalScore() >= OK_SCORE && roegetsild in player) {
                if (self.sleeping == false && self notin parent(player) && rotter.friendly == false) {
                    move self to parent(player);
                    if (parent(player) == room_loftet) give self concealed;
                    if (parent(player) ~= room_loftet && rotter.friendly == false)
                        print "^"; else "";
                    print (string) random("Katten smyger sig om dine ben i håbet
                          om at få noget at spise. ","Katten følger efter dig og
                          kigger sultent på den røgede sild. ", "Katten løber
                          efter dig, pga. den stinkende fisk. ");
                    print "^";
                }
            }
        ],
 has    neuter animate static concealed;

Object  juletrae "juletræ" room_dagligstuen
 with   name 'juletræ' 'juletræet' 'træ' 'træet',
        short_name_def "juletræet",
        description [;
	    if (self.painted == false) "Juletræet må være købt i sidste
            øjeblik, fordi det er både skævt og halvvissent. Grangrenene -- de
            få der er af dem -- er knapt nok grønne i farven.";
	     else "Et flot regnbuefarvet juletræ. Du har altid ønsket
 	    dig et regnbuefarvet juletræ.";
	],
	painted false,
	before [;
         Take, PushDir, Push, Touch, Squeeze:
            "Du har aldrig rigtig kunnet lide at røre ved prikkende grangrene.";
         Climb, Enter, Go:
            "Du har aldrig rigtig kunnet lide at klatre på de prikkende
            grangrene.";
	 TegnPaa:
	    if (self.painted == false) {
	    	print "Du tager spraydåserne en efter en, og maler
		juletræet i alle regnbuens farver. Det skal nok give
		dig lidt velfortjent opmærksomhed.^";
		self.painted = true;
		GetPoint(10, 1);
		rtrue;
	    } else {
		"Du gør dit kunstværk helt perfekt ved at tilføje
		nogle skyggeeffekter til regnbuen.";
	    }
	],
 has    neuter static concealed enterable;

Object  vindue "vindue" room_dagligstuen
 with   name 'vindue' 'vinduet' 'vinduer' 'vinduerne' 'vindue' 'sne' 'sneen' 'udenfor' 'ud',
        short_name_def "vinduet",
	opened false,
        before [;
         Examine, Search:
            "Den kolde vintervind hvirvler sneen rundt i luften
	    udenfor, og der er nok faldet mere end en halv meter sne
	    siden i går. Julen bliver helt sikkert hvid -- med eller
	    uden risengrød i din mave.";
         Take, Open:
	    if (self has open) {
		"Det er underligt, at du ikke har lagt mærke til, at vinduet
                allerede er åbent: Sneen hvirvler ind ad det, og der ligger små
                snedriver på gulvet.";
	    } else {
		give self open;
 	    	print "Du åbner vinduet på vid gab, og gardinerne står nu
	    	    nærmest vandret ind i stuen. Sne fyger ind og lægger
	    	    sig som en dyne over gulvet.^";
		if (self.opened == false) {
		    self.opened = true;
	    	    GetPoint(10, 1);
		}
		rtrue;
	    }
	 Close:
	    if (self hasnt open) {
		"Vinduet er allerede virkelig lukket.";
	    } else {
		give self ~open;
 	    	"Du lukker med lidt besvær vinduet igen.";
	    }
        ],
 has    scenery static concealed ~open openable door;

! ============================================================================ !
!Include "room_boernevaerelset.inf";
Room  room_boernevaerelset "Børneværelset" ! Room 5
    with   description [;
               if (boern in room_boernevaerelset)
                   print "I børneværelset findes alle de ting, som man normalt
                       finder på et børneværelse, nemlig et sandt sammenrod af
                       legetøj og tøj. Ovre ved TV'et kan du se
                       husets børn sidde samlet om en spillekonsol og spille,
                       hvad der må være et sjovt spil. De tænker helt sikkert
                       slet ikke på dig, nissen på loftet, og at du ikke har
                       fået risengrød i ugevis.^"; else "I
                       børneværelset findes alle de ting, som man normalt
                       finder på et børneværelse. Mest af alt et sandt rod af
                       legetøj og tøj.";
           ],
    name 'børneværelse' 'børneværelset',
    external_name 'børneværelse' 'børneværelset',
    n_to room_foerstesalen,
    out_to room_foerstesalen,
    cant_go "Du kan kun gå ud på førstesalen her fra.",
    has    light;

Object  boern "børn" room_boernevaerelset
 with   name 'børn' 'børnene',
        short_name_def "børnene",
	before [;
         Dril: "At drille er det du er bedst til, men du må finde ud af, hvordan du vil drille.";
	 Examine:
	    if (self in room_boernevaerelset) {
                "Fra tid til anden griner børnene højlydt.";
            }
            if (self in room_spisestuen) {
                "Børnene sidder ved spisebordet og spiser morgenmad. De taler
                om, hvordan de glæder sig til juleaften, men de nævner ikke
                dig med ét ord.";
            }
	 default:
            if (self in room_boernevaerelset) "Børn, der spiller videospil, er ikke til at rokke
	    ved."; else <<Examine self>>;
	],
 has    animate scenery static pluralname;

Object  spilletv "tv" room_boernevaerelset
 with   name 'fjernsyn' 'fjernsynet' 'tv' 'tvet' 'tv^et',
        short_name_def "tv'et",
        before [;
         Examine:
            if (self has on) "Det lille fladskærms-tv blinker med farver og
            striber."; else
            "Et lille fladskærms-tv, som passer perfekt i et børneværelse. TV'et
 er slukket.";
         SwitchOn:
            if (stikkontakt hasnt on) "Der er ikke sat strøm til.";
	 SwitchOff:
            if (boern in room_boernevaerelset) "Du slukker drillende for TV'et uden at børnene ser dig. De tænder
            febrilsk for det igen, så de kan fortsætte spillet.";
        ]
 has    neuter static switchable on concealed;

Object  spillekonsol "spillekonsol" room_boernevaerelset
 with   name 'spillekonsol' 'spillekonsollen' 'konsol' 'konsollen' 'spil'
 'spillet' 'computerspil' 'computerspillet' 'computer' 'computeren',
        short_name_def "spillekonsollen",
        description [;
            if (self has on && boern in room_boernevaerelset) print "Spillekonsollen er ikke den nyeste model, men
		spillene er åbenlyst stadigvæk virkelig sjove. ";
	    if (self has on) "Konsollen er tændt og summer
	    højlydt og er forbundet til en stikkontakt ved
	    fodpanelet."; "Konsollen er slukket.";
        ],
        before [;
         Examine: PrintOrRun(self, description); rtrue; ! Undgå "is on"
            ! description
         SwitchOff:
            if (self has on)
                "Du kan ikke finde knappen på selve konsollen.";
            else
                "Den er allerede slukket.";
         SwitchOn:
            if (self has on) {
                if (boern in room_boernevaerelset)
                    "Konsollen er allerede tændt, og børnene bruger den flittigt.";
                if (spilletv hasnt on) "TV'et er slukket, så det er der ikke meget sjov ved.";
                "Du spiller et spil Tetris, men du er for sulten til at kunne sidde og hygge dig nu.";
            } else {
                "Du kan ikke finde knappen på selve konsollen.";
            }
         Spil:
            if (boern in room_boernevaerelset) "Du kan ikke komme
	    til at spille, fordi børnene bruger spillekonsollen.";
            if (spilletv hasnt on) "TV'et er slukket, så det er der ikke meget sjov ved.";
            if (self has on) "Du spiller et spil Tetris, men du er for
            sulten til at kunne sidde og hygge dig nu.";
            else "Konsollen er slukket.";
	 Take, Push:
	    "Konsollen sidder fast med en masse ledninger.";
        ],
 has    uter switchable on scenery;

Object  stikkontakt "stikkontakt" room_boernevaerelset
 with   name 'stikkontakt' 'stikkontakten' 'kontakt' 'kontakten' 'afbryder' 'afbryderen' 'ledning' 'ledningen',
        short_name_def "stikkontakten",
        description [;
            print "Fra stikkontakten løber en ledning til spillekonsollen. Kontaktens afbryder er ";
            if (self has on) "tændt."; "slukket.";
        ],
        before [;
         Examine: PrintOrRun(self, description); rtrue; ! Undgå "is on" description
         SwitchOn:
            if (self hasnt on) {
                give spillekonsol on;
                give self on;
                give spilletv on;
                "Konsollen booter op, og et spil starter.";
            } else {
                "Kontakten er allerede tændt.";
            }
	 SwitchOff:
	    if (self hasnt on) "Kontakten er allerede slukket.";
         Take:
            "Den sidder fast.";
        ],
        after [;
         SwitchOff:
            if (boern in room_boernevaerelset) {
                give spillekonsol ~on;
		print "Børnene bliver lange i ansigterne, da du slutter deres
                    spil. De råber og skændes om, hvem der slukkede på
                    kontakten. ~Vi var /så/ tæt på at gennemføre spillet /og/
                    sætte high score!~, jamrer de, og skuffede løber de ud af
                    børneværelset og nedenunder for at spise morgenmad.^";
		if (roomscores-->11 < roommaxscores-->11) move boern to	room_spisestuen; else remove boern;
		GetPoint(5, 2);
		rtrue;
            } else {
		give spillekonsol ~on;
                "Du slukker for kontakten og spillekonsollen slukker.";
            }
        ]
 has    uter switchable on scenery;

Object  legetoej "legetøj" room_boernevaerelset
 with   name 'tøj' 'legetøj' 'legetøjet' 'tegneserie' 'tegneserier'
             'dukker' 'dominobrikker' 'sokker' 'puslespil' 'bukser' 'lego' 'Anders' 'And'
            'bluser' 'blade' 'bipbipspil' 'sneakers' 'brætspil' 'yoyoer' 'frisbees'
            'trøjer' 'bøger' 'pizzabakker' 'colaflasker',
        short_name_def "legetøjet",
        examined false,
        before [;
         Examine:
            self.examined = true;
            "Et flot og uoverskueligt rod af dukker, dominobrikker, sokker,
            puslespil, bukser, bluser, Anders And-blade, bipbipspil,
            colaflasker, LEGO, sneakers, brætspil, yoyoer, gadgets, frisbees,
            trøjer, bøger, pizzabakker, osv., osv., osv.";
         Take:
            if (self.examined == false) {
                self.examined = true;
                print "Et flot og uoverskueligt rod af dukker, dominobrikker,
                    sokker, puslespil, bukser, bluser, Anders And-blade,
                    bipbipspil, colaflasker, LEGO, sneakers, brætspil, yoyoer,
                    gadgets, frisbees, trøjer, bøger, pizzabakker, osv., osv.,
                    osv. ";
            }
            "Hvis du havde mere tid, så kunne du underholde dig selv her i
            dagevis. Du vælger dog ikke at fortabe dig i børnenes sager.";
         default:
            <<Examine self>>;
        ],
 has    pluralname scenery;

! ============================================================================ !
!Include "room_bryggerset.inf";
Room  room_bryggerset "Bryggerset" ! Room 9
 with   description "I bryggerset opbevarer menneskene alle mulige ting mellem
    himmel og jord, hvoraf de fleste bliver brugt meget sjældent. Heldigvis
    står alle tingene pænt arrangeret på reoler langs alle væggene.",
        name 'bryggers' 'bryggerset',
        external_name 'bryggers' 'bryggerset',
        e_to room_entreen,
        out_to room_entreen,
        cant_go "Du kan kun gå ud i entreen her fra.",
 has    light;

Object  lanterne "messinglanterne" room_bryggerset
 with   name 'messing' 'lanterne' 'lanternen' 'messinglanterne'
 'messinglanternen' 'lampe' 'lampen' 'lys' 'lyset' 'lygte' 'lygten',
        description "",
        short_name_def "messinglanternen",
        been_lit false,
        before [;
         Examine:
            print "Messinglanternen består af en glasklokke oven på
		en messingbeholder fyldt med lampeolie. ";
            if (self has on) "Lanternen udsender et svagt og gyldent lys, som
            lige nøjagtigt nok til at se ved.";
            if (self hasnt on) "Lanternen er slukket.";
         Drop:
            if (location hasnt light) "Det er ikke en god ide at smide sin
            eneste mulighed for at få lys.";
         Take:
            if (CanCarryMore()) {
                if (IsHeld(self) == false) {
                    move self to player;
                    "Du tager den specielle messinglanterne.";
                }
            }
         Burn:
	    <<SwitchOn self>>;
         SwitchOff:
	    if (player in room_kaelderen && self has on) "Det ville gøre
	    det meget svært at se her.";
            if (self hasnt on) "Den er allerede slukket.";
            if (self has on && IsHeld(self) == false) "Du må samle lanternen op, før du kan slukke den.";
            give self ~on;
            give room_kaelderen ~light;
            "Du puster til messinglanternens flamme og ilden slukker,
            hvorefter den udsender en underligt velduftende røg.";
         SwitchOn:
            if (self has on) "Det giver ikke meget mening at tænde
	    en tændt lanterne.";
            if (self has on && IsHeld(self) == false) "Du må samle lanternen op, før du kan tænde den.";
            if (location has light) "Du behøver ikke yderligere lys
	    for at kunne se i dette rum, så du vælger ikke at brænde
	    al lampeolien af nu.";
            if (IsHeld(lighter) && player in room_kaelderen) {
                give self on;
                give room_kaelderen light;
                move lanterne to room_kaelderen;
                move lighter to room_kaelderen;
                print "Du tænder lighteren og fører den hen til
                    messinglanternens væge. Dette giver liv til en svag
                    flamme, som lyser kælderen op. Du sætter lanternen og
                    lighteren på gulvet.^";
                if (self.been_lit == false) {
		    self.been_lit = true;
		    GetPoint(9, 3);
		}
		rtrue;
            } else {
                "Messinglanternens væge kræver en form for ild for at kunne
                antændes.";
            }
        ],
        after [;
         Drop:
            if (location has light) {
                "Du sætter lanternen på gulvet.";
            }
        ],
 has    uter switchable concealed;

Object  -> lighter "engangslighter"
 with   name 'engangslighter' 'engangslighteren' 'lighter' 'lighteren',
        short_name_def "lighteren",
        description "En tarvelig engangslighter lavet af gennemsigtig grøn plastik.",
        before [;
         Drop:
            if (location hasnt light) "Det er ikke en god ide at smide sin
            eneste mulighed for at få lys.";
	 SwitchOn, Burn:
	    "Du forsøger at tænde lighteren, men den giver blot en
	    gnist, hver gang du trykker på den.";
        ],
 has    uter concealed;

Object  saltsyre "flaske saltsyre" room_bryggerset
 with   name 'saltsyre' 'saltsyren' 'syre' 'syren' 'flaske' 'flasken',
        short_name_def "saltsyreflasken",
        empty false,
        description [;
            if (self.empty) "En tom grøn flaske, som engang indeholdt saltsyre.";
            else "En grøn flaske med saltsyre i.";
        ],
        before [;
         PutOn, ThrowAt, Insert:
            if (NotHeld(self)) "Du holder ikke saltsyreflasken.";
            if (self.empty) "Der er ikke mere saltsyre tilbage i flasken.";
            if (player in room_kaelderen && staalrist in room_kaelderen && taeppe.been_moved) {
                staalrist.decalcified = true;
                self.empty = true;
                "Du hælder forsigtigt saltsyren over stålristen. Kalken begynder
                at syde og sprutte, og efter noget tid er der ikke mere kalk
                tilbage.";
            }
            if (player in room_kaelderen && taeppe.been_moved == false && second == taeppe) {
                "At hælde saltsyre over et antikt og ægte tæppe kan du trods
                alt ikke få dig selv til, så hvis du vil lege den lille
                kemiker, må du flytte tæppet først.";
            }
            rfalse;
         Taste, Eat, Drink:
            "Dødningehovedet og den ætsende finger på flasken fortæller dig, at
            det ville være en dårlig idé.";
        ],
 has    uter edible concealed;

Object  spraydaaser "spraydåser" room_bryggerset
 with   name 'spraydåse' 'spraydåser' 'spraydåserne' 'dåse' 'dåser' 'dåserne' 'maling'
 'malingen' 'spray' 'sprayen',
        short_name_def "spraydåserne",
        description [;
            "En samling spraydåser i alle regnbuens farver.";
        ],
        before [;
         Wear: "Du har en naturlig skønhed, som SÅ meget make-up bare ville
            ødelægge.";
         PutOn:
            if (noun == nothing || noun == player || second == player) <<Wear self>>;
            if (player in room_dagligstuen) {
                if (noun == spraydaaser || second == juletrae) <<TegnPaa juletrae>>;
            }
            rfalse;
         Brug:
            if (noun == player || second == player) <<Wear self>>;
            "Du sprayer lidt maling ud i luften. Spraydåserne virker
            stadig, selvom de har stået ubrugt hen i nogen tid.";
	 TegnPaa, ThrowAt, Insert:
            if (player in room_dagligstuen) {
                if (noun == spraydaaser || second == juletrae) <<TegnPaa juletrae>>;
            }
            if (noun == nothing || noun == player || second == player)  <<Wear self>>;
            print "Du sprayer lidt maling ud i luften. Spraydåserne virker
                stadig, selvom de har stået ubrugt hen i nogen tid.";
            rfalse;
         Taste, Eat, Drink:
            "~Stærkt ætsende.~, står der på dåserne, så du lader være.";
        ],
 has    pluralname clothing concealed;

Object  fuglebur "fuglebur" room_bryggerset
 with   name 'fuglebur' 'fugleburet' 'bur' 'buret',
        short_name_def "fugleburet",
        description "Et for længst forladt fuglebur.",
 has    neuter container openable transparent concealed;

Object  pind "pegepind" room_bryggerset
 with   name 'pind' 'pinden' 'pegepind' 'pegepinden' 'spid' 'spiddet',
        short_name_def "pegepinden",
        stuck false,
        before [;
         Examine:
            if (self.stuck) "Pegepinden sidder fastspiddet i overvågningskameraet.";
            "En pegepind som man ser brugt til at pege med i skoler. Pegepinden er flere gange længere end dig.";
         Take:
            if (self.stuck) "Du kan ikke nå pegepinden, som sidder fastspiddet i kameraet.";
         Drop, Insert, PutOn, ThrowAt, PushDir:
            if (NotHeld(self)) "Du har ikke pegepinden.";
            if (second == overvaagningskamera && player in room_entreen) {
                if (overvaagningskamera.disabled == false && overvaagningskamera.broken == false) {
                    overvaagningskamera.broken = true;
                    move self to room_entreen;
                    give self concealed;
                    self.stuck = true;
                    print "Du sigter med pegepinden, som var du en spydkaster,
                        og affyrer pinden mod overvågningskameraet. Du rammer
                        plet: Lige ind i kameraets linse borer pegepinden sig.
                        Du har gjort overvågningens øje blindt og givet dig
                        selv og menneskene lidt privatliv tilbage. Ikke noget
                        man normalt forventer, at en Pind ville gøre.^";
		    GetPoint(2, 2);
                    rtrue;
                } else {
                    "Det ville være civil ulydighed, sådan at banke mere frihed ind
                    i et overvågningskamera.";
                }
            }
            if (second == roegalarm && player in room_koekkenet) {
                if (roegalarm hasnt on) {
                    give roegalarm on;
                    roegalarm.time_left = 1; StartDaemon(roegalarm);
                    move pind to location;
                    print "Du sigter med pegepinden, som var du en spydkaster,
                        og affyrer pinden mod røgalarmen. Du rammer
                        plet, og den begynder at hyle.";
                    if (roegalarm.hit == false) {
                        roegalarm.hit = true;
                        GetPoint(8, 2);
                    }
                    rtrue;
                }
            }
            if (second ~= overvaagningskamera && second ~= roegalarm && second ~= nothing) {
                "Det vil nok ikke have den store effekt.";
            }
        ],
 has    uter concealed;

Object  himmeljord "himmel og jord" room_bryggerset
 with   name 'himmel' 'himlen' 'jord' 'jorden' 'og',
        short_name_def "himlen og jorden",
        before [;
         Examine:  "Som nisse ved du, at der er mere mellem himmel og jord.";
         Take, Push, Touch: "Den er ikke særlig håndgribelig.";
         default: <<Examine self>>;
        ],
 has    pluralname scenery;

Object hylder "hylder" room_bryggerset
 with   name 'reol' 'reolen' 'reoler' 'reolerne' 'hylder' 'hylde' 'hylderne',
        short_name_def "hylderne",
        examined false,
        before [;
         Examine:
            if (self.examined == false) {
                self.examined = true;
                MakeNonConcealed(lanterne);
                MakeNonConcealed(lighter);
                MakeNonConcealed(saltsyre);
                MakeNonConcealed(spraydaaser);
                MakeNonConcealed(fuglebur);
                MakeNonConcealed(pind);
            }
            if (Locale(location, "På hylderne ser du blandt hundrede af andre ting", "På hylderne finder du også"))
                ".";
        ],
 has    uter scenery;


! ============================================================================ !
!Include "room_entreen.inf";
Room  room_entreen "Entreen, stueetagen" ! Room 2
 with   description "Du er i husets entre, det naturlige opholdssted for store
    mængder tøj og sko. Mod nord ligger køkkenet, mod øst dagligstuen, mens
    spisestuen og bryggerset ligger mod syd og vest. Trappen leder op til
    førstesalen, imens en hoveddør fører ud af huset. I loftet sidder et
    overvågningskamera.",
        name 'entre' 'entreen' 'entré' 'entréen',
        external_name 'entre' 'entreen' 'entré' 'entréen',
        u_to trappe,
        n_to room_koekkenet,
        e_to room_dagligstuen,
        s_to room_spisestuen,
        w_to room_bryggerset,
        out_to "Hvad er nu det? Overvejer du at flytte? Selvom andre hjem
    måske serverer mere risengrød, så flytter denne nisse først, når
    menneskene flytter.",
    before [;
     Smell:
        if (risengroed.announced == false) "En fugtigvarm lugt fra den store
        mængde sko og overtøj i entreen møder din nissenæse.";
        "Hvis du ikke tager helt fejl ... lugten af risengrød.";
     Listen:
        "Lyden af naboens hund, der bjæffer.";
    ],
    after [;
     Go:
	if (risengroed.announced && risengroed.seen_by_pc == false) {
	    if (noun == n_obj || noun == e_obj || noun == s_obj ||
		noun == w_obj || noun == d_obj || noun == out_obj) {
                print (string) random("En fjern duft af risengrød møder din
                                      næse. Du tror knapt dine sanser.", "Du
                                      kan lugte kanel. Kan det være rigtigt?");
                print "^";
            }
	}
    ],
    has    	light;

Object  hoveddoer "hoveddøren" room_entreen
 with   name 'hoveddør' 'hoveddøren' 'dør' 'døren',
        short_name_def "hoveddøren",
        description
            "Hoveddøren er lukket og låst.",
        before [;
         Open:
            <<Go out_obj>>;
        ],
 has    uter scenery door ~open locked;

Object  sko "sko" room_entreen
 with   name 'sko' 'skoen' 'skoene' 'støvler' 'støvlen' 'støvlerne',
        short_name_def "skoen",
        description [;
            if (IsHeld(sko)) "Det er en rød børnesko.";
            "Sko i alle størrelser, former og farver.";
        ],
        after [;
         Take:
            "Du tager en sko, en børnesko. Den ligger godt i hånden.";
        ],
        before [;
         Take:
            if (IsHeld(sko)) {
                "Du bytter skoen ud med en i en flottere farve.";
            }
         Wear:
            "Du har allerede gode nissesko på.";
         Drop, Insert, PutOn, ThrowAt:
            if (IsHeld(sko) == false) {
                "Du må først samle en sko op.";
            } else if (second == overvaagningskamera && player in room_entreen) {
                move sko to room_entreen;
                if (overvaagningskamera.disabled == false && overvaagningskamera.broken == false) {
                    overvaagningskamera.disabled = true;
                    print "Du sigter omhyggeligt og kaster skoen mod
                        overvågningskameraet. Du rammer plet og kameraet
                        drejer ind mod væggen, hvor det kan få lov at overvåge
                        en edderkop i sit spind.^";
		    GetPoint(2, 2);
		    rtrue;
                } else {
                    "Det ville være civil ulydighed, sådan at banke mere frihed ind
                    i et overvågningskamera.";
                }
                rtrue;
            } else if (player in room_loftet && risengroed.announced && rotter.friendly == false) {
                move sko to room_loftet;
                "Du kaster skoen mod rotterne, men de er for hurtige og
                undviger. Rotterne piber i kor: ~Din eneste chance er at lokke den
                dovne kat herop!~";

            } else if (second ~= nothing) {
                "Det vil nok ikke have den store effekt.";
            } else if (player in room_entreen) {
                move sko to room_entreen;
                give self concealed;
                "Du lægger skoen tilbage.";
            } else {
                move self to location;
                give self ~concealed;
                "Du smider skoen.";
            }
        ],
 has    uter concealed;


Object  edderkop "edderkop" room_entreen
 with   name 'edderkop' 'edderkoppen' 'spindelvæv' 'spindelvævet' 'spind' 'spindet',
        short_name_def "edderkop",
       	before [;
         Take: "Edderkoppen sidder højt oppe under loftet, så du kan ikke nå den.";
	 default:
	    if (overvaagningskamera.disabled) "En edderkop med meget
 	    lidt privatliv."; "En edderkop i sit spind.";
        ],
 has    uter scenery;

Object overvaagningskamera "overvågningskamera" room_entreen
 with   name 'overvågningskamera' 'overvågningskameraet' 'kamera' 'kameraet' 'cctv',
 with   short_name_def "overvågningskameraet",
        description "",
        disabled false,
        broken false,
        before [;
         Examine:
            if (self.disabled == false && self.broken == false)
                "Som et olmt stirrende glasøje opfanger kameraet enhver af
                dine bevægelser. Du føler dig ubehageligt overvåget.";
            if (self.disabled == true)
                "Kameraet filmer nu blot edderkoppespindet i loftet.";
            if (self.broken == true)
               "Kameraets linse er gennemboret af en pegepind og lader dig
                drille videre i fred.";
         Take, Touch, Push, Attack, SwitchOn, SwitchOff:
            "Kameraet er uden for din rækkevidde.";
        ],
 has    neuter concealed static;

Object  overtoej "overtøj" room_entreen
 with   name 'overtøj' 'overtøjet' 'tøj' 'tøjet',
        short_name_def "overtøjet",
        examined false,
        on_hook true,
        before [;
	 Take:
	    if (self.examined == false && self.on_hook) {
                self.examined = true;
                "Tøjet hænger lag på lag på en alt for lille knage.
		Hvis du bare tænker på at røre ved det, så falder
		det nok ned.";
            } else if (self.examined && self.on_hook) {
                self.on_hook = false;
                print "Du var advaret, og inden du når at gøre noget, falder
                    al tøjet ned på gulvet i en stor bunke. Det vil drive
                    ordensmennesker til vanvid.^";
		GetPoint(2, 1);
		rtrue;
            } else if (self.examined && self.on_hook == false) {
                "Tøjet ligger hulter til bulter på gulvet -- noget der nok skal
                drive ordensmennesker til vanvid.";
            }
         default: <<Take self>>;
        ],
 has    pluralname scenery ;

! ============================================================================ !
!Include "room_foerstesalen.inf";
Room  room_foerstesalen "På trappen, første sal" ! Room 1
 with   description "Du er på trappen på husets første sal. Op ad væggen står
    et gammelt bornholmerur, og på væggene hænger en række portrætter af
    familiens slægtninge. Herfra kan desuden mod nord finde kontoret, mod øst
    forældrenes soveværelse, mod syd børneværelset og mod vest et
    badeværelse. En stige fører op til loftet, mens trappen, du står på også
    fører ned til husets entre.",
        external_name 'trappeafsatsen' 'trappeafsats' 'trappen' 'trappe' 'førstesal' 'førstesalen',
        u_to stige,
        d_to room_entreen,
        n_to room_kontoret,
        e_to room_sovevaerelset,
        s_to room_boernevaerelset,
        w_to room_badevaerelset,
    numsounds 0,
    before [;
     Smell:
        if (risengroed.announced == false) "Lugten af ingen risengrød.";
        "Hvis du ikke tager helt fejl ... lugten af risengrød.";
     Listen: if (noun == nothing) {
         self.numsounds = 0;
         if (rotter in room_loftet) "Hvad er nu det? Lyden af rotter på loftet?";
         print "Du kan høre ";
         if (boern in room_boernevaerelset) self.numsounds++;
         if (foraeldre in room_sovevaerelset) self.numsounds++;
         if (bornholmerur has on) self.numsounds++;
         if (boern in room_boernevaerelset) {
             print "nogle glade børnestemmer";
             if (self.numsounds > 2) print ", ";
             if (self.numsounds == 2) print " og ";
             if (self.numsounds == 1) ".";
             self.numsounds--;
         }
         if (foraeldre in room_sovevaerelset) {
             print "nogle dybe snorkelyde";
             if (self.numsounds > 2) print ", ";
             if (self.numsounds == 2) print " og ";
             if (self.numsounds == 1) ".";
             self.numsounds--;
         }
         if (bornholmerur has on) {
             print "et tikkende bornholmerur";
             if (self.numsounds > 2) print ", ";
             if (self.numsounds == 2) print " og ";
             if (self.numsounds == 1) ".";
             self.numsounds--;
         }
         if (boern in room_spisestuen || foraeldre in room_spisestuen)
             "nogle menneskestemmer fra underetagen.";
         rtrue;
     }
    ],
    after [;
     Go:
	if (risengroed.announced && risengroed.seen_by_pc == false) {
	    if (noun == n_obj || noun == e_obj || noun == s_obj ||
		noun == w_obj || noun == u_obj || noun == d_obj) {
		    print (string) random("Du mødes af en udtalt duft af
                                          risengrød med et strejf af kanel.",
                                          "Den umiskendelige duft af
                                          risengrød.");
                print "^";
	    }
	}
    ],

 has    light;

Object  stige "stige" room_foerstesalen
 with   name 'stige' 'stigen',
        short_name_def "stigen",
        description
            "Stigen, som leder op til loftet, mangler et trin eller
	    to.",
        door_dir [;
            if (self in room_loftet) return d_to;
            return u_to;
        ],
        door_to [;
            if (self in room_loftet) return room_foerstesalen;
            return room_loftet;
        ],

        found_in room_loftet room_foerstesalen,
 has    uter scenery static door open;

Object  trappe "trappe" room_foerstesalen
 with   name 'trappe' 'trappen',
        short_name_def "trappen",
        description
            "Trappen, som leder ned til stueetagen, knirker og knager, selv når en nisser betræder den.",
        door_dir [;
            if (self in room_foerstesalen) return d_to;
            return u_to;
        ],
        door_to [;
            if (self in room_foerstesalen) return room_entreen;
            return room_foerstesalen;
        ],
	found_in room_foerstesalen room_entreen,
 has    uter scenery static door open;

Object  portraetmaleri "portrætmalerier" room_foerstesalen
 with   name 'portrætmaleri' 'portrætmalerier' 'portrætmaleriet' 'portrætmalerierne'
 'maleri' 'malerier' 'maleriet' 'malerierne' 'billede' 'billeder' 'billederne'
 'billedet' 'portræt' 'portrætter' 'slægtning' 'slægtninge' 'slægtningene',
        short_name_def "portrætmalerierne",
        description [;
	    print "En samling vellignende malerier af familiens
		olde- og tipoldeforældre, som du alle har drillet
		gennem tiderne. ";
	    if (self.changed == false) "Malerierne hænger på en snorlige
 	    række."; "Malerierne hænger pilskævt.";
	],
        changed false,
        before [;
         Push, Pushdir, Touch, Take, Remove:
            if (self.changed == false) {
                print "Et efter et puffer du til malerierne, så de alle sammen
                    hænger pilskævt. Det vil gøre visse af familiemedlemmerne
                    rasende.^";
		self.changed = true;
		GetPoint(1, 1);
		rtrue;
            } else {
                "Hvis du piller mere ved malerierne, så falder de
		nok ned, og du har ikke lyst til at bære malerierne
		som hovedbeklædning i dag.";
            }
         Attack:
            print "Sådan en voldsom handling ville nok få malerierne
		til at falde ned fra væggen, ";
            if (self.changed) {
                "og du har ikke lyst til at bære malerierne som
		hovedbeklædning.";
            } else {
                "og selvom du er så sulten, at du er parat til at gå
		til yderligheder, så er der nok en mindre voldsom
		løsning her.";
            }
        ],
 has    uter scenery static;

Object  bornholmerur "bornholmerur" room_foerstesalen
 with   name 'bornholmerur' 'bornholmeruret' 'ur' 'uret' 'bornholmer' 'bornholmeren' 'klokken' 'pendul' 'pendulet',
        short_name_def "bornholmeruret",
        description "",
        clock_changed false,
        before [;
         Take, Remove, Push, Pull:
            "Det er et bornholmerur, ikke et lommeur, så du må lade
	    uret stå.";
         Open:
            print "Du åbner bornholmer urets låge og ser to lodder og et pendul. ";
            if (self has on) "Pendulet svinger langsomt fra side til side.";
            else "Pendulet hænger helt stille.";
         Close:
	    "Du sørger for at bornholmeruret er helt lukket.";
         Enter:
            "Det er der ikke tid til.";
         Examine:
            print "Bornholmeruret er gammelt og lidt afskallet i malingen. ";
            if (self has on) {
                "Fra urets indre kan du høre en langsom og dyb
		tikken.";
            } else {
                "Uret er helt stille nu, da det er sat i stå.";
            }
            rtrue;
         Set, SetTo:
            print "Du sætter uret til et tilfældigt klokkeslæt.^";
	    if (self.clock_changed == false) { GetPoint(1, 1); self.clock_changed = true; }
 	    rtrue;
         SwitchOff:
            if (self has on) {
                give self ~on;
                print "Du sætter bornholmeruret i stå. Det skal nok gøre
		    resten af dagen hektisk for menneskene.^";
		if (self.clock_changed == false) { GetPoint(1, 1); self.clock_changed = true; }
		rtrue;
            } else {
                "Uret er allerede sat i stå.";
            }
         SwitchOn:
            if (self hasnt on) {
                give self on;
                "Du sætter uret i gang igen, men på et tilfældigt klokkeslet.";
            } else {
                "Uret går allerede.";
            }
         SkruFrem:
            print "Du sætter uret frem til et tilfældigt klokkeslæt. Det skal nok
            gøre resten af dagen hektisk for menneskene.^";
	    if (self.clock_changed == false) { GetPoint(1, 1); self.clock_changed = true; }
	    rtrue;
         SkruTilbage:
            print "Du sætter uret tilbage på et tilfældigt klokkeslæt. Det skal nok
            gøre resten af dagen hektisk for menneskene.^";
	    if (self.clock_changed == false) { GetPoint(1, 1); self.clock_changed = true; }
	    rtrue;
         Touch, Push:
            print "Du lægger hånden på bornholmeruret";
            if (self has on) " og kan mærke svage vibrationer fra pendulets
            svingen indeni."; " og mærker intet uventet.";
         Listen: if (self has on) "Du hører sløve og dybe tiktak fra urets gang. Tiden, den går,
            og en gang i mellem høres mekaniske lyde fra urets fjedre og
            tandhjul."; if (risengroed.announced == false) "Du hører intet fra
            uret. Det er som om, tiden står stille."; "Du hører ingen lyde fra urets indre.";
         default: "Sådan virker dette ur ikke.";
        ],
 has    pluralname scenery switchable on openable ~open;

! ============================================================================ !
!Include "room_kaelderen.inf";
Room  room_kaelderen "Kælderen"
    with   description [;
	       print "Kælderen lægger hus til et store oliefyr, som fylder
                   det meste af rummet. ";
	       if (taeppe.been_moved == false) print "Gulvet er på usædvanlig vis dækket
	           af et stort ægte tæppe. "; else print "I gulvet kan du
		   se en stålrist. ";
	       "En trappe leder op til køkkenet.";
	   ],
    name 'kælder' 'kælderen',
    external_name 'kælder' 'kælderen',
    u_to kaeldertrappe,
    d_to staalrist,
    cant_go [;
        if (staalrist has open) "Du kan kun gå op i køkkenet eller ned i afløbet
        her fra."; "Du kan kun gå op i køkkenet her fra.";
    ],
    before [;
     Listen: "Lyden af stilhed.";
     Go, Climb:
        if (noun == d_obj && staalrist has open) {
            if (NumCarried() > 1) {
                print "Afløbet er så snævert, at du må smide alt, hvad du
                    bærer på først.^";
                rtrue;
            }
        }
        if (noun == d_obj && staalrist hasnt open) {
            if (taeppe.been_moved == false) "Dette er en kælderen, den
            nederste etage i et hus.";
            print "Stålristen gør at større ting, som nisser ikke falder ned i
                afløbet.^";
            rtrue;
        }
    ],
    has    ~light;

Object  kaeldertrappe "kældertrappe" room_kaelderen
 with   name 'trappe' 'trappen' 'kældertrappe' 'kældertrappen',
        short_name_def "kældertrappen",
        description
            "En slidt kældertrappe.",
        door_to room_koekkenet,
        door_dir u_to,
 has    uter scenery static door open;

Object  oliefyr "oliefyr" room_kaelderen
 with   name 'oliefyr' 'oliefyret' 'oliefyrret' 'fyr' 'fyret' 'fyrret',
        short_name_def "oliefyret",
        description [; ! Denne måde for at få "... er slukket/tændt" på samme linje
            print "Et stort skrummel af et oliefyr. ";
            if (self has on && self.hot == true) "Fyret er rødglødende og dets
            termometer står på 84 grader.";
            if (self hasnt on && self.hot == false) "Fyret er slukket og dets
            termometer står på 13 grader.";
            if (self has on && self.hot == false) "Fyret er tændt og dets
            termometer står på 20 grader.";
        ],
        hot false,
	point_given false,
        before [;
         Examine: PrintOrRun(self, description); rtrue; ! Undgå "is on" description
         SwitchOn:
            if (self has on) "Fyret er allerede tændt.";
            give self on;
            print "Du tænder fyret. Det står på 20 gader.";
	    if (self.point_given == true) { TakePoint(3, 2); self.point_given = false; }
	    rtrue;
         SkruOp:
            if (self hasnt on) <<SwitchOn self>>;
            if (self.hot == true) "Fyret kan ikke skues højere op.";
            self.hot = true;
            print "Du skruer oliefyret op til 84 grader, og efter nogle få
	    minutter kan du mærke sveden pible frem på din pande. Det
	    bliver varmt som i en finsk sauna.^";
	    if (self.point_given == false) { GetPoint(3, 2); self.point_given = true; }
	    rtrue;
	 SkruNed:
            if (self hasnt on) "Fyret er ikke tændt.";
            if (self.hot == true) {
		self.hot = false;
		print "Du skruer fyret ned til 20 grader igen.";
		if (self.point_given) { TakePoint(3, 2); self.point_given = false; }
		rtrue;
	    } else {
		<<SwitchOff self>>;
	    }
         SwitchOff:
            if (self has on) give self ~on;
            self.hot = false;
            print "Du slukker fyret. Snart vil der være hundekoldt i hele huset.^";
	    if (self.point_given == false) { GetPoint(3, 2); self.point_given = true; }
	    rtrue;
        ]
 has    neuter switchable on scenery;

Object  staalrist "stålrist" room_kaelderen
 with   name 'stålrist' 'stålristen' 'rist' 'risten' 'stålrist' 'stålristen',
        short_name_def "stålristen",
        description "Stålristen fungerer som afløb ned til kloaksystemet under huset.",
        decalcified false,
        entered false,
        door_to room_labyrint_0,
        door_dir d_to,
        before [;
         Take, Open, Push, Remove:
            if (taeppe.been_moved == false) "Du ser ikke noget sådant her.";
            if (self.decalcified == false) "Stålristen er kalket fuldstændig til og sidder ubevægeligt fast.";
            if (self.decalcified == true && self has open) {
                "Risten er allerede taget af afløbet.";
            }
            if (self.decalcified == true && self hasnt open) {
                give self open;
                "Du løfter risten af. En varm og stinkende luft strømmer
                dig op i hovedet fra et mørkt og beskidt afløb.";
            }
         Close:
            if (taeppe.been_moved == false) "Du ser ikke noget sådant her.";
            if (self has open) {
                give self ~open;
                "Du lægger stålristen tilbage på afløbet.";
            }
         Go, Climb, Enter:
            if (taeppe.been_moved == false) "Du ser ikke noget sådant her.";
            if (self has open)
                <<Go d_obj>>;
            "Stålristen gør at større ting, som nisser ikke falder ned i afløbet.";
         default:
            if (taeppe.been_moved == false) "Du ser ikke noget sådant her.";

        ],
	found_in room_kaelderen room_labyrint_0,
 has    uter scenery door ~open ~openable;

Object  afloeb "afløb" room_kaelderen
 with   name 'afløb' 'afløbet' 'kloak' 'kloakken',
        short_name_def "afløbet",
        before [;
         Examine:
            "Afløbet under risten er ikke særligt stort men gør det
            muligt for vand at finde vej til kloakkenen.";
         Go, Enter, Climb:
            if (staalrist has open)
                <<Go d_obj>>;
        ],
 has    neuter static concealed;

Object  taeppe "ægte tæppe" room_kaelderen
 with   name 'det' 'ægte' 'tæppe' 'tæppet',
        short_name_def "tæppet",
        description "Et ægte tæppe, som man normalt ikke ville lægge
	    på gulvet af en fyrkælder.",
        been_moved false,
        before [;
         Take, Remove, Push, Pushdir:
            if (self.been_moved) "Du flytter lidt mere rundt på tæppet. Der er ikke fejet noget yderligere ind
            under det.";
            self.been_moved = true;
            move staalrist to room_kaelderen;
            "Du hiver med stor iver i det tunge tæppe, og under
	    tæppet finder du en stålrist i gulvet.";
        ],
 has    neuter static concealed;


! ============================================================================ !
!Include "room_koekkenet.inf";
Room  room_koekkenet "Køkkenet"
 with   description "Dette er ikke just et samtalekøkken, men derimod et lille
    og praktisk køkken på fire kvadratmeter. Køleskabet minder dig
    selvfølgelig om din sult efter risengrød, og du har tidligere erfaret, at
    der kan laves en intet mindre end eminent god risengrød i dette rum. Den
    flotte røde brødrister på bordet fanger desuden din opmærksomhed. I loftet
    sidder en røgalarm, imens der i gulvet er en lem, der fører ned til
    kælderen.",
    name 'køkken' 'køkkenet',
    external_name 'køkken' 'køkkenet',
        s_to room_entreen,
        out_to room_entreen,
        d_to kaelderlem,
        cant_go "Du kan kun gå ud i entreen eller ned i kælderen her fra.",
    before [;
     Listen:
        if (noun == nothing) {
            if (roegalarm has on) "Du hører mest af alt en røgalarm.";
            "Lyden af et køleskab, der køler.";
        }
     Smell: if (noun == nothing) {
         if (risengroed.announced) "Her kunne godt lugte af ... nylavet
         risengrød! Men den er ikke at finde her i køkkenet længere."; else
             "Her lugter desværre langt fra af risengrød.";
     }
     Go:
        if (noun == d_obj) {
            if (room_kaelderen hasnt light && lanterne in player && lanterne has on) give room_kaelderen light;
        }
    ],
    after [;
     Go:
	if (noun == u_obj) {
            if (lanterne in player) give room_kaelderen ~light;
	}
	if (risengroed.announced && risengroed.seen_by_pc == false) {
	    if (noun == u_obj || noun == n_obj) {
                print "Køkkenet dufter af risengrød! Der er nogen, der har
                    lavet risengrød fornylig i dette køkkenet. Men du kan ikke
                    se risengrøden her.^";
            }
	}
    ],
    has    light;

Object  -> kaelderlem "lem i køkkengulvet"
 with   name 'lem' 'lemmen' 'dør' 'døren',
        short_name_def "kælderlemmen",
        description
            "En ganske almindelig lem i gulvet.",
        door_to room_kaelderen,
        door_dir d_to,
 has    uter scenery door openable open;

Object  -> koeleskab "køleskab"
 with   name 'køleskab' 'køleskabet' 'skabet' 'skab' 'køleren' 'køler',
        short_name_def "køleskabet",
        opened false,
        before [;
         Take: "Køleskabet vejer næsten 100 kg, så held og lykke med det.";
         Smell: "Køleskabet lugter langt fra af risengrød.";
         Examine:
            give self open;
            print "Køleskabet indeholder masser af varer her op til jul, men
                du ser med det samme, at der ingen risengrød er. ";
	    rfalse;
         Open:
            if (self has open) "Køleskabet er allerede åbent.";
            print "Du åbner køleskabet. ";
            <Examine self>;
            rtrue;
         Close:
            if (self has open) {
                give self ~open;
                "Du lukker køleskabet.";
             } else {
                 "Køleskabet er allerede lukket.";
             }
        ],
 has    neuter scenery container ~open;

Object  flaeskesteg "flæskesteg" koeleskab
 with   name 'flæskesteg' 'flæskestegen' 'steg' 'stegen',
        short_name_def "flæskestegen",
        description "En pænt stor flæskesteg.",
        before [;
         Eat, Taste:
            "Den er ikke tilberedt endnu, så du vælger at lade være.";
         Take: "Du har ikke lyst til flæskesteg, så du lader dem ligge.";
        ],
 has    uter edible;

Object  kartofler "kartofler" koeleskab
 with   name 'kartofler' 'kartoflerne',
        short_name_def "kartoflerne",
        description "En pose små kartofler til bruning.",
        before [;
         Eat, Taste:
            "De er ikke tilberedt endnu, så du vælger at lade være.";
         Take: "Du har ikke lyst til ukogte kartofler, så du lader dem ligge.";
        ],
 has    pluralname edible;

Object  tarteletter "tarteletter" koeleskab
 with   name 'tartelet' 'tartellet' 'tarteletter' 'tarteletten',
        short_name_def "tarteletterne",
        description "En bakke med fyldte tarteletter.",
        before [;
         Eat, Taste: "De er ikke varme og smager ikke af risengrød.";
         Take: "Du har ikke lyst til kolde tarteletter, så du lader dem ligge.";
        ],
 has    pluralname edible;

Object  franskbroed "franskbrød" koeleskab
 with   name 'franskbrød' 'franskbrødet' 'brød' 'brødet' 'toast' 'toasten'
             'skive' 'skiver' 'toastbrød' 'toastbrødet',
        short_name_def "franskbrødet",
        description "En pakke toastbrød; skiveskåret.",
        toastedness 0,
        before [;
         Examine:
            if (self.toastedness == 0){
                "En pakke toastbrød; skiveskåret og med godt
		    med E-numre på varedeklarationen. Udløbsdatoen
		    var for nogle dage siden. ";
            }
            if (self.toastedness == 1){
                "Franskbrødet er gyldent. ";
            }
            if (self.toastedness > 1){
                "Franskbrødet er helt sort. ";
            }
         Smell:
            if (self.toastedness == 0){
                "Brødet ser lidt tørt ud og må nok en gang på brødristeren for
                at blive spiseligt.";
            }
            if (self.toastedness == 1){
                "Brødet lugter af velristet brød.";
            }
            if (self.toastedness > 1){
                "Brødet lugter af brændt brød.";
            }
         Eat, Taste:
            if (self.toastedness > 1){
                "Brødet er uspiseligt nu.";
            }
            "Hvis du ikke snart får noget risengrød, så kan det godt være, at
            du må bide i det tørre brød. Men du er ikke så desperat endnu.";
         Insert:
            if (second == broedrister && franskbroed in broedrister) "Der er allerede brød i brødristeren.";
         PutOn:
            if (second == broedrister) <<Insert self broedrister>>;
        ],
 has    neuter edible;


Object  roegetsild "røget sild" koeleskab
 with   name 'røgede' 'røget' 'sild' 'silden' 'fisk' 'fisken',
        short_name_def "den røgede sild",
        description "En flot røget sild, rødlig i farven.",
        before [;
         Examine:
            "En flot røget sild, der er rødlig i farven. Den lugter langt fra
            af risengrød og er ikke egnet som hverken menneske- eller nisseføde.";
         Smell:
            "Fisken lugter virkelig meget af fisk -- normalt ikke et godt tegn.";
         Eat, Taste:
            "Hvis du har prøvet at lugte til fisken, så ved du, at
	    den ikke er egnet til at blive spist af hverken
	    mennesker eller nisser.";
         Give:
            if (second == kat && kat.sleeping == false) "Du lader katten nippe lidt til den røgede
            sild, hvilket den lader til at blive endnu mere sulten af."; "Katten
                lader ikke til at være interesseret i tilbuddet lige nu. Måske senere.";
        ],
 has    uter edible;

Object  nisseoel "nisseøl" koeleskab
 with   name 'nisseøl' 'nisseøllen' 'nisseøllet' 'øl' 'øllet' 'øllen',
        short_name_def "nisseøllen",
        before [;
         Examine:
            "Friskbrygget nisseøl. Der findes igen bedre drik for en nisse.";
         Smell:
            "Øllet dufter sødt.";
         Eat, Drink, Taste:
            "Du drikker lidt nisseøl, og som enhver god aperitif skærper det
            blot din sult.";
        ],
 has    uter edible;

Object  foedevarer "assorterede og ikke særlig interessante fødevarer" koeleskab
 with   name 'færdigret' 'færdigretter' 'færdigretterne' 'grøntsag' 'grøntsager' 'grøntsagerne' 'fødevare' 'fødevarer' 'fødevarerne',
        short_name_def "fødevarerne",
        before [;
         Examine:
	    "Det er bare nogle uinteressante færdigretter, nogle
	    grøntsager og andre almindelige fødevarer. Fjernt fra at
	    være risengrød.";
         Take: "Hvilken vare vil du tage?";
	 default: <<Examine self>>;
        ],
 has    pluralname static;

Object  broedrister "brødrister" room_koekkenet
 with   name 'brødrister' 'brødristeren' 'toaster' 'toasteren' 'rister' 'risteren',
        short_name_def "brødristeren",
        description [;
            if (self.broken) "En godt smadret brødrister.";
            "En brødrister med plads til to vertikalt placerede stykker brød.";
        ],
        wet false,
        broken false,
        daemon [;
            if (self.time_left > 0) {
                (self.time_left)--;
                if (franskbroed in self) franskbroed.toastedness++;
                if (IndirectlyContains(location,self)) {
                    print "^Brødristerens ur tikker lystigt.";
                    if (franskbroed.toastedness >= 2){
                        print " Franskbrødet begynder at ryge kraftigt, og
                            derefter begynder det at brænde.";
                        give roegalarm on;
                        roegalarm.time_left = 1; StartDaemon(roegalarm);
                        give self ~on;
                        StopDaemon(self);
                        print " Røgalarmen i loftet tænder.^";
                        if (roegalarm.activated == false) {
                            roegalarm.activated = true;
                            GetPoint(8, 2);
                        }
                        rtrue;
                    }
                    if (franskbroed.toastedness == 1) print " Franskbrødet
                        ser ud til at blive langsomt gyldent.^";
                }
            } else {
                if (IndirectlyContains(location,self) && self has on) {
                    print "^Brødristeren slukker igen.^";
                    give self ~on;
                }
            }
        ],
        time_left 0,
        after [;
         SwitchOn:
            if (self.broken) "Brødristeren er brændt sammen og virker ikke længere.";
            if (self.wet && self.broken == false) {
                self.broken = true;
                print "Du tænder for brødristeren, og med det samme begynder gnister
                og røg at stå op ad den. Den brænder fuldstændig sammen.^";
                GetPoint(8, 2);
                rtrue;
            }
            self.time_left = 1;
            StartDaemon(self);
         SwitchOff:
            !StopDaemon(self);
         Receive:
            if (noun == franskbroed) "Du tager to stykker brød og lægger dem i brødristeren.";
        ],
        before [;
         SwitchOff:
            "Brødristeren er automatisk, så du må vente til den slukker selv.";
         SwitchOn:
            if (NumObjects(self) == 0 && self.wet == false) { "Brødristeren er tom, og selvom du
                godt kunne bruge lidt varme i denne kolde tid, så vælger du at
                spare på energien i stedet."; }
            if (self has on) "Den er allerede tændt.";
         Take: "Denne brødrister er desværre ikke en bærbar model.";
         Examine:
            if (self has on) "Brødristeren er tændt og dens glødetråde er helt røde.";
            if (self hasnt on) "Brødristeren er slukket.";
         Receive:
            if (noun ~= franskbroed && noun ~= nisseoel) "Det er brødristeren ikke beregnet til.";
            if (noun == nisseoel) {
                move nisseoel to koeleskab;
                if (broedrister.wet) print "Du hælder endnu mere"; else print
                    "Du hælder en god sjat";
                broedrister.wet = true;
                " nisseøl i brødristeren.";
            }
        ],
 has    uter container open switchable ~on static concealed;

Object  roegalarm "røgalarm" room_koekkenet
 with   name 'røgalarm' 'røgalarmen' 'alarm' 'alarmen',
        short_name_def "røgalarmen",
        activated false,
        hit false,
        daemon [;
            if (self.time_left > 0) {
                (self.time_left)--;
                if (IndirectlyContains(location,self))
                    print "^Røgalarmen udsender en øredøvende hyletone, og
                        husets familie kommer styrtende ind i køkkenet for at
                        se, hvad der brænder.^";
            } else {
                if (IndirectlyContains(location,self) && self has on) {
                    give self ~on;
                    print "^Røgalarmen slukker efter fem lange minutter. ";
                    if (TotalScore() < OK_SCORE) "~Det er nok bare nissen~, spøger
                    famlien, men de lægger desværre ikke to og to sammen og
                    laver dig noget risengrød."; else "";
                }
            }
        ],
        time_left 0,
        before [;
         Examine:
            if (self hasnt on) {
                if (self.activated == false)
                    "Mørkegul efter mange års stegeos. Om den virker
                    vides ikke.";
                else "Røgalarmen er dejlig stille nu.";
            } else {
                "Den hyler frygtelig højt.";
            }
         SwitchOff, SwitchOn, Take, Touch, Push:
            "Røgalarmen sidder alt for højt oppe til, at du kan nå den.";
        ],
 has    scenery switchable;

! ============================================================================ !
!Include "room_kontoret.inf";
Constant MAXPASSWORDLENGTH 35;
Array skrevet_password -> MAXPASSWORDLENGTH;
Array oprindeligt_password -> 6 '1' '2' '3' '4' '5' '6';
Array rigtigt_password -> 6 '1' '2' '3' '4' '5' '6';

Room  room_kontoret "Kontoret" ! Room 6
    with   description "Kontoret er et lille rum uden vinduer, mest
    af alt egnet til forskellige kontor- og computeropgaver. Reoler
    med ringbind og bøger står langs alle væggene, og på et stort
    skrivebord står en computer.",
    external_name 'kontor' 'kontoret' 'computerrum' 'computerrummet',
    s_to room_foerstesalen,
    out_to room_foerstesalen,
    cant_go "Du kan kun gå ud på førstesalen her fra.",
    before [;
     Smell:
	if (noun == nothing) "Lugten af elektronik og papir.";
     Listen:
        if (noun == nothing) {
            if (computer has on) {
                "Lyden af en computerblæser, en skramlende harddisk og din
                mave, der rumler.";
            } else {
                "Lyden af din mave, der rumler.";
            }
        }
     default:
        if (teksteventyr.play_state == 1) {
            teksteventyr.play_state = 2;
            if (noun == n_obj) {
		Bold("^In Forest^");
	     	print "You are in open forest, with a deep valley to one side.^^";
	    }
	    if (noun == e_obj || noun == adventurebuilding) {
                Bold("^Inside Building^");
	     	print "You are inside a building, a well house for a large
		    spring.^There are some keys on the ground here.^There is a shiny brass lamp nearby.^There is food here.^^";
	    }
	    if (noun == s_obj) {
		Bold("^In A Valley^");
		print "You are in a valley in the forest beside a stream
		    tumbling along a rocky bed.^^";
	    }
	    if (noun == w_obj) {
		Bold("^At Hill In Road^");
		print "You have walked up a hill, still in the forest. The
		    road slopes back down the other side of the hill.
		    There is a building in the distance.^^";
	    }
	    teksteventyr.play_state = 0;
	    "Du bliver grebet af teksteventyret, hvori du ender op i
	    en underjordisk hule, hvor du skal samle en masse
	    skatte. Men du er alt for sulten til, at du kan forsætte
	    med at spille ret meget længere, så du gemmer spillet og
	    afslutter det.";
        }
    ],
    has    light;

Object -> adventurebuilding "Inside building"
 with   name 'house' 'building' 'hus' 'huset',
        short_name_def "bygningen",
        before [;
         default: <<Go e_obj>>;
        ],
 has    uter scenery;

Object -> reoler "reoler"
 with   name 'reol' 'reolen' 'reoler' 'reolerne',
        short_name_def "reolerne",
        before [;
         Examine:
            "Reolerne er nok mest interessante for folk med interesse for bogholderi.";
        ],
 has    uter scenery;

Object -> boeger "bøger"
 with   name 'bog' 'bøger' 'bøgerne',
        short_name_def "bøgerne",
        before [;
         Examine, Take, Consult:
            "Bøgerne er primært ordbøger og bøger om regnskaber og hvordan man
            bruger computerprogrammer. Du finder desværre ikke din
            yndlingsbog: ~Risengrød på 100 måder.~";
        ],
 has    uter scenery;

Object -> ringbind "ringbind"
 with   name 'ringbind' 'ringbindene',
        short_name_def "ringbindene",
        before [;
         Examine, Take, Consult:
            "Ringbindene ville kun have din interesse lige nu, hvis de indeholdt opskrifter på risengrød. Det gør disse ikke.";
        ],
 has    uter scenery;

Object -> skrivebord "skrivebord"
 with   name 'skrivebord' 'skrivebordet' 'bord' 'bordet',
        short_name_def "skrivebordet",
        description "",
        before [;
         Examine:
            "Skrivebordet er lavet af mørkt træ og har skuffer i begge sider.";
        ],
 has    neuter scenery;

Object -> skuffe "skuffe"
 with   name 'skuffe' 'skuffen' 'skuffer' 'skufferne',
        short_name_def "skuffen",
        description "",
        before [;
         Examine:
            if (self has open) {
		print "Den øverste skuffe er åben, og du kan se en
 masse stykker papir i den. ";
		if (kontaktlim in self && lineal in self) {
		    "Der er også en tube kontaktlim og en lineal.";
		} else {
		    if (kontaktlim in self) "Der er også en tube kontaktlim.";
		    if (lineal in self) "Der er også en gennemsigtig platisklineal.";
		}
            } else {
		"Skrivebordets skuffer er lukkede.";
	    }
         Open:
            if (self hasnt open) {
                give self open;
                "Du åbner en af de øverste skuffer og finder en
		masse stykker papir og nogle kontorartikler, blandt
		andet en tube kontaktlim og en lineal.";
            }
         Take:
            <<Open self>>;
        ],
 has    uter scenery container openable;

Object -> -> papir "papir"
 with   name 'papir' 'papiret' 'papirer' 'papirerne' 'besked' 'beskeden' 'note' 'noten',
        short_name_def "papirene",
        description "",
        before [;
         Examine:
            kodeord.known = true;
            "Du tager nogle tilfældige stykker papir op: En ubetalt
	    regning, en lønseddel, nogle breve, og et papir med
	    teksten: ~login pc: 123456~";
         Take:
	    if (kodeord.known == false) <<Examine self>>;
            "Din hukommelse er god nok til at kunne huske et 6-tegn
	    langt kodeord.";
        ],
 has    pluralname scenery;

Object -> -> kontorartikler "kontorartikler"
 with   name 'kontorartikler' 'kontorartiklerne',
        short_name_def "kontorartiklerne",
        description "Der er blandt andet noget lim og en lineal.",
        before [;
         Take:
            "Du må være mere specifik. Du er ikke ved at starte en samling af
            kontorartikler.";
        ],
 has    pluralname scenery;

Object -> -> kontaktlim "tube kontaktlim"
 with   name 'kontaktlim' 'kontaktlimen' 'lim' 'limen',
        short_name_def "kontaktlimen",
        description "En gul tube kontaktlim, trykket flad på midten.",
        before [;
	 Take:
            if (CanCarryMore()) {
                if (IsHeld(self) == false) {
                    move self to player;
	            print "Du tager tuben med kontaktlim.^";
                    rtrue;
                }
            }
	 PutOn, Insert:
            if (IsHeld(self) == false) {
                if (CanCarryMore()) {
                    if (IsHeld(self) == false) {
                        move self to player;
                    }
                }
            }
	    if (second == player) "Det er ikke sundt at indtage kontaktlim.";
            if (second == skuffe) {
                move kontaktlim to skuffe;
                print "Du lægger limen tilbage i skuffen.^";
                rtrue;
            }
	    if (second == computermus) {
		if (computermus.glued) "Musen sidder allerede godt fast.";
		computermus.glued = true;
                if (self notin player) print "Du tager limen og ";
                else print "Du tager hætten af limtuben og ";
		print "putter godt med kontaktlim under musen og på bordet.
                    Efter nogle minutter presser du musen ned mod limen på
                    bordet. Der kommer til at gå lang tid, før musen kommer ud
                    at løbe igen. Du lægger limen tilbage i skuffen.^";
                move kontaktlim to skuffe;
		GetPoint(6, 1);
		rtrue;
	    }
	    if (second == computer) {
		if (computer.glued) "CD-ROM-drevet sidder allerede godt og grundigt fast.";
		computer.glued = true;
                if (self notin player) print "Du tager limen, ";
                else print "Du tager hætten af limtuben, ";
		print "åbner computerens CD-ROM-drev og fylder det med lim. Du
                    lukker drevet og lægger limen tilbage i skuffen.^";
		move kontaktlim to skuffe;
		GetPoint(6, 1);
		rtrue;
	    }
	    if (second == tastatur) {
		if (tastatur.glued) "Tasterne er allerede godt klistrede.";
		tastatur.glued = true;
                if (self notin player) print "Du tager limen og ";
                else print "Du tager hætten af limtuben og ";
		print "lægger en klat kontaktlim under nogle af de mest brugte
                    taster på tastaturet: E, R, N, D og T. Derefter lægger du
                    limen tilbage i skuffen.^";
		move kontaktlim to skuffe;
		GetPoint(6, 1);
		rtrue;
	    }
            if (player notin room_kontoret) "Lim hører kun til i kontoret, og det
            ville i øvrigt værre noget værre svineri.";
	    "Det ville være noget værre svineri.";
	 Smell:
	    "Det bliver man dum af.";
	 Taste, Drink:
	    "Kun til udvortes brug.";
        ],
 has    uter;

Object -> -> lineal "lineal"
 with   name 'lineal' 'linealen' 'plastiklineal' 'plastiklinealen',
        short_name_def "linealen",
        description "En slidt, gennemsigtig plastiklineal.",
 has    uter;

Object -> computermus "computermus"
 with   name 'computermus' 'computermusen' 'mus' 'musen' 'håndrotte' 'håndrotten',
        short_name_def "computermusen",
        description [;
	    print "Computermusen, også kendt som ~håndrotten~, er
	    en af de helt gamle med to knapper og en kugle
	    underneden. Du skubber til musen, ";
	    if (self.glued) "men den rokker sig ikke ud af stedet,
	    fordi den er limet fast til bordet."; "og den bevæger sig
	    	forholdsvist nemt rundt på bordet.";
	],
	glued false,
	before [;
	 Lim:
	    <<PutOn kontaktlim self>>;
         PushDir, Push:
            if (self.glued) "Musen er limet fast til bordet med kontaktlim.";
            "Du skubber musen rundt på bordet. Den løber meget trægt.";
	 Take:
	    if (self.glued) "Musen er limet fast til bordet med
	    kontaktlim, så den kan du ikke tage."; "Musen er ikke trådløs, så
                du kan ikke sådan rende med den.";
	],
 has    uter concealed static;


Object 	-> tastatur "tastatur"
 with   name 'tastatur' 'tastaturet' 'keyboard' 'keyboardet' 'taster' 'tasterne',
        short_name_def "tastaturet",
        description [;
	    print "Tastaturet ser godt brugt ud";
	    if (self.glued) print ". Nogle af tasterne sidder fast"; else
                print", og tasterne kunne godt trænge til en god omgang rengøring";
	    ".";
	],
	glued false,
        glueannounced false,
	before [;
	 Lim:
	    <<PutOn kontaktlim self>>;
	 Take:
	    "Tastaturet er sidder fast med en ledning, så du må lade det stå.";
	],
 has    uter concealed static;

Object -> computer "computer"
 with   name 'computer' 'computeren' 'PC' 'PC^en' 'PCen' 'datamat' 'datamaten',
        short_name_def "computeren",
        opened_once false,
	formatted false,
        glued false,
        description "",
        before [;
         Hack:
            "Som drillenisse har du de helt rigtige egenskaber til at blive en
            god hackernisse, nemlig opfindsomhed, elegance og humor.";
         Crack:
            "At cracke computeren og dens programmer vil nok mest drille anti-piratnisse-gruppen.";
	 Close:
	    <<SwitchOff self>>;
         SwitchOn:
            if (self.formatted) {
                "Computeren starter op med sort skærm og teksten: ~Installér
                venligst et styresystem på computeren.~ Efter nogle sekunder slukker
                computeren igen.";
            };
         Formater:
            <<Formater harddisk>>;
         Take, Remove, Push, Pushdir:
            "Nok er du en stærk nisse, men dette er ikke en bærbar computer.";
         Examine:
            print "En stor beige-farvet bambus-PC men dog med en
		nyere og ret stor fladskærm. Til højre for
		tastaturet ligger en computermus. ";
            if (self has on) "Computeren er tændt."; else "Computeren er slukket.";
         Burn: <<SwitchOn self>>;
         Open: <<SwitchOn self>>;
	 Formater, Slet: <<Formater harddisk>>;
         Lim:
	    <<PutOn kontaktlim self>>;
        ],
        after [;
         SwitchOn:
            print "Du trykker på tændknappen, og efter nogle minutters rumlen
                fra computerens kabinet lyser skærmen op. Der står
                ~Installerer 1 af ", random(24)+8, " opdateringer~. Du
                venter ", random(10)+7, " lange minutter, før computeren er
                klar til, at du kan logge på.^^Skriv venligst kodeordet: ";
            skrevet_password->0 = MAXPASSWORDLENGTH-3;
            read skrevet_password 0;
            if (CompareReadWithTextArray(rigtigt_password, skrevet_password)) {
                print "^~FEJL 40: Forkert kodeord.~ Computeren slukker igen.^";
                give self ~on;
                give self ~open;
            } else {
                give self on;
                give self open;
                print "^~Korrekt kodeord,~ svarer computeren. ";
                if (kodeord.known == false) {
		    print "Åhr hvad?! Du gættede menneskenes
			latterligt nemme kodeord, din hackernisse! ";
                    kodeord.known = true;
                }
                print "På computerens harddisk kan du nu se en masse filer.^";
            }
            rtrue;
         SwitchOff:
            give self ~open;
            "Du hører et kort og kraftigt BIP fra computeren, og
	    der installeres nogle flere opdateringer, hvorefter
	    skærmen bliver sort. Der bliver efterhånden helt stille
	    i rummet da PC'ens ventilatorer ånder lettet op.";
        ],
  has  uter scenery switchable container;

Object -> -> kodeord "kodeord"
 with   name 'password' 'passwordet' 'kodeord' 'kodeordet' 'kode' 'koden',
        description [;
            if (self.changed == false) print "Computerens alt for nemme kodeord
                er: ";
            else print "Computerens kodeord er: ";
            PrintTextArray(rigtigt_password);
            print "^";
        ],
        short_name_def "kodeordet",
        known false,
        changed false,
        before [;
         Set, SetTo, Skift, Aendre, Aendr:
            print "Angiv et nyt kodeord: ";
            skrevet_password->0 = MAXPASSWORDLENGTH-3;
            read skrevet_password 0;
            CopyReadTextArrayToTextArray(skrevet_password, rigtigt_password);
            print "Du ændrer kodeordet";
            if (CompareReadWithTextArray(oprindeligt_password, skrevet_password)) {
                print ". Et nyt kodeord skal nok give menneskene noget at bryde hovederne med.^";
		if (self.changed == false) { GetPoint(6, 1); self.changed = true;}
 		rtrue;
            } else {
                print ", men til det nemme kodeord, som menneskene er vant til.^";
		if (self.changed == true) { TakePoint(6, 1); self.changed = false; rtrue;}
		rtrue;
            }
        ],
 has    neuter scenery;

Object -> -> harddisk "harddisk"
 with   name 'harddisk' 'harddisken' 'disk' 'disken',
        description "Harddisken er af ældre dato. Man kan slet ikke
	    købe diske så små længere.",
        short_name_def "harddisken",
        before [;
         Formater, Slet:
	    if (computer.formatted == false) {
            	give computer ~on ~open;
	    	computer.formatted = true;
            	print "Du skriver nogle kommandoer på tastaturet, og computerens
            	    harddisk begynder at knurre, imens byte for byte af data
            	    forsvinder. Efter nogle få minutter lukker computeren ned med en
            	    sort skærm. Vi må håbe menneskene har en god backup.^";
	    	GetPoint(6, 2); rtrue;
	    }
        ],
 has    uter scenery;

Object -> -> filer "filer"
 with   name 'fil' 'filer' 'filerne' 'computerfiler' 'computerfilerne',
        some_deleted false,
        some_renamed false,
        some_moved false,
        rename_index 0,
        short_name_def "filerne",
        before [;
         Hack:
            "Som drillenisse har du samtidig de helt rigtige egenskaber til at
            være en god hackernisse. Egenskaber som opfindsomhed, elegance og
            humor.";
         Crack:
            "At cracke computerens programmer vil nok mest drille anti-piratnisse-gruppen.";
         Slet:
            if (self.some_deleted) "Jeg tror du har slettet nok filer til at have drillet menneskene tilpas meget.";
            GlueAnnounce();
            print "Du sletter en masse tilfældige filer. At slette menneskers data er en af de mere ondskabsfulde drillerier en nisse kan gøre.^";
            self.some_deleted = true;
	    GetPoint(6, 1); rtrue;
         Omdoeb:
            GlueAnnounce();
            print "Du omdøber nogle tilfældige filer til mere interessante navne, såsom ";
	    switch (self.rename_index) {
		0: self.rename_index = 1; print "risengroed-opskrift.doc, paa-loftet-sidder-nissen.mp3 og IngenGroed-IngenFred.exe.^";
	    	1: self.rename_index = 2; print "HuskNissen.pdf, mer-groed.mp3 og all-i-want-for-xmas-is-risengroed.mp3.^";
	        2: self.rename_index = 0; print "JULESJOV.exe, Menneske-kend-din-nisse.epub og FangRotterne.exe.^";
	    }
	    if (self.some_renamed == false) { GetPoint(6, 1); self.some_renamed = true; }
	    rtrue;
         Push:
            GlueAnnounce();
            print "Du flytter filer rundt på må og få på computeren. Held og lykke at finde dem igen, mennesker.^";
	    if (self.some_moved == false) { GetPoint(6, 1); self.some_moved = true; }
	    rtrue;
         Kopier:
            GlueAnnounce();
            "Du kopierer en masse filer, som en ægte piratnisse, men det driller nok mest anti-piratnisse-gruppen.";
         Del:
            GlueAnnounce();
            "Som en piratnissen deler du masse af menneskenes medie-filer på nettet, men det driller nok mest anti-piratnisse-gruppen.";
         Examine:
            "Der er virkelig mange filer på denne computer. Du kan blandt andet liste, åbne, omdøbe, flytte og slette dem.";
         Krypter:
            "Du er vist en af de nisser, som afholder nissecryptoparties, hvor
            nisser lærer alt om at holde hemmeligheder.";
         List:
            GlueAnnounce();
            print "Du lister filerne og ser at der er filer med navne som
                f.eks.: ";
            Filnavne();
            ". Du kan også se et teksteventyr med navnet ADVENTURE.";
         Open, Consult:
             "Du overvejer at åbne filerne, men du er en nisse, som
            respekterer retten til privatliv og andre menneske- og
            nisserettigheder, så du undlader at åbne og kigge i filerne.";
        ],
 has    uter scenery openable;

Object -> -> teksteventyr "teksteventyr"
 with   name 'teksteventyr' 'teksteventyret' 'eventyr' 'eventyret' 'adventure' 'advent' 'adv' 'adven' 'zork' 'computerspil' 'computerspillet' 'spil' 'spillet',
        short_name_def "teksteventyret",
        description "Teksteventyret er et af de gode gamle uden nogen form for
            grafik -- udover de billeder, som du skaber med din fantasi,
            hvilke jo tit er de bedste.",
        examined false,
        play_state 0,
	played false,
        before [;
         Examine:
            self.examined = true;
            PrintOrRun(self, description); rtrue;
         Spil, Open, SwitchOn:
	    if (self.played == false) { GetBonus(40); print "^"; self.played = true; }
            if (computer has on) {
                self.play_state = 1;
                if (self.examined == false) {
                    print "Teksteventyret er et af de gode gamle uden nogen form for
                        grafik -- udover de billeder, som du skaber med din fantasi,
                        hvilke jo tit er de bedste. ";
                }
                print "Du starter spillet og på skærmen står:^^";
                Bold("ADVENTURE^");
                print "By Will Crowther (1976) and Don Woods (1977)^^";
                Bold("At End Of Road^");
                "You are standing at the end of a road before a small brick
                building. Around you is a forest. A small stream flows out of
                the building and down a gully.";
            }
        ],
 has    neuter scenery openable;

Object  monitor "fladskærm" room_kontoret
 with   name 'fladskærm' 'fladskærmen' 'monitor' 'monitoren' 'skærm' 'skærmen',
        short_name_def "fladskærmen",
        before [;
         Examine: "En simpel skærm i 4:3-billedeformat.";
         default: "Der er ikke noget exceptionelt interessant ved denne skærm.";
        ],
 has    uter scenery;

! ============================================================================ !
!Include "room_loftet.inf";
Room  room_loftet "På loftet"
    with  description [;
	      if (TotalScore() < OK_SCORE) {
		  if (TotalScore() < 1) {
		      "Loftsrummet er koldt og næsten helt mørkt. Morgenlyset
                      kan kun lige akkurat trænge igennem tagvinduerne, som er
                      dækket af nattens snefald, og vinden blæser af og til et
                      pust sne op under tagskægget, som finder hvile i små
                      hvide bunker rundt omkring på gulvet. En skål med en
                      grødske i står ved siden af lemmen, som leder ned til
                      resten af huset.";
		  } else {
		      "Loftsrummet er koldt og næsten helt mørkt.
		      Risengrødsskålen, som står her oppe, er
		      stadig lige tom.";
		  }
	      } else {
                  risengroed.seen_by_pc = true;
		  print "På midten af gulvet står en skål fyldt med risengrød,
                      perfekt anrettet med kanelsukker og smørklat i. ";
                  if (groedske in skaal) print "En grødske af træ er sat i grøden. ";
		  if (rotter in self && rotter.friendly == false) {
		      "Fem frække rotter danser syngende rundt om
                      risengrødskålen.";
		  } else {
		      print "De fem søde rotter ";
		      if (kat in self) print "og katten ";
		      "sidder pænt rundt om skålen og kigger med
		      store øjne på den lækre risengrød.";
		  }
	      }
	  ],
    external_name 'loft' 'loftet',
    d_to lem,
    cant_go [;
        print "Den vej kan du ikke gå. ";
        <<Examine lem>>;
    ],
    clearcoastcheck false,
    before [;
     Go:
        if (noun == d_obj && self.clearcoastcheck == false) {
            self.clearcoastcheck = true;
            print "Inden du bevæger dig ned i huset, kigger du gennem lemmen
                for at sikre dig, at der er fri bane. Det er der.^";
            rfalse;
        }
     Smell:
	if (noun == nothing) {
	    if (risengroed in skaal) "Her lugter dejligt af
	    risengrød med kanelsukker."; else "Her lugter mest af støvet loftsrum.";
	}
     Listen:
        if (noun == nothing) {
            if (TotalScore() < OK_SCORE) {
                if (boern in room_boernevaerelset) "Du kan høre vindens susen op under taget og nogle fjerne børnestemmer i huset nedenunder.";
                else "Lyden af ingen risengrød.";
            } else {
                if (rotter in self && rotter.friendly == false) {
                    "De fem frække rotter synger en velkendt julemelodi i et højt toneleje.";
                } else {
                    "Lyden af risengrød ... i surround sound.";
                }
            }
        }
    ],
    after [;
     Go:
        if (noun == u_obj && rotter.seen_by_pc == false && risengroed.announced == true) {
            rotter.seen_by_pc = true;
            print "Inden du når at gå op ad stigen til loftet,
                skubber fem frække rotter dig til side og løber
                op på loftet. Du følger forsigtigt efter.^";
        }
    ],
    has    light;

Object  skaal "skål" room_loftet
 with   name 'grødskål' 'grødskålen' 'skål' 'skålen' 'fad' 'fadet' 'tallerken'
 'tallerkenen' 'grøden' 'grød',
        short_name_def "skålen",
        before [;
         Examine:
            if (risengroed notin self) {
                "Skålen er tom, meget tom, og tanken om den lune
		nissegrød, som engang var i den, gør dig om muligt
		endnu mere sulten, end du var i forvejen.";
            } else {
                print "Skålen er fyldt med dejlig nylavet risengrød, perfekt
                anrettet med kanelsukker og smørklat i. ";
                if (groedske in self) print "En grødske af træ er
                sat i grøden. ";
                if (rotter.friendly) print "Hvad venter du på?";
                print "^";
                rtrue;
            }
         Smell:
            if (risengroed notin self)
            	print "Du stikker næsen ned i skålen, og med din livlige
		fantasi kan du genkalde dig duften af den risengrød,
		som engang var i skålen."; else print "Den nylavede grød
		udsender en fantastisk duft... bare fantastisk, med
		dens kanelsukker og smørklat i. ";
            if (rotter.friendly) print "Hvad venter du på?";
            print "^";
            rtrue;
         Listen:
	    if (risengroed notin self) "Lyden af en tom grødskål
 	    fylder rummet."; "Risengrøden synes næsten at synge en
		julesalme, så sød.";
         Take, Eat, Taste:
            if (risengroed notin self) "Du løfter den tomme skål,
	    men det mætter ikke det mindste. Du stiller skålen fra dig igen.";
            else <<Taste risengroed>>;
        ],
 has    uter static container concealed transparent open;

Object  groedske "grødske" skaal
 with   name 'grødske' 'grødskeen' 'ske' 'skeen',
        short_name_def "grødskeen",
        after [;
         Take: give self ~concealed;
        ],
        before [;
         Examine:
            if (risengroed.announced == false) "En grødske uden risengrød er ikke meget værd, så du lader den ligge.";
            if (risengroed.announced && rotter.friendly) "Grødskeen skal nok snart komme til sin ret.";
            if (risengroed.announced && rotter.friendly == false) "Grødskeen er dit yndlingsbestik.";
         Take:
            if (risengroed.announced) {
                if (CanCarryMore()) {
                    if (IsHeld(self) == false) {
                        move self to player;
                        give self ~concealed;
                        if (rotter.friendly) {
                            "Du tager grødskeen, som nok snart skal komme til sin ret.";
                        } else {
                            "Du skynder dig at smutte ind mellem rotterne og snuppe grødskeen.";
                        }
                    }
                }
            } else {
                <Examine self>;
                rtrue;
            }

         PutOn, Insert:
            if (risengroed.announced) {
                if (second == risengroed || second == skaal) {
                    if (CanCarryMore()) {
                        if (IsHeld(self) == false) {
                            move self to player;
                            give self ~concealed;
                            <<Taste risengroed>>;
                        }
                    }
                }
            }
        ],
 has    uter static concealed;

Object  sne "sne" room_loftet
 with   name 'sne' 'sneen' 'snebunke' 'bunke' 'snefald' 'snefaldet' 'snefnug' 'snefnuggene',
        short_name_def "sneen",
        describe [; rtrue;], ! To avoid printing and Inform guessing game
        ! http://www.firthworks.com/roger/informfaq/oo.html#3 by having two static
        description [;
            print "Sneen er nyfalden. ";
            if (TotalScore() < OK_SCORE) "Du burde være glad for, at det bliver en hvid
            jul, men den manglende risengrød har fjernet det meste af din
            juleglæde.";
            else; "Godt du ikke behøver at nøjes med at drømme om en hvid jul i
            år.";
        ],
        before [;
         Taste:
            "Du samler lidt sne op og smager på den, men du bliver ikke rigtig
            mæt af det.";
         Take:
            "Med begge hænder samler du lidt sne op. Den smelter langsomt,
            imens du drømmer om, at det var varm risengrød.";
         Attack:
            "Du sparker og slår lidt til snebunkerne, men det bliver en noget
            ensom sneboldskamp.";
        ],
 has    pluralname static concealed;

Object  vind "vind" room_loftet
 with   name 'vind' 'vinden',
        short_name_def "vinden",
        before [;
         Examine:
             "En kold vind fra nordøst. Vinden hvisker noget i dit øre, men du
            fanger ikke helt meningen med det.";
         default:
            <<Examine self>>;
        ],
 has    pluralname static concealed;

Object  tagskaeg "tagskæg" room_loftet
 with   name 'tagskæg' 'tagskægget',
        short_name_def "tagskægget",
        description [;
            Bold("tagskæg"); print " substantiv, intetkøn.^Bøjning: -get, -,
                -gene.^Betydning: den nederste kant af et (skråt) tag, som
                rager ud over bygningens mur. Se også: tagudhæng.^Eksempel:
                ~Han fandt nøglen under tagskægget og låste sig ind.~ (Bjarne
                Reuter: ~Tre til Bermudos~, Gyldendal 1990.) Kilde:
                http://ordnet.dk/ddo/^";
        ],
 has    neuter static concealed;

Object  tagudhaeng "tagudhæng" room_loftet
 with   name 'tagudhæng' 'tagudhænget' 'udhæng' 'udhænget',
        short_name_def "tagudhænget",
        description "Dette er et teksteventyr, ikke en ordbog.",
 has    neuter static concealed;

Object  huset "huset" room_loftet
 with   name 'hus' 'huset' 'taget' 'tag',
        short_name_def "huset",
        description "Huset er et ældre to-etagers hus med kælder og loft.",
 has    neuter static concealed;

Object  loft "loftet" room_loftet
 with   name 'loft' 'loftet' 'loftsrum' 'loftsrummet',
        short_name_def "loftet",
        description "Hjem, kære hjem.",
 has    neuter static concealed;

Object  rotter "rotter" room_loftet
 with   name 'rotter' 'små' 'rotte' 'rotterne',
        short_name_def "rotterne",
        description [;
            if (rotter.friendly == true && player in room_loftet && kat in room_loftet) "Rotterne
            sidder pænt og kigger skiftevis på dig, katten og grøden.";
            if (rotter.friendly == true && player in room_loftet && kat notin room_loftet) "Rotterne
            sidder pænt og kigger skiftevis på dig og grøden.";
            "Rotterne er i forskellige nuancer af grå og brun og på størrelse
            med dig selv.";
            ],
        friendly false,
        seen_by_pc false,
        daemon [;
            if (kat in room_loftet) {
                if (self.friendly == false && player in room_loftet) {
                    print "Da rotterne ser katten, som er fulgt
			efter dig op på loftet, bliver de bange og
			springer hurtigt til side og piber i kor:
			~Undskyld nisse og kat, vi vil allerhelst
			bare være venner og spise den gode risengrød
			sammen.~^";
                    self.friendly = true;
                }
            }
        ],
        before [;
         Attack, Tru:
            if (self.friendly == true) "I stedet for klør du et par af rotterne bag ørerne.";
            if (groedske in player) print "Du bliver hidsig og truer rotterne med
            grødskeen: ~Jeg henter katten, hvis I ikke holder op!~"; else print
            "Du bliver hidsig og truer rotterne: ~Jeg henter katten, hvis I ikke holder op!~";
            " Rotterne svinser og svanser dog bare videre rundt om grøden.";
         Take, Touch:
            if (self.friendly == true) "Du klør et par af rotterne bag ørerne.";
            "Rotterne er ikke særlig venligt udseende lige nu, så det ville
            jeg ikke gøre. Rotterne svinser og svanser bare videre rundt om
            grøden.";
         Listen:
            "Rotterne synger en velkendt julemelodi i et højt
	    toneleje.";
        ],

 has    pluralname scenery static concealed;

Object  risengroed "risengrød" room_loftet
 with   name 'risengrød' 'risengrøden' 'grød' 'grøden' 'julegrød' 'julegrøden',
        short_name_def "risengrøden",
        description
            "Risengrøden er nylavet, og der er rigeligt af den.
	    Kanel og smørklat på toppen fuldender den klassiske
	    nissemenu.",
        announced false,
        waited true,
        seen_by_pc false,
        daemon [;
            if (TotalScore() >= OK_SCORE) {
                if (self.announced == false && self.waited == true) {
                    self.announced = true;
		    remove fake_risengroed;
                    move self to skaal;
		    move rotter to room_loftet;
                    if (foraeldre notin room_sovevaerelset && boern notin room_boernevaerelset)
                        print "^Husets forældre og børn siger nu i kor: ";
                    if (foraeldre notin room_sovevaerelset && boern in room_boernevaerelset)
                        print "^Husets forældre siger nu i kor: ";
                    if (foraeldre in room_sovevaerelset && boern notin room_boernevaerelset)
                        print "^Husets børn siger nu i kor: ";
                    if (foraeldre in room_sovevaerelset && boern in room_boernevaerelset)
                        print "^Hov, hvad var det? Du hører på afstand husets
                            beboere sige højlydt i munden på hinanden: ";
                    "~Nu må det snart slutte med det nissedrilleri. Vi må se
                    at få lavet den risengrød i en fart og få sat den op på
                    loftet til nissen, den plageånd!~ ";
                }
            } else {
                self.waited = true;
            }
        ],
        before [;
         Examine:
            if (rotter in room_loftet && rotter.friendly == false) <<Examine skaal>>;
         Taste, Eat:
            if (rotter in room_loftet && rotter.friendly == false) {
                print "De frem rotter løber rundt om grøden, så du kan ikke komme
                tæt nok på til at smage.";
                if (groedske in skaal) print " Grødskeen er dog inden for din
 rækkevidde.";
                print "^";
                rtrue;
            } else {
                deadflag = 2;
                GetFinalPoint(0, 2);
                "Du tager en stor skefuld risengrød med grødskeen og smager på
                den. Perfekt lavet risengrød med kanelsukker og smørklat i.
                Den bedste grød du har smagt i lang tid. Nedenunder i huset
                kan du høre, at menneskene er ved at komme sig oven på dine
                drillerier, og at julefreden snart så småt kan sænke sig over
                huset. God jul!";
            }
         Take:
            <<Take skaal>>;
        ],
 has    uter scenery static concealed edible;

Object  fake_risengroed "risengrød" room_loftet
 with   name 'risengrød' 'risengrøden' 'grød' 'grøden',
        short_name_def "risengrøden",
	before [;
	 default: "Du kigger i grødskålen. Den er tom og har været
	    det i alt for lang tid.";
	],
 has    pluralname scenery static concealed edible;

Object  lem "lem i gulvet" room_loftet
 with   name 'lem' 'lemmen' 'dør' 'døren',
        short_name_def "lemmen",
        door_to room_foerstesalen,
        door_dir d_to,
        before [;
         Search: "Du kigger ned gennem lemmen for at sikre dig, at der er fri
            bane ned i huset. Det er fri bane.";
         Examine:
            print "Lemmen i gulvet er den eneste vej ned til resten af huset.
                Lemmen står åben, fordi menneskene har glemt at lukke den, da
                de var heroppe med grød sidst ";
            if (risengroed.announced) "(hvilket i øvrigt ikke er mange minutter siden!).";
            "(hvilket i øvrigt er al for lang tid siden).";
         Open:
            if (self has open) "Den er allerede åben.";
         Close:
            if (self has open) "Det kommer der ikke grød i skålen af at gøre.";
        ],
 has    uter scenery static door open;

Object  gulvet "gulvet" room_loftet
 with   name 'gulv' 'gulvet',
        description "Brede, brune træplanker, som ikke har været fejet i årtier.",
        short_name_def "gulvet",
 has    neuter scenery;

Object  vinduer "tagvindue" room_loftet
 with   name 'tagvindue' 'tagvinduet' 'tagvinduer' 'tagvinduerne' 'vindue'
 	    'vinduet' 'vinduer' 'vinduerne',
        short_name_def "tagvinduerne",
        description [;
            if (risengroed.announced) "Sneen er blæst af tagvinduerne, og du
            kan skimte nattens første stjerne på den mørkeblå julehimmel.";
            "Tagvinduerne er dækket af nyfalden sne, så du kan ikke se ud ad
            dem.";
        ],
        before [;
         Go, Enter, Climb: <<Open self>>;
         Open:
            "Vinduerne er enten frosset fast eller for tunge at skubbe op.";
        ],
 has    pluralname scenery static concealed;

! ============================================================================ !
!Include "room_labyrint.inf";
LabyrintRoom  room_labyrint_0 "Labyrintens indgang"
    with   description "Dette ligner ikke en helt almindelig kloak.
    På væggen hænger et skilt. Du kan gå syd og øst.",
    external_name 'afløb' 'afløbet' 'kloak' 'kloakken',
    u_to room_kaelderen,
    out_to room_kaelderen,
    labyrintroomnum 0,
    after [;
     Go, Climb:
	if (noun == d_obj) {
	    if (rotter.seen_by_pc == false && staalrist.entered == false) {
  		staalrist.entered = true;
 		print "Inden du når at bevæge dig ned i afløbet, kommer fem
                    frække rotter spurtende op fra afløbet. Rotterne, som til
                    daglig bor i kloakken under huset, skubber dig til side og
                    forsvinder op i huset. De er sikkert også på jagt efter
                    noget spiseligt.^^Efter at du har sundet dig lidt, bevæger
                    du dig forsigtigt ned i afløbet.^";
	    }
	}
    ],
    before [;
     Go, Climb:
  	if (noun == u_obj || noun == n_obj || noun == out_obj) {
            if (NumCarried() > 1) {
                "Afløbet er så snævert, at du må smide dine ejendele først.";
            } else {
                print "Du klatrer op i kælderen igen.^";
            }
        }
    ],
    s_to room_labyrint_4,
    e_to room_labyrint_1,
    has    light;

LabyrintRoom  room_labyrint_1 "Labyrinten"
    with   description "Du kan gå vest.",
    w_to room_labyrint_0,
    labyrintroomnum 1,
    has    light;

LabyrintRoom  room_labyrint_2 "Labyrinten"
    with description [;
             print "Du har nået en blindgyde. ";
             if (skovl has concealed && spade has concealed) print "I hjørnet
                 står en skovl og en spade. ";
             "Du kan gå øst.";
         ],
    e_to room_labyrint_3,
    labyrintroomnum 2,
    has    light;

LabyrintRoom  room_labyrint_3 "Labyrinten"
    with   description "Jorden skælver svagt, som du bevæger dig
    igennem labyrinten. Du kan gå vest og syd.",
    s_to room_labyrint_7,
    w_to room_labyrint_2,
    labyrintroomnum 3,
    has    light;

LabyrintRoom  room_labyrint_4 "Labyrinten"
    with   description "Væggene består af ler og sand med indlejrede sten og blokke. Vand løber langsomt ned ad væggen flere steder og samler sig på gulvet i små vandpytter. Du kan gå nord, syd og øst.",
    n_to room_labyrint_0,
    s_to room_labyrint_8,
    e_to room_labyrint_5,
    labyrintroomnum 4,
    has    light;

LabyrintRoom  room_labyrint_5 "Labyrinten"
    with   description "Alle labyrintens snørklede gange ligner mere eller mindre hinanden. Du kan gå syd, øst og vest.",
    s_to room_labyrint_9,
    e_to room_labyrint_6,
    w_to room_labyrint_4,
    labyrintroomnum 5,
    has    light;

LabyrintRoom  room_labyrint_6 "Labyrinten"
    with description [; if (stearinlys in room_labyrint_6 && stearinlys has concealed) print "Et enligt stearinlys står for enden af labyrintgangen og lyser smukt. "; "Du kan gå vest."; ],
    w_to room_labyrint_5,
    labyrintroomnum 6,
    has    light;

LabyrintRoom  room_labyrint_7 "Labyrinten"
    with   description "Her har du vist ikke været før -- eller har du? Du kan gå nord og syd.",
    n_to room_labyrint_3,
    s_to room_labyrint_11,
    labyrintroomnum 7,
    has    light;

LabyrintRoom  room_labyrint_8 "Labyrinten"
    with   description "Du kan gå nord.",
    n_to room_labyrint_4,
    labyrintroomnum 8,
    has    light;

LabyrintRoom  room_labyrint_9 "Labyrinten, bålplads"
    with description [;
             if (baal.burned == false) "Dette er en
             underjordisk bålplads, hvor et kæmpe bjerg af kviste og grene
             blokerer din videre færd mod øst.";
             "Dette er en underjordisk bålplads. Du kan se resterne af et
             udbrændt bål. Du kan gå nord eller øst."; ],
    before [;
     Go, Climb:
  	if (noun == e_obj && baal.burned == false) "Du forsøger at klatre
        over bjerget af grene og kviste, men det mislykkes.";
],
    n_to room_labyrint_5,
    e_to room_labyrint_10,
    labyrintroomnum 9,
    has    light;

LabyrintRoom  room_labyrint_10 "Labyrinten"
    with   description "Du føler dig lidt ensom her nede i labyrinten. Det er ikke for sent at vende om og komme op i sikkerhed. Du kan gå øst og vest.",
    e_to room_labyrint_11,
    w_to room_labyrint_9,
    labyrintroomnum 10,
    has    light;

LabyrintRoom  room_labyrint_11 "Labyrinten, æbleskivetræet"
    with   description "Et mægtigt æbleskivetræ står foran dig. Grenene buer
    tungt nedad tynget af hundredevis af rygende varme æbleskiver. Du kan gå
    nord, syd og vest.",
    before [;
     Go, Climb:
  	if (noun == s_obj && stenskred in room_labyrint_11) {
            if (stenskred.appeared == false) {
                stenskred.appeared = true;
                "Inden du når at gå mod syd, begynder labyrint-væggen at
                skælve og en sand lavine af lavasten styrter ned foran dig og
                blokerer din vejen mod syd.";
                }
            if (stenskred.digged == false) "Du kan ikke komme forbi de rødglødende lavasten mod syd.";
        }
     Dig: if (NotHeld(skovl) && NotHeld(spade)) "Du har ikke lyst til at grave med de bare hænder.";
    ],
    n_to room_labyrint_7,
    s_to room_labyrint_15,
    w_to room_labyrint_10,
    labyrintroomnum 11,
    has    light;

LabyrintRoom  room_labyrint_12 "Labyrintens udgang"
    with   description "Du kan gå øst.",
    before [;
     Go, Climb:
  	if (noun == u_obj || noun == s_obj || noun == out_obj) {
            if (NumCarried() > 1) {
                "Udgangen er så snæver, at du må smide dine ejendele først.";
            } else {
                print "Du klatrer op i kælderen igen.^";
            }
        }
    ],
    u_to room_kaelderen,
    s_to room_kaelderen,
    out_to room_kaelderen,
    e_to room_labyrint_13,
    labyrintroomnum 12,
    has    light;

LabyrintRoom  room_labyrint_13 "Vest for afgrunden"
    with   description "Den uendelige afgrund strækker sig ud foran dig mod
    øst. Fra denne side kan du ikke se regnbuen. Du kan gå vest.",
    before [;
     Go, Climb:
  	if (noun == e_obj) "Afgrunden mod øst er umulig at krydse på nissefod.";
    ],
    w_to room_labyrint_12,
    labyrintroomnum 13,
    has    light;

LabyrintRoom  room_labyrint_14 "Øst for afgrunden"
    with   description "En uendelig afgrund strækker sig ud foran dig mod vest. En lille regnbue strækker sig fra hvor du står og over til den anden siden af afgrunden. Du kan gå øst.",
    before [;
     Go, Climb:
  	if (noun == w_obj) "Du sætter din nissefod på regnbuen, men kan mærke,
        at den ikke kan bære, din fod går lige igennem. Den dybe afgrund foran
        dig mod vest er umulig for dig at krydse til fods.";
    ],
    after [;
     Go:
    	if (noun == w_obj) {
      	    move enhjoerning to room_labyrint_14;
      	    print "Enhjørningen følger efter dig mod vest.^";
      	    <Look>;
    	}
   	rtrue;
    ],
    e_to room_labyrint_15,
    labyrintroomnum 14,
    has    light;

LabyrintRoom  room_labyrint_15 "Labyrinten, dragehulen"
    with   description [;
               if (drage in room_labyrint_15)
                   "En enorm hule mod vest åbner sig for dig. Du kan gå nord
                   og vest, hvis det ikke var for den ildspyende drage, som
                   blokerer din videre færd.";
               else "Du kan gå nord og vest.";
    ],
    before [;
     Go, Climb:
  	if (noun == w_obj && drage in room_labyrint_15) "Den ildspyende drage
        gør det umuligt at bevæge sig mod vest. Når du prøver at nærme dig,
        spyr den bare endnu mere ild.";
    ],
    n_to room_labyrint_11,
    w_to room_labyrint_14,
    labyrintroomnum 15,
    has    light;

Object 	skilt0 "skilt" room_labyrint_0
 with 	name 'skilt' 'skiltet',
  	short_name_def "skiltet",
        description [;
	    print "På skiltet står med gotisk skrift: ~Velkommen til den
                underjordiske labyrint. Hvis du har mod på det, kan du her gå
                på opdagelse og få oplevelser, som har været meget få forundt.
                Ellers kan du roligt begive dig op i sikkerhed i huset ovenpå.
                Magi har en vis virkning hernede, men derudover får du brug
                for en anelse sund fornuft og vovemod. (Det magiske ord er
                XYZZY)~^";],
 has    neuter static concealed;

Object 	pebernoedder "pebernødder" room_labyrint_1
 with 	name 'skål' 'skålen' 'pebernød' 'pebernødder' 'nødder' 'nødderne',
        description "En skål pebernødder står på et lille bord.",
  	short_name_def "pebernødderne",
  	before [;
  	 Eat, Taste:
	    remove pebernoedder;
	    player.magic = player.magic + 1;
	    print "Du spiser én pebernød, du spiser to. Du føler en
		magisk kraft vokse inden i dig. Du spiser en
		pebernød til og bliver ved og ved, til der ikke er
		flere.^^[Du får 1 magi-point og har nu ";
	    print player.magic;
	    ".]";
	],
 has 	pluralname edible;

Object 	skovl "skovl" room_labyrint_2
 with 	name 'skovl' 'skovlen',
        description "En ganske almindelig skovl.",
  	short_name_def "skovlen",
	before [;
	 Take:
            if (spade in location) {
                if (CanCarryMore()) {
                    if (IsHeld(spade) == false) {
                        give skovl ~concealed;
                        give spade ~concealed;
	                move spade to player;
     	                "Du kalder skovlen for en spade og tager dén.";
                    }
                }
            } else {
                if (CanCarryMore()) {
                    if (IsHeld(skovl) == false) {
                        give skovl ~concealed;
                        give spade ~concealed;
                        move skovl to player;
                        "Du kalder skovlen for en skovl og tager dén.";
                    }
                }
            }
	],
 has 	uter concealed;

Object 	spade "spade" room_labyrint_2
 with 	name 'spade' 'spaden',
        description "En ganske almindelig spade.",
  	short_name_def "spaden",
	before [;
	 Take:
            if (skovl in location) {
                if (CanCarryMore()) {
                    if (IsHeld(skovl) == false) {
                        give skovl ~concealed;
                        give spade ~concealed;
	                move skovl to player;
     	                "Du kalder spaden for en skovl og tager dén.";
                    }
                }
            } else {
                if (CanCarryMore()) {
                    if (IsHeld(spade) == false) {
                        give skovl ~concealed;
                        give spade ~concealed;
	                move spade to player;
                        "Du kalder spaden for en spade og tager dén.";
                    }
                }
            }
	],
 has 	uter concealed;

Object 	drage "ildspyende drage" room_labyrint_15
 with 	name 'ildspyende' 'drage' 'dragen',
  	short_name_def "dragen",
        description "Ud ad dragens mund står en 3 meter lang flamme,
	    som blænder dine øjne og varmer dit ansigt op.",
  	before [;
    	 Xyzzy:
       	    <<Xyzzy>>;
    	 Blow: "En ildspyende drage er desværre ikke ligeså nem at puste ud
            som et julelys.";
         Attack: "Du kan ikke komme tæt nok på til at angribe dragen.";
         Kiss, Squeeze: "Kærlighed overvinder alt -- på nær ildspyende drager.";
    	 default: "Du forsøger at nærme dig dragen, men varmen fra ilden gør, at du må gå tilbage igen.";
  	],
 has 	uter concealed static;

Object 	regnbue "regnbue" room_labyrint_14
 with 	name 'regnbue' 'regnbuen',
        description "Regnbuen udsender lys i alle universets farver. Du har
            aldrig set noget ligende.",
  	short_name_def "regnbuen",
 has 	uter scenery;

Object 	afgrund "afgrund" room_labyrint_14
 with 	name 'afgrund' 'afgrunden',
        description "Du går forsigtigt frem til kanten af afgrunden, men må
            dække dine øjne og gå tilbage igen, da afgrunden nærmest suger dig
            til sig.",
        before [;
         Jump, Enter:
            "Du nærmer dig afgrunden, men enhjørningen napper dig i din
            nisseblue og trækker dig væk igen.";
        ],
  	short_name_def "afgrunden",
 has 	uter scenery;

Object 	enhjoerning "enhjørning" room_labyrint_15
 with 	name 'enhjørning' 'enhjørningen' 'væsen' 'væsenet',
  	short_name_def "enhjørningen",
  	before [;
    	 Examine:
       	    "Den hvide enhjørning er lille, kun lidt større end dig, og står og
            pruster og skraber utålmodigt i jorden med sine hove.";
    	 Xyzzy:
      	    if (player in room_labyrint_14) <<Xyzzy>>; "Enhjørningen
	    vrinsker og pruster og nikker med hovedet.";
         Klap:
            "Enhjørningen ser ud til godt at kunne lidt det, men løber så væk
            igen og betragter dig på behørig afstand.";
         Rid, Climb:
            "Du nærmer dig enhjørningen for at prøvet at klatre op på den, men
            den løber væk fra dig og vrinsker fra en afstand.";
         Kiss, Squeeze:
            "Det er en enhjørning, ikke en frø. Enhjørningen smiler til dig.";
    	 default:
      	    "Enhjørningen løber rundt om dig og ser dig an.";
  	],
 has 	uter static;

Object 	hest "hest" room_labyrint_15
 with 	name 'hest' 'hesten',
  	short_name_def "hesten",
  	before [;
    	 default:
       	    "~Jeg er en enhjørning, ikke en hest!~ fnyser enhjørningen fornærmet.";
  	],
 has 	uter concealed static;

Object 	stearinlys "enligt stearinlys" room_labyrint_6
 with 	name 'stearinlys' 'stearinlyset' 'kalenderlys' 'kalenderlyset' 'lys' 'lyset',
        description "Det er et kalenderlys brændt næsten ned til den 24.",
  	short_name_def "kalenderlyset",
  	invent "et kalenderlys",
        after [;
         Drop: "Du sætter meget forsigtigt lyset ned, så det ikke går ud.";
        ],
  	before [;
         Burn, SwitchOn: "Lyset brænder allerede meget smukt.";
    	 Blow, SwitchOff: "Lyseslukker! Du vælger ikke at slukke sådan et smukt
	    lys.";
    	 Take:
            if (CanCarryMore()) {
                if (IsHeld(self) == false) {
                    give self ~concealed;
                    move self to player;
                    "Du tager med rystende hånd lyset op i dine hænder og passer på,
                    at det ikke går ud.";
                }
            }
  	],
 has 	neuter concealed;

Object 	baal "bål" room_labyrint_9
 with 	name 'utændt' 'utændte' 'bål' 'bålet' 'kviste' 'kvist' 'kvistene' 'gren' 'grene' 'grenene' 'bålplads' 'bålpladsen',
  	short_name_def "bålet",
        burned false,
  	before [;
    	 Examine: if (self.burned == false) "Labyrintgangen mod øst er udfyldt af et
            kæmpe bjerg af kviste og grene, der strækker sig fra gulv til loft og
            fra væg til væg."; else "Det er de
             sørgelige rester af et mægtigt bål.";
         SwitchOff:
            if (self.burned) "Du sparker lidt til de sidste gløder af bålet.";
            "Det er nemt at slukke et utændt bål. Voilà.";
    	 Burn, SwitchOn:
       	    if (stearinlys notin player) "Du har ikke noget at tænde det med.";
            self.burned = true;
            remove stearinlys;
       	    "Du smider kalenderlyset ind i det utændte bål. En lille kvist
            antændes og ilden spreder sig hurtigt, så hele bålet står i fuld
            flamme. Du nyder lyset og varmen og venter til bålet er brændt ud.
            Du kan nu bevæge dig videre i labyrinten mod øst.";
  	],
 has 	neuter static switchable concealed;

Object 	stenskred "stenskred" room_labyrint_11
 with 	name 'stenskred' 'stenskredet' 'sten' 'stenene' 'lavasten' 'lavastenene' 'lava' 'lavaen',
  	short_name_def "lavastenene",
        appeared false,
        digged false,
  	before [;
    	 Examine: "Lavastenene udsender et pulserende rødt skær og sort røg
            står op fra sprækker i stenene.";
    	 Xyzzy:
       	    "Her virker magi desværre ikke. En mere lavpraktisk løsning, som
            en skovl ville dog være behjælpelig.";
         Attack:
            "Du prøver at sparke til stenene, men de flytter sig slet ikke.
            Varmen fra lavastenene gør, at du må træde tilbage for ikke at
            brænde dine øjenbryn af.";
    	 Dig, Remove, Take, Skovl:
            if (self.digged) "Lavastenene er sikkert meget interessante for en
            geolog-nisse, men nu da du kan komme forbi lavastenene, behøver du
            ikke studere sten længere.";
            if (skovl notin player && spade notin player) "Med dine bare fingre?
 Så ville du brænde nallerne.";
            if ((spade in player && skovl notin player) || (second == spade && spade in player)) "Du prøver at skovle
            lavasten med spaden, men du har brug for en skovl til sådan en
            opgave.";
            if ((skovl in player) || (second == skovl && skovl in player)) {
                self.digged = true;
       		"Du skovler og skovler, lavastenene væk. Efter noget tid har
                du skabt en smal sti, som du kan bevæge dig syd ad.";
    	    } <<Examine self>>;
    	 default: <<Examine self>>;
  	],
 has 	neuter concealed static;

Object kilde "magisk kilde" room_labyrint_8
 with 	name 'kilde' 'kilden' 'magisk' 'magiske',
        description "Fra den magiske kilde strømmer en dybrød væske.",
  	short_name_def "kilden",
        before [;
         Drink, Taste, Eat: <<Taste gloegg>>;
        ],
 has 	uter;

Object 	gloegg "gløgg" room_labyrint_8
 with 	name 'væske' 'væsken' 'rød' 'røde' 'gløgg' 'gløggen' 'glögg' 'glöggen',
  	short_name_def "gløggen",
        examined false,
        tasted false,
  	before [;
         Examine:
            self.examined = true;
            "Den dampende røde væske dufter af gløgg ... Det ER gløgg!";
    	 Eat, Taste, Drink:
            if (self.examined == false) {
                print "Du bukker dig ned mod kilden.
                Væsken, der strømmer fra den, damper og udsender en
                    genkendelig duft ... af gløgg ... Der ER gløgg!";
            } else {
                print "Du bukker dig ned mod kilden og drikker af den.";
            }
            if (self.tasted == false) {
                self.tasted = true;
                player.magic = player.magic + 1;
                print " Den varme gløgg fylder dig med en magisk følelse
                    indeni.^^[Du får 1 magi-point og har nu ";
	        print player.magic;
                ".]";
            } else {
                " Du kan næsten ikke få nok af den dybrøde drik, men husk du
                har en labyrint at navigere rundt i.";
            }
    	 Take: "Det er meget svært at samle en strømmende, varm væske op.";
  	],
 has 	pluralname static edible concealed;

Object 	aebleskiver "æbleskiver" room_labyrint_11
 with 	name 'æbleskive' 'æbleskiver' 'æbleskivene' 'skiver' 'skiverne' 'skive',
        description "Æbleskiver i hundredevis hænger rygende på
            æbleskivetræets tunge grene. De er gyldne og rygende varme.",
  	short_name_def "æbleskiverne",
        tasted false,
  	before [;
  	 Taste, Eat, Take:
            if (self.tasted == false) {
                self.tasted = true;
	        player.magic = player.magic + 1;
	        print "Du spiser én æbleskive, du spiser to. Du føler en
	            magisk kraft vokse inden i dig. Du spiser en æbleskive
	            til og bliver ved og ved, til du ikke kan klemme flere ned.^^[Du får 1 magi-point og har nu ";
	        print player.magic;
	        ".]";
            } else {
                "Du kan næsten ikke klemme flere æbleskiver ned, men kun
                næsten, så du spiser én til.";
            }
	    remove aebleskiver;
  	],
 has 	pluralname concealed edible;

Object 	aebleskivetrae "æbleskivetræ" room_labyrint_11
 with 	name 'træ' 'træet' 'gren' 'grene',
  	short_name_def "æbleskivetræet",
  	before [;
         Examine:
            "Et mægtigt æbleskivetræ af en meget sjælden sort.";
  	 Eat, Taste:
	    <<Eat aebleskiver>>;
         default:
            "Hvad med en æbleskive til?";
  	],
 has 	neuter scenery;

Object 	skilt12 "skilt" room_labyrint_12
 with 	name 'skilt' 'skiltet',
        description [;
	    print "På skiltet står skrevet med skrifttypen Comic Sans: ~Vær
		hilset, skiltelæser! Vi håber, at du havde en god og
		spændende oplevelse i labyrinten (og at du fik smagt
		lidt på sagerne). Dit unikke nisseID er: ~";
 	    PrintTextArrayOffsetLength(UUID_ARRAY,8,36);
	    "~. På skiltet er der desuden tegnet en pil, som peger
	    op, hvorunder der står ~UDGANG~.";
	],
  	short_name_def "skiltet",
  	sign_read false,
  	after [;
    	 Examine:
      	    if (self.sign_read == false) { GetBonus(64); self.sign_read = true; }
    	],
 has    neuter static;

! ============================================================================ !
!Include "room_sovevaerelset.inf";
Room  room_sovevaerelset "Soveværelset" ! Room 7
    with   description [;
               if (foraeldre in room_sovevaerelset) {
                   "I soveværelset er der en dobbeltseng og et par
		   natborde. Familiens forældre ligger i sengen og
		   sover lystigt. Dette er ikke just noget, som
		   bringer risengrød i din skål oppe på loftet.";
	       } else {
		   "I soveværelset er der en uredt dobbeltseng og et
		   par natborde. Derudover er her ikke meget at
		   komme efter.";
	       }
           ],
    name 'soveværelse' 'soveværelset',
    external_name 'soveværelse' 'soveværelset',
    w_to room_foerstesalen,
    out_to room_foerstesalen,
    cant_go "Du kan kun gå ud på førstesalen her fra.",
    has    light;

Object  foraeldre "forældre" room_sovevaerelset
 with   name 'forældre' 'forældrene' 'mennesker' 'menneskerne' 'menneskene',
        before [;
         Dril: "At drille er det du er bedst til, men du må finde ud af, hvordan du vil drille.";
	 Examine:
	    if (self in room_sovevaerelset) {
                "Forældrene er udprægede B-mennesker og sover
		virkelig tungt.";
            }
            if (self in room_spisestuen) {
                "Forældrene sidder ved spisebordet og er ved at spise
                morgenmad. De taler om, hvordan de glæder sig til juleaften,
                men de nævner ikke dig med ét ord.";
            }
	 WakeOther:
	    "Det er lettere sagt end gjort. De sover virkelig tungt.";
         default:
            <<Examine self>>;
        ],
 has    animate scenery static pluralname;

Object  dobbeltseng "dobbeltseng" room_sovevaerelset
 with   name 'dobbeltseng' 'dobbeltsengen' 'seng' 'sengen',
        short_name_def "dobbeltsengen",
        description [;
               if (foraeldre in room_sovevaerelset) {
                   "Dobbeltsengen er optaget af to stk. sovende forældre.";
	       } else {
		   "Dobbeltsengen er uredt, men bortset fra det ser den ret indbydende ud.";
	       }
	],
        before [;
	 Enter, Climb:
	    if (foraeldre in room_sovevaerelset) "Sengen er desværre optaget
	    af to sovende mennesker."; "Selvom sengen ser indbydende ud, så
                kommer man ikke sovende til risengrød.";
         Push, Touch:
            "Sengen er stor og tung og ikke sådan at flytte.";
        ],
 has    uter scenery enterable;

Object	natbord "natbord" room_sovevaerelset
 with 	name 'natbord' 'natbordet' 'natborde' 'natbordene' 'bord' 'bordet' 'borde' 'bordene',
        short_name_def "natbordet",
        description  [;
            print "Der står et natbord på hver side af dobbeltsengen.";
            if (bog in natbord || vandglas in natbord) {
		print " På det ene natbord er der";
                if (bog in natbord) print " en bog";
		if (bog in natbord && vandglas in natbord) print " og
 på det andet";
		if (vandglas in natbord) print " et vandglas";
		".";

            } else {
                ".";
            }
       	],
 has  	neuter supporter scenery;

Object  -> -> vandglas "glas vand"
 with   name 'vandglas' 'vandglasset' 'glas' 'glasset' 'vand' 'vandet',
        short_name_def "vandglasset",
        empty false,
        description [;
            if (self.empty) "Vandglasset er lige så tomt som din mave.";
            else "En glas med vand i. Der er små luftbobler på glassets inderside.";
        ],
        after [;
         Take: give self ~concealed;
        ],
        before [;
         Drop, PutOn, ThrowAt, Insert:
            if (second ~= natbord && player in room_sovevaerelset) {
		if (foraeldre in room_sovevaerelset) {
                    self.empty = true;
                    print "Du tager glasset og hælder langsomt vandet ud over
                        forældrenes ansigter. Dét får dem til at vågne og
                        sætte sig forfærdede op i sengen. Efter de har sundet
                        sig lidt, bliver de enige om at gå stå op og går ud af
                        rummet. ";
		    if (roomscores-->11 < roommaxscores-->11) move foraeldre to	room_spisestuen; else remove foraeldre;
		    move self to natbord;
            	    print "Du sætter vandglasset tilbage på bordet, hvor du fandt det.^";
		    GetPoint(7, 2);
		    rtrue;
		}
            }
            if (player in room_sovevaerelset) {
                give self concealed;
                move self to natbord;
                if (self.empty) "Glasset er tomt. Du sætter glasset igen.";
                "Du sætter vandglasset tilbage på bordet, hvor du fandt det.";
            }
         Taste, Eat, Drink:
            "Vandet ser alt andet end friskt ud.";
        ],
 has    neuter edible concealed;

Object  -> -> bog "bog"
 with   name 'bog' 'bogen' 'roman' 'romanen',
        short_name_def "bogen",
        before [;
         default:
	    "Det er en novellesamling med titlen ~Døde Blomster~. Det er nok
            ikke interessant læsning for nisser eller andre husvætter.";
        ],
 has    uter scenery static;

! ============================================================================ !
!Include "room_spisestuen.inf";
Room  room_spisestuen "Spisestuen" ! Room 11
 with   description [;
	    print "I spisestuen står et stort spisebord. ";
	    <<Examine spisebord>>;
	],
        name 'spisestue' 'spisestuen',
        external_name 'spisestue' 'spisestuen',
        n_to room_entreen,
        out_to room_entreen,
        cant_go "Du kan kun gå ud i entreen her fra.",
 has    light;

Object  familie "familie" room_spisestuen
 with   name 'familie' 'familien',
        short_name_def "familien",
        description [;
	    if (foraeldre in room_spisestuen || boern in room_spisestuen) {
            	if (foraeldre in room_spisestuen && boern in room_spisestuen) print "Børnene og forældrene";
	    	if (foraeldre notin room_spisestuen && boern in room_spisestuen) print "Børnene";
	    	if (foraeldre in room_spisestuen && boern notin room_spisestuen) print "Forældrene";
		" sidder rundt om bordet og spiser deres morgenmad. De ser ud
 til at nyde det, indtil videre.";
            } else {
                "Familien er endnu ikke kommen ned for at spise morgenmad.";
            }
        ],
 has    pluralname scenery;


Object  spisebord "spisebord" room_spisestuen
 with   name 'spisebord' 'spisebordet' 'bord' 'bordet',
        short_name_def "spisebordet",
        examined false,
        description [;
            print "Spisebordet er dækket op til morgenmad til hele
		familien. De dybe tallerkener er fyldt op med
		havregryn.";
 	    if (saltboesse in room_spisestuen ||
		peberboesse in room_spisestuen ||
		chili in room_spisestuen) {
                if (self.examined == false) {
                    self.examined = true;
                    print " Der står nogle krydderier på bordet";
                } else {
		    print " På bordet står også forskellige krydderier (";
		    if (saltboesse in room_spisestuen &&
		        peberboesse in room_spisestuen &&
		        chili in room_spisestuen) print "en saltbøsse, en
 		        peberbøsse og noget chili)";
		    if (saltboesse notin room_spisestuen &&
		        peberboesse in room_spisestuen &&
		        chili in room_spisestuen) print "en
 		        peberbøsse og noget chili)";
		    if (saltboesse in room_spisestuen &&
		        peberboesse notin room_spisestuen &&
		        chili in room_spisestuen) print "en saltbøsse og noget chili)";
		    if (saltboesse in room_spisestuen &&
		        peberboesse in room_spisestuen &&
		        chili notin room_spisestuen) print "en saltbøsse
 		        og en peberbøsse)";
		    if (saltboesse in room_spisestuen &&
		        peberboesse notin room_spisestuen &&
		        chili notin room_spisestuen) print "en saltbøsse)";
		    if (saltboesse notin room_spisestuen &&
		        peberboesse in room_spisestuen &&
		        chili notin room_spisestuen) print "en peberbøsse)";
		    if (saltboesse notin room_spisestuen &&
		        peberboesse notin room_spisestuen &&
		        chili in room_spisestuen) print "noget chili)";
	        }
            }
	    print ". ";
            if (foraeldre in room_sovevaerelset && tallerkener.eaten == false) print "Forældrene sover stadig, så de må have
            dækket op allerede i går inden de gik i seng. ";
	    if (foraeldre in room_spisestuen || boern in room_spisestuen) {
            	if (foraeldre in room_spisestuen && boern in room_spisestuen) print "Børnene og forældrene";
	    	if (foraeldre notin room_spisestuen && boern in room_spisestuen) print "Børnene";
	    	if (foraeldre in room_spisestuen && boern notin room_spisestuen) print "Forældrene";
		" sidder rundt om bordet og spiser deres morgenmad.";
	    } else {
		if (tallerkener.eaten == false) "Forældrene og
		børnene kommer nok snart sultne ned for at spise
		morgenmad."; else "";
	    }
            ],
 has    neuter static supporter concealed;

Object  krydderier "krydderier" room_spisestuen
 with   name 'krydderier' 'krydderierne',
        short_name_def "krydderierne",
        before [;
         Examine:
            if (saltboesse in room_spisestuen ||
		peberboesse in room_spisestuen ||
		chili in room_spisestuen) {
                print "Der er ";
                if (saltboesse in room_spisestuen &&
                    peberboesse in room_spisestuen &&
                    chili in room_spisestuen) print "både salt, peber og chili på bordet.";
                if (saltboesse notin room_spisestuen &&
                    peberboesse in room_spisestuen &&
                    chili in room_spisestuen) print "både peber og noget chili på bordet.";
                if (saltboesse in room_spisestuen &&
                    peberboesse notin room_spisestuen &&
                    chili in room_spisestuen) print "både salt og noget chili på bordet.";
                if (saltboesse in room_spisestuen &&
                    peberboesse in room_spisestuen &&
                    chili notin room_spisestuen) print "både salt og peber på bordet.";
                if (saltboesse in room_spisestuen &&
                    peberboesse notin room_spisestuen &&
                    chili notin room_spisestuen) print "salt på bordet.";
                if (saltboesse notin room_spisestuen &&
                    peberboesse in room_spisestuen &&
                    chili notin room_spisestuen) print "peber på bordet.";
                if (saltboesse notin room_spisestuen &&
                    peberboesse notin room_spisestuen &&
                    chili in room_spisestuen) print "noget chili på bordet.";
                print "^";
                rtrue;
            }
            print "kun lidt saltkorn på bordet.";
            rtrue;
        ]
 has    pluralname scenery;

Object  tallerkener "tallerkener" room_spisestuen
 with   name 'tallerkener' 'tallerkenerne' 'havregryn' 'havregrynene'
 'morgenmad' 'morgenmaden' 'havre' 'havren' 'havregrød' 'havregrøden'
 'gryn' 'grynene' 'mad' 'maden',
        short_name_def "tallerkenerne",
        description [;
            print "Dybe tallerkener med havregryn.";
            if (tallerkener.spicy == 1) " Med havsalt.";
            if (tallerkener.spicy == 2) " God pebret.";
            if (tallerkener.spicy == 3) " Godt med salt og peber på.";
            if (tallerkener.spicy > 3) " Spicy!";
            " Klar til at blive spist.";
        ],
        spicy 0,
        eaten false,
        before [;
         Take, Taste:
            "Det er havregryn, ikke engang havregrød og slet ikke
	    risengrød. Ægte nisser spiser helst risengrød.";
        ]
 has    pluralname static edible concealed;

Object  saltboesse "saltbøsse" room_spisestuen
 with   name 'saltbøsse' 'saltbøssen' 'salt' 'salten',
        short_name_def "saltbøssen",
        description "En saltbøsse i lyst træ.",
        before [;
         PutOn, ThrowAt, Insert:
            if (second == tallerkener) {
		move self to room_spisestuen;
		if (tallerkener.spicy >=3) "Du hælder en masse
	      	salt ud over de nu uspiselige havregryn. ";
		if (tallerkener.spicy == 1) "Du tilsætter
		    endnu mere salt til havregrynene og stiller
		    saltbøssen tilbage på bordet. ";
		if (tallerkener.spicy == 0 || tallerkener.spicy == 2) {
		    tallerkener.spicy = tallerkener.spicy + 1;
		    print "Du tilsætter salt til havregrynene --
			riiigeligt med salt -- og stiller saltbøssen
			tilbage på bordet. ";
		}
		if (roomscores-->11 < (roommaxscores-->11)-1) {
		    if (foraeldre in room_spisestuen || boern in room_spisestuen) {
		    	print "Menneskene sidder og snakker om, at de glæder
		    	    sig til i aften, men de nævner ikke dig med ét
		    	    ord. De taler om at havregrynene er lidt salte.^";
		    }
		} else {
		    if (foraeldre in room_spisestuen || boern in room_spisestuen) {
			if (foraeldre in room_spisestuen) remove foraeldre;
			if (boern in room_spisestuen) remove boern;
			tallerkener.eaten = true;
		    print "Du venter, og nu tager menneskene deres næste
                            skefuld havregryn og efter 3 sekunders tyggen,
                            spytter de synkront morgenmaden ud og hoster og
                            hakker oven på den eksplosive kulinariske
                            overraskelse.^";
		    } else {
			print "Når menneskene vågner og skal spise deres
		    	    morgenmad, så skal de nok få sig en krydret
		    	    kulinarisk oplevelse.^";
		    }
		}
		!print "^";
		GetPoint(11, 1);
		rtrue;
            }
	    rtrue;
	 Eat, Taste:
	    "Du putter lidt salt i munden, måske kan det hjælpe på
	    din sult? Næh, nu er bare meget tørstig.";
        ],
        after [;
         Take: give self ~concealed;
         Drop: if (player in room_spisestuen) give self concealed;
        ],
 has    uter concealed edible;

Object  peberboesse "peberbøsse" room_spisestuen
 with   name 'peberbøsse' 'peberbøssen' 'peber' 'peberen',
        short_name_def "peberbøssen",
        description "En peberbøsse i mørkt træ.",
        before [;
         PutOn, ThrowAt, Insert:
            if (second == tallerkener) {
		move self to room_spisestuen;
		if (tallerkener.spicy >=3) "Du hælder en masse
		peber ud over de nu uspiselige havregryn. ";
		if (tallerkener.spicy == 2) "Du tilsætter
		    endnu mere peber til havregrynene og stiller
		    peberbøssen tilbage på bordet. ";
		if (tallerkener.spicy == 0 || tallerkener.spicy == 1) {
		    tallerkener.spicy = tallerkener.spicy + 2;
		    print "Du tilsætter peber
		    	til havregrynene -- riiigeligt med peber -- og
		    	stiller peberbøssen tilbage på bordet. ";
		    if (roomscores-->11 < (roommaxscores-->11)-2) {
 			if (foraeldre in room_spisestuen || boern in room_spisestuen) {
			    print "Menneskene sidder og snakker om, at de glæder
		    	    	sig til i aften, men de nævner ikke dig med ét
		    	    	ord. De nævner at havregrynene smager lidt pebret.^";
		    	}
		    } else {
		    	if (foraeldre in room_spisestuen || boern in room_spisestuen) {
		    	    if (foraeldre in room_spisestuen) remove foraeldre;
		    	    if (boern in room_spisestuen) remove boern;
			    tallerkener.eaten = true;
		    print "Du venter, og nu tager menneskene deres næste
                                skefuld havregryn og efter 3 sekunders tyggen,
                                spytter de synkront morgenmaden ud og hoster
                                og hakker oven på den eksplosive kulinariske
                                overraskelse.^";
		    	} else {
		    	    print "Når menneskene vågner og skal spise deres
		    	    morgenmad, så skal de nok få sig en krydret
		    	    kulinarisk oplevelse.^";
		    	}
		    }
		    !print "^";
		    GetPoint(11, 2);
		    rtrue;
            	}
	    }
	    rtrue;
	 Eat, Taste:
	    "Du putter lidt peber i munden, måske kan det hjælpe på
	    din sult? Næh, nu hoster du bare.";
        ],
        after [;
         Take: give self ~concealed;
         Drop: if (player in room_spisestuen) give self concealed;
        ],
 has    uter concealed edible;

Object  chili "dåse chili" room_spisestuen
 with   name 'chili' 'chilien' 'chilipeber' 'chilipeberen' 'dåse' 'dåsen',
        short_name_def "chilien",
        description "En dåse chili. På dåsen står: ~Den stærkeste i
	    verden med en vindstyrke på firs trilliarder og 1.569.301
	    Scoville-enheder.~",
        before [;
         PutOn, ThrowAt, Insert:
            if (second == tallerkener) {
		move self to room_spisestuen;
		if (tallerkener.spicy >=3) {
                    tallerkener.spicy++;
                    "Du hælder en masse chili ud over de nu uspiselige havregryn. ";
                }
		tallerkener.spicy = tallerkener.spicy + 3;
                print "Du hælder chili i alle tallerkenerne -- riiigeligt med
                    chili -- og stiller dåsen med chili tilbage på bordet. ";
		if (foraeldre in room_spisestuen || boern in room_spisestuen) {
		    if (foraeldre in room_spisestuen) remove foraeldre;
		    if (boern in room_spisestuen) remove boern;
		    tallerkener.eaten = true;
		    print "Du venter, og nu tager menneskene deres næste
                        skefuld havregryn og efter 3 sekunders tyggen, spytter
                        de synkront morgenmaden ud og hoster og hakker oven på
                        den eksplosive kulinariske overraskelse.^";
		} else {
		    print "Når menneskene vågner og skal spise deres
		    morgenmad, så skal de nok få sig en eksplosiv
		    kulinarisk oplevelse.^";
		}
		!print "^";
		GetPoint(11, 3);
		rtrue;
            }
	 Eat, Taste:
	    "Du putter lidt chili i munden, måske kan det hjælpe på din sult?
            Efter nogle sekunder bliver dit ansigt mere og mere rødt, og du
            begynder at svede og så græde. Du har helt glemt, hvor sulten du
            var, men sulten vender langsomt tilbage, som chilien damper af.";
        ],
        after [;
         Take: give self ~concealed;
         Drop: if (player in room_spisestuen) give self concealed;
        ],
 has    uter concealed edible;

! ============================================================================ !
!Include "character_player.inf";
! Configuring the Player Character (PC) Cf. http://www.firthworks.com/roger/informfaq/oo.html
Object  mySelfobj "(self object)"
 with   name 'dig' 'selv' 'mig' 'du' 'nisse' 'nisser',
        short_name  [;
            return L__M(##Miscellany, 18);
        ],
        capacity MAX_CARRIED,
       	magic 0,
        examined false,
        before [;
         Examine:
            if (self.examined == false) {
                self.examined = true;
                "Menneskene i huset kalder dig ~Nissen på loftet~, selvom det
                er længe siden, de har tænkt på dig sidst. Du er en
                drillenisse med stort D og bærer med stolthed din
                traditionelle nissedragt: røde sokker, grønne bukser og bluse,
                og på dit hoved sidder en rød hue.";
            }
            print "Du ligner dig selv med dine røde sokker og grønne bukser og
                bluse. På dit hoved sidder en rød hue. ";
            if (TotalScore() < 2) "Du er umanerlig sulten efter risengrød.";
            if (TotalScore() < OK_SCORE) "Du er meget sulten efter risengrød,
            men at være en lille drillenisse, får dig til at glemme det lidt.";
            if (TotalScore() >= OK_SCORE) "Du er meget sulten efter risengrød
            og bliver mere og mere sulten efter den risengrød, som du kan
            lugte.";
         Touch: "Du mærker efter og har en underlig blandet følelse af sult og
            fjern juleglæde.";
         Kiss, Squeeze: "Du er en nisse med et højt selvværd.";
	 Disrobe: "Det er ikke særlig varmt, så du beholder tøjet
	    på.";
	 Sing: "Du nynner en lille julemelodi.";
        ]
 has    animate concealed proper transparent;

Object  -> toej "nissedragt"
 with   name 'tøj' 'tøjet' 'bukser' 'bukserne' 'bluse' 'blusen' 'hat'
 'hatten' 'hue' 'huen' 'nissehue' 'nissehuen' 'nissedragt'
 'nissedragten' 'dragt' 'dragten' 'nissetøj' 'nissetøjet' 'sokker',
        short_name_def "nissedragten",
	invent "din røde nissehue og din nissedragt",
	before [;
	 Examine:
            "Den traditionelle nissedragt: røde sokker, grønne
	    bukser, grøn bluse. På dit hoved sidder en rød hue.";
	 Disrobe, Drop, Remove:
	    if (oliefyr.hot && player in room_kaelderen) "Pyha, det
	    er blevet så varmt i kælderen nu, at du med glæde smider
	    alt dit nissetøj for at nyde varmen. Der er intet bedre end
	    et saunabesøg på denne årstid. Efter lidt tid tager du
	    tøjet på igen."; "Det er lidt koldt
	    i de fleste rum i huset på denne årstid, så du beholder
	    nissehue og nissetøj på.";
         Take: "Hvis du mener dit nissetøj, så har du det allerede på.";
	],
 has    pluralname concealed scenery clothing worn;



! ============================================================================ !
!Include "initialise.inf";
[ Initialise;
    StartDaemon(risengroeddaemon);
    StartDaemon(kat);
    StartDaemon(rotter);
    remove risengroed;
    remove rotter;
    remove staalrist;
    remove enhjoerning;

    lookmode = 2; ! 1=BRIEF mode, 2=VERBOSE mode, 3=SUPERBRIEF mode

    player = mySelfobj;
    location = room_loftet;
    thedark.short_name = "Kælderen";

!    print "På loftet sidder nissen med sin julegrød,^sin julegrød, så god og
!        sød,^han nikker, og han spiser, og han er så glad^for julegrød er hans
!        bedste mad.^^-- Skandinavisk julesang, tekst: Margrethe Munthe
!        (1911)^^";!

    print "^Du vågner langsomt, og det første du mærker er, at du
	stadig er sulten -- efter risengrød. Det er efterhånden fire
	lange uger siden, at der blev stillet grød herop. I aften er
	det juleaften, og derfor er det på høje tid, at du minder
	menneskene om din eksistens.^^";

    Banner();

    print "^Skriv HJÆLP for hjælp eller OM for information om dette
        teksteventyr.^";

    return 2;
];

 Object  risengroeddaemon
    with  daemon [;
              risengroed.daemon();
          ];

[ AboutSub;
    print "Dette er noget så sjældent som et teksteventyr på dansk, og det er
        samtidig mit første forsøg på at skrive et sådant, her 40 år efter
        genren opstod i midten af 1970'erne, som en af de tidligste former for
        computerspil.^^";
    print "Jeg opdagede teksteventyr som 10-årig på min hjemmecomputer, hvor
        spil som Colossal Cave Adventure og Zork var både fascinerende og
        frustrerende at spille. Fascinerende, fordi ~Computeren forstår, hvad
        jeg skriver!~, og frustrerende, fordi ~Computeren tit /ikke/ forstår,
        hvad jeg skriver!~, og fordi mit engelske ordforråd var lille. Nogle
        få dansksprogede teksteventyr udkom i sidste halvdel af 1980'erne, men
        jeg fik desværre ikke fingre i dem, og grafikspillene havde ærgerligt
        nok fanget al min interesse.^^Med dette teksteventyr har jeg prøvet at
        skrive et teksteventyr som jeg tror, jeg ville have syntes om som
        barn, og samtidig håber jeg, at det kan more nutidens både unge og
        gamle generationer.^^";
    print "Temaet for teksteventyr er valgt således, fordi nissen, som det
        specielle skandinaviske væsen det er, i høj grad har fortjent at være
        hovedpersonen i et teksteventyr.^^";

    print "Med teksteventyrlige hilsener,^Thomas Bøvith, december 2016.^^";

    print "Skriv til mig: "; Bold("thomas(snabel-a)teksteventyr.dk^^");

    print "Tak til Jacob Gunness, Fredrik Ramsberg, Eirik Krogstad, Jason
        Scott og Bobby Ågren.^";
    print "Beta-testerne har også fortjent en stor tak: Jacob Gunness,
        Torben Böckhaus, Kenneth Pedersen, Martin Olesen, Simon Kokkendorff,
        Anders T. Jensen og Noe Habermann.^";

    print "^~På loftet sidder nissen~ af Thomas Bøvith udgives under en
        ~Creative Commons Kreditering-DelPåSammeVilkår 4.0 International
        licens (CC BY-SA 4.0).~ http://creativecommons.org/licenses/by-sa/4.0/deed.da^^";
    print "Med andre ord: Dette teksteventyr er fri kultur og må kopieres,
        deles, ændres og udbygges frit med kildeangivelse og udgivelse på
        samme vilkår.^";

    print "^WWW : http://teksteventyr.dk/nissen";
    print "^IFDB: http://ifdb.tads.org/viewgame?id=";
    PrintTextArrayOffsetLength(TUID_ARRAY,8,16);
    print "^CASA: http://solutionarchive.com/game/id,7361/På+Loftet+Sidder+Nissen.html";
    print "^IFID: "; PrintTextArrayOffsetLength(UUID_ARRAY,8,36);
    print "^";
    rtrue;
];

[ AboutHelpSub;
    print "Du har startet et teksteventyr, og du ser nu noget tekst og en
        blinkende markør. Hvis du ikke helt ved, hvordan du kommer videre, så
    læs videre her:^^";
    Bold("1) "); print "Læs teksten omhyggeligt. Beskrivelser af steder, ting
        eller personer giver oftest gode ledetråde til, hvad der ville være
        logisk at gøre.^";
    Bold("2) "); print "Brug dine sanser, din sunde fornuft og din fantasi.
        Se, lyt, lugt, smag og ikke mindst undersøg ting. Saml ting op og brug
        dem. Bevæg dig rundt.^";
    !Tag steder hen og prøv at tale med personer eller væsener.^";
    Bold("3) "); print "Tegn et kort over stederne du besøger og tag notater
        undervejs.^";
    Bold("4) "); print "Lær de mest almindelige kommandoer og deres
        forkortelser (se nedenfor).^";
    Bold("5) "); print "Teksteventyrets parser forstår et varieret sprog, men
        jo simplere og mere præcist du kan udtrykke dig, jo større er chancen
        for, at du bliver forstået.^";

    Bold("^SPECIFIK HJÆLP TIL ~PÅ LOFTET SIDDER NISSEN~");

    print "^Sådan kommer du hurtigt i gang:^Bevæg dig rundt ved at bruge
        kompasretningerne og OP/NED eller f.eks. GÅ NED I KÆLDEREN eller bare
        KÆLDEREN. Hvis et rum kun har én logisk udgang, kan du skrive UD for
        at komme ud. Du behøves ikke at forsøge at tale med menneskene -- de
        kan ikke rigtig høre eller se dig. Ellers er det bare at sige:
        UNDERSØG alt, KIG dig omkring, TAG ting, osv. (Se nedenfor for
        eksempler på, hvad du kan gøre). Du får point for alle ~gode~
        gerninger, og der er 24 point og et antal bonuspoint at få. God
        fornøjelse!^";

    Bold("^TEKSTEVENTYR-ORDBOGEN");
    print "^Eksempler på nogle af de mest almindelige kommandoer i teksteventyr:";
    print " GÅ NORD/ØST osv., GÅ OP/NED/IND/UD, UNDERSØG noget, f.eks.
        UNDERSØG COMPUTEREN (forkortet U COMPUTEREN eller X COMPUTEREN, se
        nedenfor). ";
    print "UNDERSØG MIG giver ofte en beskrivelse af dig selv. KIG/SE
        (forkortet K/L) giver en beskrivelse af rummet, du er i. STATUS
        (forkortet T/I) viser, hvad du bærer på.";
    print " TAG/SMID noget, ÅBN/LUK noget, TÆND/SLUK noget, SKUB/RØR noget,
        LÆG/HÆLD noget PÅ noget, SMAG/SPIS/DRIK noget, LUGT TIL noget, LÆS
        noget. Du kan også prøve LYT, LUGT, HOP, DANS, KYS, BED, VENT, SYNG,
        osv. Du kan fortryde en kommando med FORTRYD, gentage forrige kommando
        med IGEN, og derudover GENSTART, AFSLUT, GEM og HENT spillet. UDSKRIFT (SCRIPT)
        starter optagelse af spillet. TRANSKRIBER muliggør brug af ae, oe og
        aa for æøå.^";

    print "Find mere hjælp her: http://teksteventyr.dk/hjaelp-nissen^";

    Bold("^TEKSTEVENTYR-FORKORTELSER^");
    print "Det bliver hurtigt trættende at skulle skrive f.eks. UNDERSØG,
        STATUS og GÅ OP, så derfor findes der forkortelser for de mest
        anvendte kommandoer (de fleste almindelige engelske forkortelser
        virker også):^";
    Bold("N/S/Ø/V, N/S/E/W = "); print "NORD/SYD/ØST/VEST. ";
    Bold("U, X = "); print "UNDERSØG (eXamine på engelsk). ";
    Bold("K, L = "); print "KIG, SE (Look på engelsk). ";
    Bold("T, I = "); print "STATUS (Inventory på engelsk). ";
    Bold("G = "); print "IGEN. ";
    Bold("H = "); print "HJÆLP. ";
    Bold("Q = "); print "AFSLUT (Quit på engelsk). ";
    Bold("Z = "); print "VENT. ";
    print "^";
    rtrue;
];

! ============================================================================ !
Include "DanishG";
! ============================================================================ !
!Include "verbs.inf";
Verb meta 'om' 'info' 'about' 'credits' 'kredits' 'kolofon'
    *                                   -> About;

Verb meta 'hjælp' 'hint' 'help' 'h//'
    *                                   -> AboutHelp;

Verb 'hæld'
    * noun 'i//' noun                   -> PutOn
    * noun 'på' noun                    -> PutOn;

! room_kontoret
[ FormaterSub; "Jeg ved ikke helt, hvad du vil formatere."; ];
Verb 'formater'
    *                                   -> Formater
    * noun                              -> Formater;

[ KopierSub; "Jeg ved ikke helt, hvad du vil kopiere."; ];
Verb 'kopier'
    *                                   -> Kopier
    * noun                              -> Kopier;

[ OmdoebSub; "Jeg ved ikke helt, hvad du vil omdøbe."; ];
Verb 'omdøb'
    *                                   -> Omdoeb
    * noun                              -> Omdoeb;

[ ListSub; "Måske mente du noget i stil med ~status~?"; ];
!Verb 'list'
!    *                                   -> List
!    * noun                              -> List;
Extend 'list' last
    * noun                               -> List;

[ KrypterSub; "Jeg ved ikke helt, hvad du vil kryptere."; ];
Verb 'krypter' 'dekrypter'
    *                                   -> Krypter
    * noun                              -> Krypter;

[ SkiftSub; "Jeg ved ikke helt, hvad du vil skifte."; ];
Verb 'skift'
    *                                   -> Skift
    * noun                              -> Skift
    * noun 'ud'                         -> Skift;

[ SletSub; "Jeg ved ikke helt, hvad du vil slette."; ];
Verb 'slet'
    *                                   -> Slet
    * noun                              -> Slet;

[ DelSub; "Jeg ved ikke helt, hvad du vil dele."; ];
Verb 'del'
    *                                   -> Del
    * noun                              -> Del;

[ HackSub; "Jeg ved ikke helt, hvad du vil hacke."; ];
Verb 'hack'
    *                                   -> Hack
    * noun                              -> Hack;

[ CrackSub; "Jeg ved ikke helt, hvad du vil cracke."; ];
Verb 'crack'
    *                                   -> Crack
    * noun                              -> Crack;

[ SmileSub; "Dit ansigt lyser op imens du smiler og tænker på jul."; ];
Verb 'smil'
    *                                   -> Smile;

[ TisSub; if (player in room_badevaerelset) "Du får lige lidt privatliv, imens du
    bruger toilettet."; "Det er vist ikke det rette sted at gøre det."; ];
Verb 'tis' 'skid'
    *                                   -> Tis
    * 'på' noun                         -> Tis;

[ NikSub; "Du nikker lidt og tænker på risengrød."; ];
Verb 'nik' 'nikke'
    *                                   -> Nik;

[ XyzzySub; ];
Verb 'xyzzy'
    *                                   -> Xyzzy
    * noun                              -> Xyzzy;
Extend 'sig' first
    * 'xyzzy'                           -> Xyzzy;

[ PlughSub; "Så magisk er dette teksteventyr dog heller ikke!";];
Verb 'plugh'
    *                                   -> Plugh;


[ AendreSub; ];
Verb 'ændre'
    *                                   -> SetTo
    * noun                              -> SetTo;

[ AendrSub; ];
Verb 'ændr'
    *                                   -> SetTo
    * noun                              -> SetTo;

Extend 'luk' last
    * switchable                        -> SwitchOff;

! room_foerstesalen
Extend 'drej' first
    * noun 'frem'                       -> SkruFrem
    * noun 'tilbage'                    -> SkruTilbage;

Extend 'stil' first
    * noun 'frem'                       -> SkruFrem
    * noun 'tilbage'                    -> SkruTilbage;

Extend 'sæt' first
    * noun 'i//' 'stå'                    -> SwitchOff
    * noun 'frem'                       -> SkruFrem
    * noun 'tilbage'                    -> SkruTilbage;

Extend 'skru' first
    * noun 'frem'                       -> SkruFrem
    * noun 'tilbage'                    -> SkruTilbage
    * 'op' 'for' noun                   -> SkruOp
    * 'ned' 'for' noun                  -> SkruNed
    * noun 'op'                         -> SkruOp
    * noun 'ned'                        -> SkruNed;

[ SkruFremSub; "Jeg ved ikke helt, hvad du vil skrue."; ];
Verb 'skrufrem'
    *                                   -> SkruFrem
    * noun                              -> SkruFrem;

[ SkruTilbageSub; "Jeg ved ikke helt, hvad du vil skrue.";];
Verb 'skrutilbage'
    *                                   -> SkruTilbage
    * noun                              -> SkruTilbage;

[ SkruOpSub; "Jeg ved ikke helt, hvad du vil skrue."; ];
Verb 'skruop'
    *                                   -> SkruOp
    * noun                              -> SkruOp;

[ SkruNedSub; "Jeg ved ikke helt, hvad du vil skrue.";];
Verb 'skruned'
    *                                   -> SkruNed
    * noun                              -> SkruNed;

[ TegnPaaSub;  if (spraydaaser in player && noun == player) "Du har en naturlig skønhed, som SÅ meget
            make-up bare ville ødelægge."; "Jeg ved ikke helt, hvad du vil tegne."; ];
Verb 'tegn' 'mal' 'skriv' 'spray'
    *                                   -> TegnPaa
    * noun                              -> TegnPaa
    * 'med' noun                        -> Brug
    * 'med' noun 'på' noun              -> TegnPaa
    * 'på' noun                         -> TegnPaa
    * 'på' noun 'med' noun              -> TegnPaa;

[ LimSub; "Det ville nok grise en kende for meget."; ];
Verb 'lim'
    * noun                              -> Lim
    * noun 'fast'			-> Lim
    * noun 'på' noun			-> Lim
    * noun 'fast' 'på' noun		-> Lim;

[ BrugSub; "Jeg ved ikke helt, hvad du vil bruge."; ];
Verb 'brug'
    *                                   -> Brug
    * noun                              -> Brug
    * noun 'på'                         -> Wear
    * noun 'på' noun                    -> PutOn
    * clothing                          -> Wear;

[ SpilSub; "Jeg ved ikke helt, hvad du vil spille."; ];
Verb 'spil'
    *  'på' noun                        -> Spil
    *  noun                             -> Spil;

Extend 'gå' first
        * 'i//' enterable                     -> Enter;

Extend 'sov' first
    * 'i//' enterable                     -> Enter;

[ DansSub; "Du danser ~The Støvledans~, og glemmer at du er sulten."; ];
Verb 'dans'
    *                                   -> Dans;

[ TroSub; if (noun == player) "Tror du på nisser? Selvfølgelig gør du det!"; "Hvad tror du?"; ];
Verb 'tro'
    *                                   -> Tro
    * 'på' noun                         -> Tro;

[ TruSub; "Jeg ved ikk helt, hvem du vil true."; ];
Verb 'tru' 'true'
    *                                   -> Tru
    * noun                              -> Tru
    * 'med' noun                        -> Tru;

[ RaabSub; if (player in room_loftet && risengroed.announced && rotter.friendly == false) <<Tru rotter>>; else "Du råber lidt, men handling taler højere end ord."; ];
Verb 'råb' 'skrig'
    *                                   -> Raab
    * 'ad' noun                       -> Raab
    * 'til' noun                      -> Raab
    * noun                              -> Raab;

[ KlapSub; if (noun == player) "Du klapper dig selv velfortjent på ryggen."; "Du
    klapper i hænderne."; ];
Verb 'klap'
    *                                   -> Klap
    * noun                              -> Klap;

[ RidSub; "Jeg ved ikke helt, hvad du vil ride."; ];
Verb 'rid'
    *                                   -> Rid
    * noun                              -> Rid
    * 'på' noun                         -> Rid;

[ KomSub; "Jeg er ikke sikker på, hvad du vil."; ];
Verb 'kom'
    *                                   -> Kom
    * noun 'på'                         -> Wear
    * noun 'på' noun                    -> PutOn;

Extend 'kys' first
    * noun                     		-> Kiss;

Extend 'put' first
    * noun 'på' 			-> Wear;

Extend 'put' last
    * noun 'under' noun			-> PutOn;

Extend 'spis' last
    * noun 'med' noun			-> Taste;

Extend 'rør' last
    * noun 'med' noun			-> Touch;

Extend 'skub' last
    * 'til' noun 'med' noun             -> Touch;

Extend 'skub' last
    * 'til' noun 'med' noun             -> Touch;

Extend 'klatre' last
    * 'op' 'i//' noun                   -> Climb;

Extend 'hop' last
    * 'i//' noun                        -> Enter;

Extend 'fjern' first
    * worn                              -> Disrobe;

Extend 'fjern' last
    * noun 'med' noun                   -> Remove;

Extend 'tag' last
    * noun 'på'                         -> Wear
    * noun 'med' noun                   -> Remove;

Extend 'læs' first
    * openable                          -> Open;

Extend 'se' last
    * noun                              -> Examine
    * noun=ADirection 'gennem' noun     -> Examine
    * noun=ADirection 'ad' noun         -> Examine
    * 'gennem'/'igennem' noun           -> Examine
    * 'nedad' noun                      -> Examine
    * 'ned' ' ad' noun                  -> Examine
    * 'ud' 'ad'/'gennem'/'igennem' noun -> Examine;

Extend 'spark' last
    * 'til' noun                        -> Attack;

[ SkovlSub; "Jeg ved ikke helt, hvad du vil skovle."; ];
Verb 'skovl'
    * noun                              -> Skovl
    * noun 'med' noun                   -> Skovl;

[ DrilSub; if (noun == player) "Det lyder nok sjovere end det er."; if (noun hasnt
    animate) "Det giver ikke så meget mening at drille ting."; "At drille er det
    du er bedst til, men du må finde ud af, hvordan du vil drille."; ];
Verb 'dril'
    * noun                              -> Dril;
